!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!
      SUBROUTINE VOBSLYRS(lcolumnghr,lobsSpaceData)
#if defined (DOC)
*
***s/r  VOBSLYRS
*
*Author  : P. Koclas *CMC/AES  Sept 7 1994
*Revision:
*          P. Koclas *CMC/AES  February 1995
*          -Allow ROBDATA(NCM_XTR,JDATA)=0 when
*           level of data= Bottom layer of model (pressure case)
*
*          P. Koclas *CMC/AES  July 1995
*          -fix bugs
*          -( mobdata <---> robdata confusion)
*          -( IF ( NINT(ZLEV) .EQ. NINT(ZPB) ) THEN )
*          P. Koclas *CMC/AES  April 1996
*          - new definition of "NCM_LYR" to include reference levels of satem data
*          C. CHARETTE *CMC/AES OCT 1998
*          - Adapted for version on eta levels
*          C. CHARETTE *CMC/AES OCT 2000
*          - Adapted to process data with pressure vertical coordinate
*          D. Anselmo *ARMA/MSC OCT 2004
*          - Adapted to process atmospheric and surface ln specific humidity
**    Purpose:
*      Find which model levels to use for the vertical interpolation
*      of model fields to CMA data.
*
*Arguments
*
*       input:
*          none
#endif
      use EarthConstants_mod
      use MathPhysConstants_mod
      use obsSpaceData_mod
      use columnData_mod 
      use bufr
      IMPLICIT NONE
#include "comlun.cdk"
      type(struct_columnData) :: lcolumnghr
      type(struct_obs) :: lobsSpaceData

      INTEGER :: JK,JDATA,NLEV
      REAL(8) :: ZLEV,ZPT,ZPB
      INTEGER :: IOBS,IK,ITYP
      LOGICAL :: LLOK
      CHARACTER(len=2) :: cvar
      integer :: index_header, index_body
C
C-----------------------------------------------------------------------
C         --------
C           ETA
C         --------
C
C     1. Find where extrapolation is needed
C        ----------------------------------
C
C     1.1 PPP Vertical coordinate
C
         DO JDATA= 1,obs_numbody(lobsSpaceData)
            LLOK = ( (obs_elem_i(lobsSpaceData,'ASS ',JDATA) .EQ. 1     .OR.
     &                obs_elem_i(lobsSpaceData,'VNM ',JDATA) .EQ. BUFR_NEHU) .AND.
     &                obs_elem_i(lobsSpaceData,'VCO ',JDATA) .EQ. 2 )
            IF ( LLOK ) THEN
               IF(obs_elem_i(lobsSpaceData,'VNM ',JDATA) .NE. BUFR_NEDZ ) THEN
                  ZLEV = obs_elem_r8(lobsSpaceData,'PPP ',JDATA)
               ELSE
                  ZLEV = obs_elem_r8(lobsSpaceData,'PRL ',JDATA)
               ENDIF
               IOBS = obs_elem_i(lobsSpaceData,'OBS ',JDATA)
               ITYP = obs_elem_i(lobsSpaceData,'VNM ',JDATA)
               cvar = vnl_vartypeFromVarnum(ITYP)
               ZPT= col_getPressure(LCOLUMNGHR,1,IOBS,cvar)
               ZPB= col_getPressure(LCOLUMNGHR,COL_GETNUMLEV(LCOLUMNGHR,cvar),IOBS,cvar)
               IF ( ZLEV .LT. ZPT ) THEN
                  call obs_set_i(lobsSpaceData,'XTR ',JDATA,1)
               ELSE IF ( ZLEV .GT. ZPB ) THEN
                  call obs_set_i(lobsSpaceData,'XTR ',JDATA,2)
               ELSE
                  call obs_set_i(lobsSpaceData,'XTR ',JDATA,0)
               ENDIF
             ENDIF
         END DO
C
C     1.2 ZZZ Vertical coordinate
C
         DO JDATA= 1,obs_numbody(lobsSpaceData)
            LLOK = (obs_elem_i(lobsSpaceData,'ASS ',JDATA) .EQ. 1 .AND.
     &              obs_elem_i(lobsSpaceData,'VCO ',JDATA) .EQ. 1 )
            IF ( LLOK ) THEN
               IF(obs_elem_i(lobsSpaceData,'VNM ',JDATA) .NE. BUFR_NEDZ ) THEN
                  ZLEV = obs_elem_r8(lobsSpaceData,'PPP ',JDATA)
               ELSE
                  ZLEV = obs_elem_r8(lobsSpaceData,'PRL ',JDATA)
               ENDIF
               IOBS = obs_elem_i(lobsSpaceData,'OBS ',JDATA)
               ITYP = obs_elem_i(lobsSpaceData,'VNM ',JDATA)
               cvar = vnl_vartypeFromVarnum(ITYP)
               nlev=COL_GETNUMLEV(LCOLUMNGHR,cvar)
               ZPT= col_getHeight(lcolumnghr,1,IOBS,cvar)/RG
               ZPB= col_getHeight(lcolumnghr,NLEV,IOBS,cvar)/RG
               IF ( ZLEV .GT. ZPT ) THEN
                  call obs_set_i(lobsSpaceData,'XTR ',JDATA,1)
               ELSE IF ( ZLEV .LT. ZPB ) THEN
                  call obs_set_i(lobsSpaceData,'XTR ',JDATA,2)
               ELSE
                  call obs_set_i(lobsSpaceData,'XTR ',JDATA,0)
               ENDIF
            ENDIF
         END DO
C
C
C     2. FInd interpolation layer
C        ------------------------
C        (Model levels are assumed to be in increasing order in Mbs)
C        ...The SIGN and MAX intrinsincs are used for vectorization
C           purposes......
C
C     2.1  PPP Vertical coordinate
C
         DO JDATA=1,obs_numbody(lobsSpaceData)
            call obs_set_i(lobsSpaceData,'LYR ',JDATA,0)
         END DO
C
         DO JDATA= 1,obs_numbody(lobsSpaceData)
            LLOK = ( (obs_elem_i(lobsSpaceData,'ASS ',JDATA) .EQ. 1     .OR.
     &                obs_elem_i(lobsSpaceData,'VNM ',JDATA) .EQ. BUFR_NEHU) .AND.
     &                obs_elem_i(lobsSpaceData,'VCO ',JDATA) .EQ. 2 )
            IF ( LLOK ) THEN
               IOBS = obs_elem_i(lobsSpaceData,'OBS ',JDATA)
               ZLEV = obs_elem_r8(lobsSpaceData,'PPP ',JDATA)
               ITYP = obs_elem_i(lobsSpaceData,'VNM ',JDATA)
               IK = 1
               cvar = vnl_vartypeFromVarnum(ITYP)
               nlev=COL_GETNUMLEV(LCOLUMNGHR,cvar)
               DO JK = 2,NLEV - 1
                  ZPT = col_getPressure(LCOLUMNGHR,JK,IOBS,cvar)
                  IF( ZLEV .GT. ZPT ) IK = JK
               END DO
               ZPT = col_getPressure(LCOLUMNGHR,IK,IOBS,cvar)
               ZPB = col_getPressure(LCOLUMNGHR,IK+1,IOBS,cvar) 
               call obs_set_i(lobsSpaceData,'LYR ',JDATA, IK)
            ENDIF
         END DO
C
C     2.2  ZZZ Vertical coordinate and surface observations
C
         DO JDATA= 1,obs_numbody(lobsSpaceData)
            LLOK = ( (obs_elem_i(lobsSpaceData,'ASS ',JDATA) .EQ. 1     .OR.
     &                obs_elem_i(lobsSpaceData,'VNM ',JDATA) .EQ. BUFR_NEHS) .AND.
     &                obs_elem_i(lobsSpaceData,'VCO ',JDATA) .EQ. 1 )
            IF ( LLOK ) THEN
               IOBS = obs_elem_i(lobsSpaceData,'OBS ',JDATA)
               ZLEV = obs_elem_r8(lobsSpaceData,'PPP ',JDATA)
               ITYP = obs_elem_i(lobsSpaceData,'VNM ',JDATA)
               IK = 1
               cvar = vnl_vartypeFromVarnum(ITYP)
               nlev=COL_GETNUMLEV(LCOLUMNGHR,cvar)
               DO JK = 2,NLEV - 1
                 ZPT = col_getHeight(lcolumnghr,JK,IOBS,cvar)/RG
                 IF( ZLEV .LT. ZPT ) IK = JK
               END DO
               IF((ITYP.EQ.BUFR_NEPS .or. ityp .eq. BUFR_NEPN) ) THEN
                 IK=0
               ELSEIF(ITYP.EQ.BUFR_NETS .or. ityp .eq. BUFR_NESS .OR.
     &                ITYP.EQ.BUFR_NEUS .or. ityp .eq. BUFR_NEVS .OR.
     &                ITYP.EQ.BUFR_NEHS ) THEN
                 IK=nlev-1
               ENDIF
               call obs_set_i(lobsSpaceData,'LYR ',JDATA, IK)
            ENDIF
         END DO
C
      RETURN
      END

