!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!
      SUBROUTINE CMABDY(PVALUES,KLIST,KFLAGS,LDFLAG,PROFIL,LDERR,LDSAT, &
                        LDGO,LDAIRS,LDIASI,n_elements_in_block, &
                        n_levels_in_block,KNT,KNDAT,KVCORD,PVCORD, &
                        KINDEX,KIDTYP,PPMIS,nvcordtyp,vcordsf, &
                        vcordsf_dims,vconv,nonelev)
      use obsSpaceData_mod
      IMPLICIT NONE
!
      INTEGER, intent(out) :: KNDAT
      INTEGER, intent(in)  :: n_elements_in_block,n_levels_in_block,KNT
      INTEGER, intent(in)  :: KVCORD,KINDEX,KIDTYP
      INTEGER, intent(in)  :: KLIST(n_elements_in_block)
      INTEGER, intent(in)  :: KFLAGS(n_elements_in_block,n_levels_in_block,KNT)
      integer, intent(in)  :: nvcordtyp,nonelev
      integer, intent(in)  :: vcordsf_dims(2)
!
      REAL(kind=8),intent(in)::PVALUES(n_elements_in_block,n_levels_in_block,KNT)
      REAL(kind=8),intent(in)::PVCORD(n_levels_in_block)
      REAL(kind=8),intent(in)::PROFIL(n_levels_in_block)
      REAL(kind=8),intent(in)::PPMIS
      real(kind=8),intent(in)::vconv
                                        ! vertical coordinate parameters
                                        ! for surface data
      real(kind=8),intent(in)::vcordsf(vcordsf_dims(1),vcordsf_dims(2))
!
      LOGICAL, intent(in) :: LDFLAG,LDERR,LDSAT,LDGO,LDAIRS,LDIASI
!
#if defined (DOC)
!***********************************************************************
!
!***s/r CMABDY -FILL BODY OF CMA REPORT
!
!Author    . P. KOCLAS(CMC TEL. 4665)
!
!Revision:
!          . P. Koclas *CMC/AES Sept  1994: Add call to cvt3d
!          .   before insertion of U and V for consistency
!          . P. Koclas *CMC/AES February  1995:
!          .  New call sequence neccessary to :
!          . -allow insertion of "grouped data" records in BURP files.
!          . -allow data observed in various vertical coordinates
!          . -observation errors no longer initialized
!
!          . P. Koclas *CMC/AES March     1995:
!            -Additions for humsat and satem data
!          .
!          . C. Charette *ARMA Jan        2001
!            -Max value for T-Td surface element(12203)
!
!           JM Belanger CMDA/SMC  Feb 2001
!                   . 32 bits conversion
!          . P. Koclas *CMC/CMDA Sept     2001:
!            -set first-guess and observation errors to missing values
!
!          .N Wagneur CMDA/SMC  Jine 2002
!                   . -Additions for goes data
!          . P. Koclas *CMC/CMDA Dec      2003:
!                -conversion for surface wind
!          . C. Charette *ARMA/SMC Apr      2005:
!                -Set flag bit #12 (Element assimilated by analysis) to zero
!                 (see banco-burp documentation for more detail)
!          . A. Beaulne *CMDA/SMC  Aug 2006
!                     -Additions for AIRS data
!          . S. Heilliette
!                     -Additions for IASI data
!
!
!    PURPOSE : TRANSFER DATA BLOCKS EXTRACTED FROM CMC BURP FILES TO
!              THE IN-CORE FORMAT (CMA) OF THE 3-D VARIATIONAL ANALYSIS
!
!    ARGUMENTS:
!     INPUT:
!
!           -PVALUES : DATA BLOCK
!           -KLIST   : LIST OF BUFR ELEMENTS
!           -KFLAGS  : QUALITY CONTROL FLAGS
!
!           -LDFLAG  :  .TRUE. --> INSERT FLAGS IN CMA
!                      .FALSE. --> INSERT DUMMY VALUE(2**12)
!           -LERR    :  .TRUE. --> INSERT OBS ERROR IN CMA (HUMSAT DATA)
!           -LDSAT   :  .TRUE. --> INSERT REF PRESSURE IN CMA (SATEMS)
!           -LDGO    :  .TRUE. --> INSERT EMISSIVITIES IN CMA (GOES RADIANCES)
!           -LDAIRS  :  .TRUE. --> INSERT EMISSIVITIES IN CMA (AIRS RADIANCES)
!           -LDIASI  :  .TRUE. --> INSERT EMISSIVITIES IN CMA (IASI RADIANCES)
!
!           -n_elements_in_block  : NUMBER OF ELEMENTS IN DATA BLOCK
!           -n_levels_in_block    : NUMBER OF LEVELS IN DATA BLOCK
!           -KNT     :  THIRD DIMENSION OF DATA BLOCK
!           -KNDAT   :  THIRD DIMENSION OF DATA BLOCK
!           -KVCORD  :  BUFR ELEMENT CODE OF VERTICAL COORDINATE
!           -PVCORD  :  VERTICAL COORDINATE VALUES EXTRACTED FROM DATA BLOCK
!           -KINDEX  :  THIRD DIMENSION INDEX OF DATA BLOCK
!           -PPMIS   :  VALUE OF MISSING DATA
!           -VCONV   :  CONVERSION FACTOR FOR PRESSURE CO-ORDINATE
!
!    OUTPUT:
!           -KNDAT   : NUMBER OF DATA INSERTED IN CMA FILE
!
!***********************************************************************
#endif

#include "comcst.cdk"

      INTEGER ILEM,IND,IIND,IP,IK
      INTEGER IBAD,IFLAG
      INTEGER ielement,ilevel
      INTEGER ZESMAX,ZES
!
      REAL(kind=8) ZTORAD,ZFACT,padd,pmul,ZEMFACT,pvalue
!
!***********************************************************************
!     SET BAD FLAG VALUE IIND AND UNIT CONVERSION CONSTANTS
!***********************************************************************
!
      IIND  =-1
      IBAD=2**11
      ZTORAD=RPI/180.
!
      ZFACT=VCONV
!
      ZEMFACT=0.01
      ZESMAX=30.
!
      IP=obsSpaceData%numobs_recorded + 1
      IND=0
!
!***********************************************************************
!     PUT ALL NON MISSING DATA IN CMA FILE
!     EXIT IF THERE IS MORE DATA AVAILABLE THAN ALLOCATED TO CMA FILE
!     DATA IS CONVERTED TO UNITS USED BY 3D-VAR ANALYSIS.
!***********************************************************************
!
      IK= KINDEX
         DO ielement=1,n_elements_in_block
            ILEM=obs_ifind(KLIST(ielement))
            IF ( (ILEM .GT. 0) .AND. (KLIST(ielement) .NE. KVCORD) ) THEN
               DO ilevel=1,n_levels_in_block
                 if(pvcord(ilevel) .ne. ppmis .and. (nonelev .eq. -1 .or. &
                      nonelev .eq. nint(pvcord(ilevel)*zfact))) then
                        IF  ( PVALUES (ielement,ilevel,IK) .NE. PPMIS ) THEN
                           pvalue=PVALUES(ielement,ilevel,IK)
                           IF ( IP + IND .LE. obsSpaceData%mxobstotal ) THEN
!                                         VERTICAL COORDINATE
                              obsSpaceData%robdata8(NCM_PPP,IP+IND)=PVCORD(ilevel)*ZFACT +vcordsf(ilem,kidtyp)
!
!                             FOR PNM HEIGHT IS SET TO 0
!                             ----------------------------
                              IF ( ILEM .EQ. 53 ) THEN
                                 obsSpaceData%robdata8(NCM_PPP,IP+IND)=0.D0
                              ENDIF
!                             ----------------------------
!
!                             IF ( ILEM .EQ. 2 ) Units:  V
!                                          CONVERT TO GZ
                              IF ( ILEM .EQ. 3 ) THEN
                               pvalue=RG*pvalue
                              ENDIF
!                             IF ( ILEM .EQ. 4 ) Units: METERS
!                             IF ( ILEM .EQ. 8 ) Units:  CELSIUS
!                                    Max value T-Td upper air
                              IF ( ILEM .EQ. 9 ) THEN
                               IF ( pvalue .GT. ZESMAX) THEN
                                    pvalue=ZESMAX
                               ENDIF
                              ENDIF
!                                    Max value T-Td surface
                              IF ( ILEM .EQ. 11 ) THEN
                               IF ( pvalue .GT. ZESMAX) THEN
                                    pvalue=ZESMAX
                               ENDIF
                              ENDIF
!                                      CONVERT TO RADIANS
                              IF ( ILEM .EQ. 48 .OR. ILEM .EQ. 54 ) THEN
                               pvalue=ZTORAD*pvalue
                              ENDIF
!                                         FLAGS
                              IF  (LDFLAG) THEN
!                                   SET BIT 12  TO ZERO
!                                   (Element assim by 3dvar)
                                 IFLAG = KFLAGS(ielement,ilevel,IK)
                                 IFLAG = IBCLR(IFLAG,12)
                                 obsSpaceData%mobdata(NCM_FLG,IP+IND)= IFLAG
                              ELSE
                                 obsSpaceData%mobdata(NCM_FLG,IP+IND)= IBAD
                              ENDIF
!
                              obsSpaceData%robdata8(NCM_VAR,IP+IND)=pvalue
                              obsSpaceData%mobdata (NCM_VNM,IP+IND)=KLIST(ielement)
                              obsSpaceData%mobdata (NCM_VCO,IP+IND)=NVCORDTYP
                              obsSpaceData%robdata8(NCM_OMF,IP+IND)=PPMIS
                              obsSpaceData%robdata8(NCM_OMA,IP+IND)=PPMIS
                              obsSpaceData%robdata8(NCM_OMI,IP+IND)=PPMIS
                              obsSpaceData%robdata (NCM_FGE,IP+IND)=real(PPMIS)
                              obsSpaceData%robdata8(NCM_OER,IP+IND)=PPMIS
!
!                             OBS ERROR FOR HUMSAT
!
                              IF ( LDERR ) THEN
                                obsSpaceData%robdata8(NCM_OER,IP+IND)=PROFIL(ilevel)
                              ENDIF
!
!                             REFERENCE LEVEL FOR SATEMS
!
                              IF ( LDSAT ) THEN
                                obsSpaceData%robdata8(NCM_OER,IP+IND)=PROFIL(ilevel)*ZFACT
                                obsSpaceData%robdata8(NCM_OER,IP+IND)=1.0D0
                              ENDIF
!
!                             SURFACE EMISSIVITIES FOR GOES AIRS AND IASI RADIANCES
!
                              IF ( LDGO ) THEN
                                obsSpaceData%robdata8(NCM_PRL,IP+IND)=PROFIL(ilevel) &
                                     *ZEMFACT
                              ENDIF

                              IF ( LDAIRS ) THEN
                                obsSpaceData%robdata8(NCM_PRL,IP+IND)=PROFIL(ilevel) &
                                     *ZEMFACT
                              END IF

                              IF ( LDIASI ) THEN
                                 obsSpaceData%robdata8(NCM_PRL,IP+IND)=PROFIL(ilevel) &
                                      *ZEMFACT
                              END IF

!
                              IND=IND + 1
                           ELSE
!==================================================
                              KNDAT = IND
                              obsSpaceData%numobs_recorded = obsSpaceData%numobs_recorded + KNDAT
!==================================================
                              RETURN
                           ENDIF
                        ENDIF
                  ENDIF
               END DO
            ENDIF
         END DO
!=============================
      KNDAT = IND
      obsSpaceData%numobs_recorded = obsSpaceData%numobs_recorded + KNDAT
!=============================
!
      RETURN
!
      END SUBROUTINE CMABDY

