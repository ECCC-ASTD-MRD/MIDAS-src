module tovs_extrap_mod
      implicit none
      save
      private

      public :: extrap, lextrap, aextrap

!     Contents previously in comtovxt
!     -------------------------------
! === Extrapolation of temperature up to 0.1 mbs ===
!     Purpose: to define the vertical extrapolation parameters
!              required by the TOVS radiative transfer model. These
!              are used to extrapolate a temperature profile from
!              approximately to 20 mbs to 0.1 mb, using 18 levels
!              from approximately 400mbs to 20 mbs.
!
!     Revision 001 J. Halle  Fev 2000
!              extrapolation to 7 upper levels only.
!
!
!     JPXTLVIN                   : number of temperature levels used
!                                  for extrapolation
!     JPXTLVOU                   : number of temperature levels to be
!                                  extrapolated
!     JPXTOUMX                   : maximum number of temperature levels to be
!                                  extrapolated
!     MLVXTIN (JPXTLVIN)         : levels used for extrapolation
!     COEFF   (JPXTLVIN,JPXTOUMX): vertical extrapolation coefficients
!     TREFIN  (JPXTLVIN)         : reference temperature (input  levels)
!     TREFOU  (JPXTOUMX)         : reference temperature (output levels)

      INTEGER, PARAMETER :: JPXTLVIN= 18
      INTEGER, PARAMETER :: JPXTLVOU=  7
      INTEGER, PARAMETER :: JPXTOUMX=  9

      INTEGER            :: MLVXTIN(JPXTLVIN)

      REAL*8             :: COEFF (JPXTLVIN,JPXTOUMX)
      REAL*8             :: TREFIN(JPXTLVIN)
      REAL*8             :: TREFOU(JPXTOUMX)

      DATA MLVXTIN /                                                 &
        10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24,  &
        25, 26, 27 /

      DATA COEFF /                                                       &
         -0.5439,  0.0986,  0.2883,  0.2490,  0.0137, -0.3474, -0.2183,  &
         -0.0916,  0.0580,  0.2026,  0.2813,  0.3344, -0.0961, -0.4738,  &
          0.2504,  0.3186,  0.0006, -0.3001,                             &
          0.0880,  0.0314, -0.0469, -0.1341, -0.0834,  0.0611, -0.0304,  &
         -0.0843,  0.0998,  0.2731,  0.2598,  0.1852, -0.0118, -0.1806,  &
          0.0151,  0.1037,  0.1282,  0.1532,                             &
          0.6220, -0.0697, -0.3213, -0.3509, -0.1239,  0.2714,  0.0909,  &
         -0.0374,  0.1594,  0.3408,  0.2351,  0.0390,  0.0536,  0.0784,  &
         -0.1803, -0.0724,  0.2489,  0.5551,                             &
          1.2860, -0.3116, -0.6370, -0.3330, -0.0400,  0.2315,  0.1073,  &
          0.0161,  0.1668,  0.3014,  0.1870, -0.0052,  0.1122,  0.2133,  &
         -0.3026, -0.1904,  0.2886,  0.7436,                             &
          1.7806, -0.4868, -0.8326, -0.2319,  0.0540,  0.1063,  0.0949,  &
          0.0808,  0.1200,  0.1491,  0.1030,  0.0336,  0.1411,  0.2033,  &
         -0.3388, -0.2399,  0.2338,  0.6844,                             &
          1.8949, -0.4038, -0.7716, -0.1868,  0.0578,  0.0515,  0.0826,  &
          0.0956,  0.0677,  0.0351,  0.0108, -0.0102,  0.1004,  0.1710,  &
         -0.2467, -0.1952,  0.1315,  0.4446,                             &
          1.7767, -0.2038, -0.6043, -0.2209,  0.0272,  0.1665,  0.0840,  &
          0.0053, -0.0204, -0.0484, -0.0228,  0.0193,  0.0729,  0.0912,  &
         -0.1542, -0.1243,  0.0682,  0.2558,                             &
          1.5776, -0.1120, -0.4432, -0.1009,  0.0476,  0.0507,  0.0471,  &
          0.0312, -0.0225, -0.0755, -0.0393,  0.0248,  0.0461,  0.0397,  &
         -0.0892, -0.0694,  0.0387,  0.1460,                             &
          1.2832, -0.0175, -0.2829, -0.0348,  0.0495,  0.0123,  0.0183,  &
          0.0155, -0.0098, -0.0354, -0.0177,  0.0135,  0.0220,  0.0174,  &
         -0.0423, -0.0334,  0.0168,  0.0685 /

      DATA TREFIN  /                                                     &
          223.73,  221.25,  219.14,  217.28,  215.27,  213.20,  212.16,  &
          211.52,  213.15,  214.65,  216.64,  218.70,  221.39,  224.25,  &
          228.07,  232.72,  237.76,  242.52 /

      DATA TREFOU  /                                                     &
          242.25,  256.61,  264.15,  262.14,  254.40,  244.88,  237.22,  &
          231.38,  227.33 /

  CONTAINS


    SUBROUTINE EXTRAP ( PROFIN, PROFOUT, JPMOLEV, JPLEV, KNPF )
!
!**s/r EXTRAP  - Extrapolate temperature profile above 20mb
!                on RTTOV levels (up to 0.1 mbs).
!
!
!Author        : J. Halle *CMDA/AES  April 28, 1994
!
!
!Arguments
!     i   PROFIN (JPMOLEV,KNPF) : Temperature profile (to be extrapolated)
!     o   PROFOUT(JPLEV,KNPF)   : Temperature profile (      extrapolated)
!     i   JPMOLEV               : number of levels (RT model) from NWP
!     i   JPLEV                 : number of pressure levels
!     i   KNPF                  : Number of profiles
!
!    -------------------
!*    Purpose: extrapolate temperature profile above top level

      IMPLICIT NONE
      INTEGER JI, JJ, JK, KNPF, ILEV, JPMOLEV, JPLEV, NLVLS_XTRAP, JPMOTOP
      REAL*8 PROFIN(JPMOLEV,KNPF), PROFOUT(JPLEV,KNPF)


!*    1.  Initialize output temperature profile
!     .   -------------------------------------

      JPMOTOP = JPLEV - JPMOLEV + 1
      NLVLS_XTRAP = JPLEV - JPMOLEV
      DO JI= NLVLS_XTRAP+1, JPLEV
         DO JK = 1, KNPF
            PROFOUT(JI,JK) = PROFIN(JI-JPMOTOP+1,JK)
         ENDDO
      ENDDO

!*    2.  Extrapolation of temperatures
!     .   -----------------------------

      DO JJ= 1,NLVLS_XTRAP
         DO JK = 1, KNPF
            PROFOUT(JJ,JK) = TREFOU(JJ)
         ENDDO
      ENDDO

      DO JJ=1,NLVLS_XTRAP
         DO JI=1,JPXTLVIN
            ILEV = MLVXTIN(JI)
            DO JK = 1, KNPF
               PROFOUT(JJ,JK) = PROFOUT(JJ,JK) +  &
               COEFF(JI,JJ)*(PROFIN(ILEV-JPMOTOP+1,JK)-TREFIN(JI))
            ENDDO
         ENDDO
      ENDDO

    END SUBROUTINE EXTRAP


    SUBROUTINE LEXTRAP ( PROFIN, PROFOUT, JPMOLEV, JPLEV, KNPF )
!
!**s/r LEXTRAP  - Tangent linear of extrapolation of temperature profile
!                 above 20mb on RTTOV levels (up to 0.1 mbs).
!
!
!Author        : J. Halle *CMDA/AES  April 28, 1994
!
!
!Arguments
!     i   PROFIN (JPMOLEV,KNPF) : Temperature profile (to be extrapolated)
!     o   PROFOUT(JPLEV,KNPF)   : Temperature profile (      extrapolated)
!     i   JPMOLEV               : number of levels (RT model) from NWP
!     i   JPLEV                 : number of pressure levels
!     i   KNPF                  : Number of profiles
!
!    -------------------
!*    Purpose: tangent linear of extrapolation of temperature profile above top level

      IMPLICIT NONE
      INTEGER JI, JJ, JK, KNPF, ILEV, JPMOLEV, JPLEV, NLVLS_XTRAP, JPMOTOP
      REAL*8 PROFIN(JPMOLEV,KNPF), PROFOUT(JPLEV,KNPF)

!*    1.  Initialize output temperature profile
!     .   -------------------------------------

      JPMOTOP = JPLEV - JPMOLEV + 1
      NLVLS_XTRAP = JPLEV - JPMOLEV
      DO JI= NLVLS_XTRAP+1, JPLEV
         DO JK = 1, KNPF
            PROFOUT(JI,JK) = PROFIN(JI-JPMOTOP+1,JK)
         ENDDO
      ENDDO

!*    2.  Extrapolation of temperatures
!     .   -----------------------------

      DO JJ= 1,NLVLS_XTRAP
         DO JK = 1, KNPF
            PROFOUT(JJ,JK) = 0.0
         ENDDO
      ENDDO

      DO JJ=1,NLVLS_XTRAP
         DO JI=1,JPXTLVIN
            ILEV = MLVXTIN(JI)
            DO JK = 1, KNPF
               PROFOUT(JJ,JK) = PROFOUT(JJ,JK) +  &
               COEFF(JI,JJ)*PROFIN(ILEV-JPMOTOP+1,JK)
            ENDDO
         ENDDO
      ENDDO

    END SUBROUTINE LEXTRAP


    SUBROUTINE AEXTRAP( PROFIN, PROFOUT, JPMOLEV, JPLEV, KNPF )
!
!**s/r AEXTRAP  - Adjoint of extrapolation of temperature profile above 20mb
!
!                 on RTTOV levels (up to 0.1 mbs).
!
!Author        : J. Halle *CMDA/AES  November 01, 1994
!
!Arguments
!     o   PROFIN (JPMOLEV,KNPF) : output adjoint of temperature profile
!     i   PROFOUT(JPLEV,KNPF)   : input adjoint of temperature profile
!     i   JPMOLEV               : number of levels (RT model) from NWP
!     i   JPLEV                 : number of pressure levels
!     i   KNPF                  : Number of profiles
!
!    -------------------
!*    Purpose: adjoint of extrapolation of  temperature profile above the top level

      IMPLICIT NONE
      INTEGER JI, JJ, JK, KNPF, ILEV, JPMOLEV, JPLEV, NLVLS_XTRAP, JPMOTOP
      REAL*8 PROFIN(JPMOLEV,KNPF), PROFOUT(JPLEV,KNPF)

!*    1.  Initialize output adjoint of temperature profile
!     .   ------------------------------------------------

      JPMOTOP = JPLEV - JPMOLEV + 1
      NLVLS_XTRAP = JPLEV - JPMOLEV
      DO JI= 1, JPMOLEV
         DO JK = 1, KNPF
            PROFIN(JI,JK) = 0.0
         ENDDO
      ENDDO

!*    2.  Adjoint of extrapolation of temperatures
!     .   ----------------------------------------

      DO JJ = NLVLS_XTRAP, 1, -1
        DO JI = JPXTLVIN, 1, -1
          ILEV = MLVXTIN(JI)
          DO JK = KNPF, 1, -1
            PROFIN(ILEV-JPMOTOP+1,JK) = PROFIN(ILEV-JPMOTOP+1,JK) +  &
                                        PROFOUT(JJ,JK)*COEFF(JI,JJ)
          ENDDO
        ENDDO
      ENDDO

      DO JJ = NLVLS_XTRAP, 1, -1
        DO JK = KNPF, 1, -1
          PROFOUT(JJ,JK) = 0.0
        ENDDO
      ENDDO

      DO JI = JPLEV, NLVLS_XTRAP+1, -1
        DO JK = KNPF, 1, -1
          PROFIN(JI-JPMOTOP+1,JK) = PROFIN(JI-JPMOTOP+1,JK) +  &
                                    PROFOUT(JI,JK)
          PROFOUT(JI,JK) = 0.0
        ENDDO
      ENDDO

    END SUBROUTINE AEXTRAP

end module tovs_extrap_mod
