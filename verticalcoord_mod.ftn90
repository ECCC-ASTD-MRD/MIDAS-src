module verticalCoord_mod

  use mpi
  use Vgrid_Descriptors
  implicit none


  type struct_vco
     logical :: initialized=.false.
     integer :: iversion          ! code of vert coord from !! record in BG stat file
     integer :: ikind               ! kind of vertical coordinate(a la convip)
     character(len=8) :: svcod      ! type of vertical coordinate (cvcord in 3DVAR)
     integer :: nlev_T,nlev_M         ! Number of vertical levels (Thermo/Moment)
     real*8  :: dpt_T, dpt_M        ! Pressure at top level    (Thermo/Moment)
     real*8  :: dprf_T, dprf_M      ! Reference pressure       (Thermo/Moment)
     real*8  :: drcf1, drcf2        ! Expansion coefficients
     integer,pointer,dimension(:) :: ip1_T,ip1_M   ! encoded IP1 levels (Thermo/Moment)
     real*8 ,pointer,dimension(:) :: da_T,db_T     ! A, B values for Thermo levels
     real*8 ,pointer,dimension(:) :: da_M,db_M     ! A, B values for Momentum levels
     real*8 ,pointer,dimension(:) :: db_dhyb_M,db_dhyb_T  ! factor needed for TLM/AD of tt2phi
     real*8 ,pointer,dimension(:) :: da_dhyb_M,da_dhyb_T  ! factor needed for TLM/AD of tt2phi
     real*8 ,pointer,dimension(:) :: dhyb_T,dhyb_M !decoded IP1 levels (Thermo/Momentum)
     type(vgrid_descriptor) :: vgrid
  end type struct_vco

  contains

  subroutine vco_allocate(tf_vco)
    implicit none
    type(struct_vco), pointer :: tf_vco
    integer :: ilnk,stat,nl_stat

    stat        = 0

    ilnk        = tf_vco%nlev_M
    allocate (tf_vco%ip1_M(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%da_M(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%db_M(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%da_dhyb_M(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%db_dhyb_M(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%dhyb_M(ilnk),stat=nl_stat)
    stat = stat + nl_stat

    ilnk        = tf_vco%nlev_T
    allocate (tf_vco%ip1_T(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%da_T(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%db_T(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%da_dhyb_T(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%db_dhyb_T(ilnk),stat=nl_stat)
    stat = stat + nl_stat
    allocate (tf_vco%dhyb_T(ilnk),stat=nl_stat)
    stat = stat + nl_stat

    if(stat .ne. 0 ) then
       call abort3d(' vco: problem with allocate in tf_vco ')
    endif

  end subroutine vco_allocate


  subroutine vco_SetupFromFile(tf_vco,templatefile,lprecision)
!  s/r vco_SetupFromFile - Initialize structure for a standard file using vgrid_descriptors library.
    use MathPhysConstants_mod
    implicit none
    type(struct_vco),pointer :: tf_vco
    character(len=*) :: templatefile
    logical :: lprecision

    integer :: ivcode,kind,jlev,stat,sigdigits,nultemplate,ierr
    integer :: fnom,fstouv,fstfrm,fclos
    integer,   dimension(:), pointer :: my_ip1
    real    :: hyb_r4
    real*8  :: dl_ptM,dl_ptT,dl_pref,dl_rcf1,dl_rcf2,zterm
    real*8,    dimension(:), pointer :: my_a, my_b
    REAL*8 precision
    character(len=10) :: blk_S
    logical :: isExist_L

    inquire(file=templatefile,exist=isExist_L)
    IF ( isExist_L )then
      nultemplate=0
      ierr=fnom(nultemplate,templatefile,'RND+OLD+R/O',0)
      if ( ierr .eq. 0 ) then
        if(mpi_myid.eq.0) write(*,*) ' TEMPLATEFILE=', templatefile
        if(mpi_myid.eq.0) write(*,*) ' opened as unit file ',nultemplate
        ierr =  fstouv(nultemplate,'RND+OLD')
      else
        call abort3d('vco_SetupFromFile: CANNOT OPEN TEMPLATE FILE!')
      endif
    else
      call abort3d('vco_SetupFromFile: CANNOT FIND TEMPLATE FILE!')
    endif


    if(.not.associated(tf_vco)) allocate(tf_vco)

    !==========================================================================
    ! Get vertical coordinate descriptors from standard file(vgd_new reads "!!" record)
  
    stat = vgd_new(tf_vco%vgrid,unit=nultemplate,format="fst",ip1=-1,ip2=-1)
    if(stat.ne.VGD_OK)then
      call abort3d('ERROR with vgd_new')
    endif

    ! Print out vertical structure 
    if(mpi_myid.eq.0) stat = vgd_print(tf_vco%vgrid)
    if(stat.ne.VGD_OK)then
      call abort3d('ERROR with vgd_print')
    endif
    !==========================================================================
    ! Get version of the vertical coordinate

    stat = 0
    stat = vgd_get(tf_vco%vgrid,key='ig_1 - vertical coord code',value=ivcode)
    if(stat.ne.VGD_OK) then
      call abort3d('vco: problem with vgd_get: key= ig_1 - vertical coord code')
    endif

    !ivcode = kind*1000 + version 
    tf_vco%ikind    = ivcode/1000
    tf_vco%iversion = ivcode
      
    if(ivcode.eq.1002) tf_vco%svcod='etage'    ! eta vertical coord
    if(ivcode.eq.5001) tf_vco%svcod='gemhyb'   ! unstaggered hybride vertical coord 
    if(ivcode.eq.5002) tf_vco%svcod='gemstg'   ! staggered hybride vertical coord
      
    if(mpi_myid.eq.0) write(*,*) 'vco: vcode,kind,cvcod ',ivcode,tf_vco%ikind,tf_vco%svcod

    !==========================================================================
    ! Set the number of vertical levels and allocate tf_vco arrays

    stat = 0
    stat = vgd_get(tf_vco%vgrid,key='nl_m - number of momentum levels',value=tf_vco%nlev_M)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get nl_m vgd_ok= ',VGD_OK
    stat = vgd_get(tf_vco%vgrid,key='nl_t - number of momentum levels',value=tf_vco%nlev_T)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get nl_t vgd_ok= ',VGD_OK
    
    call vco_allocate(tf_vco)

    !==========================================================================
    ! Process levels (ip1, A, B)
    ! (unstaggered  vertical coord are stored in levels in "!!" records)

    stat = 0
    stat = vgd_get(tf_vco%vgrid,key='ca_m - vertical a coefficient (m)',value=tf_vco%da_M)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get ca_m vgd_ok= ',VGD_OK
    stat = vgd_get(tf_vco%vgrid,key='ca_t - vertical a coefficient (t)',value=tf_vco%da_T)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get ca_t vgd_ok= ',VGD_OK
    !
    stat = vgd_get(tf_vco%vgrid,key='cb_m - vertical b coefficient (m)',value=tf_vco%db_M)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get cb_m vgd_ok= ',VGD_OK
    stat = vgd_get(tf_vco%vgrid,key='cb_t - vertical b coefficient (t)',value=tf_vco%db_T)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get cb_t vgd_ok= ',VGD_OK
    !
    stat = vgd_get(tf_vco%vgrid,key='vipm - vertical levels (m)',value=my_ip1)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get ip1m vgd_ok= ',VGD_OK
    !
    stat = vgd_get(tf_vco%vgrid,key='ptop - pressure at top level (m)',value=dl_ptM)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get ptm vgd_ok= ',VGD_OK
    !
    stat = vgd_get(tf_vco%vgrid,key='pref - reference pressure (m)',value=dl_pref)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get vipm vgd_ok= ',VGD_OK
    !
    stat = vgd_get(tf_vco%vgrid,key='rc_1 - coefficient value for rectification (m)',value=dl_rcf1)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get vipm vgd_ok= ',VGD_OK
    !
    if(stat.ne.0) then
      call abort3d('vco: problem with vgd_get')
    endif

    if(size(tf_vco%da_M).gt.0 .and. size(tf_vco%db_M).gt.0 .and. size(my_ip1).gt.0 )then 
      if(mpi_myid.eq.0) write(*,*) 'vco: size(a,b,ip1)(m)= ',size(tf_vco%da_M),size(tf_vco%db_M),size(my_ip1)
    else
      if(mpi_myid.eq.0) write(*,*) 'vco: problem with size(a,b,ip1)(m) in vgd_get'
      return
    endif

    tf_vco%dprf_M = dl_pref
    tf_vco%dprf_T = dl_pref ! not sure if this will ever be different for thermo levels
    tf_vco%dpt_M  = dl_ptM
    tf_vco%dpt_T  = dl_ptM
    tf_vco%drcf1  = dl_rcf1
    if(ivcode.eq.5002) then
      ! temporarily compute a single pressure roughly corresponding to top trial field level
      ! (vgrid descriptor gives a pressure for a higher non-existant level, currently 7.5Pa)
      tf_vco%dpt_M  = exp( tf_vco%da_M(1) + tf_vco%db_M(1)*log(100000.0d0/dl_pref) )
      if(mpi_myid.eq.0) write(*,*) 'vco_setupFromFile: computed ptop(MM)=',tf_vco%dpt_M
      if(mpi_myid.eq.0) write(*,*) 'vco_setupFromFile: read ptop(MM)=',dl_ptM
      tf_vco%dpt_T  = exp( tf_vco%da_T(1) + tf_vco%db_T(1)*log(100000.0d0/dl_pref) )
      if(mpi_myid.eq.0) write(*,*) 'vco_setupFromFile: computed ptop(TH)=',tf_vco%dpt_T
      if(mpi_myid.eq.0) write(*,*) 'vco_setupFromFile: read ptop(TH)=',dl_ptM
    endif
    !
    ! Set to zero the non-significant digits (due to conversion from real*4)
    !
    if(lprecision) then
      sigdigits=7
      precision=10.0d0**(sigdigits-int(log10(tf_vco%dpt_M)))
      tf_vco%dpt_M = real(nint(precision*tf_vco%dpt_M),8)/precision
      precision=10.0d0**(sigdigits-int(log10(tf_vco%dprf_M)))
      tf_vco%dprf_M = real(nint(precision*tf_vco%dprf_M),8)/precision
      precision=10.0d0**(sigdigits-int(log10(tf_vco%drcf1)))
      tf_vco%drcf1 = real(nint(precision*tf_vco%drcf1),8)/precision
    endif

    !==========================================================================
    ! Assign values for Momentum levels

    if(mpi_myid.eq.0) write(*,*) 'vco: size(hyb)= ',size(tf_vco%dhyb_M)
    do jlev = 1, tf_vco%nlev_M
      tf_vco%ip1_M(jlev) = my_ip1(jlev)
      call convip(my_ip1(jlev),hyb_r4,kind,-1,blk_s,.false.)
      tf_vco%dhyb_M(jlev) = hyb_r4
      if(mpi_myid.eq.0) write(*,*) 'vco: jlev,nk,ip1,hyb(moment)= ' & 
            ,jlev,tf_vco%nlev_M,tf_vco%ip1_M(jlev),tf_vco%dhyb_M(jlev)

      ! Devrait probablement etre enleve car avec la nouvelle facon de calculer
      ! l'equation hydrostatique zterm ne sera plus requis
      if((tf_vco%drcf1-1.0d0) .lt. epsilon(tf_vco%drcf1)) then
        zterm = 1.0d0
      else
        zterm = ((tf_vco%dhyb_M(jlev) - tf_vco%dpt_M/tf_vco%dprf_M) &
                 /(1.0d0-tf_vco%dpt_M/tf_vco%dprf_M))**(tf_vco%drcf1-1.0d0)
      endif
      tf_vco%db_dhyb_M(jlev)= tf_vco%drcf1 * zterm
      tf_vco%da_dhyb_M(jlev)= tf_vco%dprf_M*(1.0d0 - tf_vco%db_dhyb_M(jlev))

    enddo

    !==========================================================================
    ! Process thermodynamic levels 

    stat = 0
    stat = vgd_get(tf_vco%vgrid,key='vipt - vertical ip1 levels (t)',value=my_ip1)
    stat = stat + VGD_OK
    if(mpi_myid.eq.0) write(*,*) 'vco: vgd_get vipt vgd_ok= ',VGD_OK
    if(stat.ne.0) then
      call abort3d('vco: problem with vgd_get')
    endif

    if(size(tf_vco%da_T).gt.0 .and. size(tf_vco%db_T).gt.0 .and. size(my_ip1).gt.0 )then 
      if(mpi_myid.eq.0) write(*,*) 'vco: size(a,b,ip1)(t)= ',size(tf_vco%da_T),size(tf_vco%db_T),size(my_ip1)
    else
      call abort3d('vco: problem with size(a,b,ip1)(t) in vgd_get')
    endif

    do jlev = 1,tf_vco%nlev_T
      tf_vco%ip1_T(jlev)  = my_ip1(jlev)
      call convip(my_ip1(jlev),hyb_r4,kind,-1,blk_s,.false.)
      tf_vco%dhyb_T(jlev) = hyb_r4
      if(mpi_myid.eq.0) write(*,*) 'vco: jlev,nk,ip1,hyb(thermo)= ' &
            ,jlev,tf_vco%nlev_T,tf_vco%ip1_T(jlev),tf_vco%dhyb_T(jlev)

      if((tf_vco%drcf1-1.0d0) .lt. epsilon(tf_vco%drcf1)) then
        zterm = 1.0d0
      else
        zterm = ((tf_vco%dhyb_T(jlev) - tf_vco%dpt_T/tf_vco%dprf_T) &
                 /(1.0d0-tf_vco%dpt_T/tf_vco%dprf_T))**(tf_vco%drcf1-1.0d0)
      endif
      tf_vco%db_dhyb_T(jlev)= tf_vco%drcf1 * zterm
      tf_vco%da_dhyb_T(jlev)= tf_vco%dprf_T*(1.0d0 - tf_vco%db_dhyb_T(jlev))

    enddo

    tf_vco%initialized=.true.

    ierr =  fstfrm(nultemplate)
    ierr =  fclos (nultemplate)

  end subroutine vco_SetupFromFile


end module VerticalCoord_mod
