!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------

!--------------------------------------------------------------------------
!!
!! *Purpose*: Main program for O-P computation
!!
!--------------------------------------------------------------------------
program main_ominusp
  use topLevelControl_mod
  use ramDisk_mod
  use utilities_mod
  use mpivar_mod
  use MathPhysConstants_mod
  use horizontalCoord_mod
  use verticalCoord_mod
  use timeCoord_mod
  use obsSpaceData_mod
  use columnData_mod  
  use gridStateVector_mod
  use burpFiles_mod
  use obsFilter_mod  
  use innovation_mod
  use WindRotation_mod
  use analysisGrid_mod
  use tovs_nl_mod
  use obserrors_mod
  implicit none

  type(struct_obs),       target  :: obsSpaceData
  type(struct_columnData),target  :: column_anl
  type(struct_columnData),target  :: column_trl
  type(struct_vco),       pointer :: vco_anl  => null()
  type(struct_vco),       pointer :: vco_trl  => null()
  type(struct_hco),       pointer :: hco_anl  => null()
  type(struct_hco),       pointer :: hco_core => null()

  character(len=48) :: obsMpiStrategy
  character(len=3)  :: obsColumnMode

  integer :: datestamp, get_max_rss, headerIndex, ierr
  integer :: fnom, fclos, nulnam

  ! Namelist
  logical :: addHBHT  
  logical :: addSigmaO
  NAMELIST /NAMOMP/addHBHT,addSigmaO

  write(*,*) " ---------------------------------------"
  write(*,*) " ---  START OF MAIN PROGRAM OminusP  ---"
  write(*,*) " ---  Computation of the innovation  ---"
  write(*,*) " ---------------------------------------"

  !
  !- 1.  Settings and module initializations
  !
  write(*,*)
  write(*,*) '> main_OminusP: setup - START'

  obsMpiStrategy = 'LIKESPLITFILES'
  obsColumnMode  = 'VAR'

  !- 1.0 Namelist
  addHBHT   = .false. ! default value
  addSigmaO = .false. 

  nulnam = 0
  ierr = fnom(nulnam,'./flnml','FTN+SEQ+R/O',0)
  read(nulnam,nml=namomp,iostat=ierr)
  if (ierr /= 0) call utl_abort('main_OminusP: Error reading namelist')
  if (mpi_myid == 0) write(*,nml=namomp)
  ierr = fclos(nulnam)

  !- 1.1 mpi
  call mpi_initialize  

  !- 1.2 timings
  call tmg_init(mpi_myid, 'TMG_OMINUSP' )
  call tmg_start(1,'MAIN')

  !- 1.3 Top level control setup
  call top_setup
  call ram_setup

  !- 1.4 Temporal grid
  call tim_setup

  !- 1.5 Burp file names and set datestamp
  call burp_setupFiles(datestamp)
  call tim_setDatestamp(datestamp)

  !- 1.6 Constants
  if (mpi_myid == 0) call mpc_printConstants(6)

  !- 1.7 Setup a column vector following the background vertical grid
  if (mpi_myid == 0) write(*,*)''
  if (mpi_myid == 0) write(*,*)' set vcoord parameters for background grid'
  call vco_SetupFromFile( vco_trl,     & ! OUT
                          './trlm_01')   ! IN
  call col_setVco(column_trl,vco_trl)

  !- 1.8 Variables of the model states
  call gsv_setup

  !- 1.9 Set the horizontal domain
  if ( addHBHT ) then
    call hco_SetupFromFile(hco_anl, './analysisgrid', 'ANALYSIS') ! IN
    if ( hco_anl % global ) then
      call agd_SetupFromHCO( hco_anl ) ! IN
    else
      !- Iniatilized the core (Non-Exteded) analysis grid
      call hco_SetupFromFile( hco_core, './analysisgrid', 'COREGRID') ! IN
      !- Setup the LAM analysis grid metrics
      call agd_SetupFromHCO( hco_anl, hco_core ) ! IN
    end if
  else
    call hco_SetupFromFile(hco_anl, './trlm_01', ' ') ! IN
    call agd_SetupFromHCO( hco_anl ) ! IN
  end if

  if ( hco_anl % rotated ) then
    call uvr_Setup(hco_anl) ! IN 
  end if

  ! 1.10 Setup a column vector following the analysis vertical grid
  if ( addHBHT ) then
    call vco_SetupFromFile(vco_anl,        & ! OUT
                           './analysisgrid') ! IN
    call col_setVco(column_anl,vco_anl)
  end if

  !- 1.11 Setup and read observations
  call inn_setupObs(obsSpaceData,obsColumnMode,obsMpiStrategy)

  !- 1.12 Basic setup of columnData module
  call col_setup

  !- 1.13 Memory allocation for background column data
  call col_allocate(column_trl,obs_numheader(obsSpaceData),mpi_local=.true.)
  if ( addHBHT ) then
    call col_allocate(column_anl, obs_numheader(obsSpaceData),mpi_local=.true.)
  end if

  if ( addSigmaO ) then
    !- 1.14 Initialize the observation error covariances
    write(*,*)
    write(*,*) '> main_OminusP: Adding sigma_O'
    call oer_setObsErrors(obsSpaceData)
  end if

  !- 1.15 Reading, horizontal interpolation and unit conversions of the 3D background fields
  call inn_setupBackgroundColumns(column_trl,obsSpaceData)

  write(*,*)
  write(*,*) '> main_OminusP: setup - END'
  write(*,*) 'Memory Used: ',get_max_rss()/1024,'Mb'

  !
  !- 2.  O-P computation
  !
 
  !- 2.1 Compute observation innovations
  write(*,*)
  write(*,*) '> main_OminusP: compute innovation'
  call inn_computeInnovation(column_trl,obsSpaceData)

  if ( addHBHT ) then
    write(*,*)
    write(*,*) '> main_OminusP: Adding HBH^T'
    !- 2.2 Interpolate background columns to analysis levels and setup for linearized H
    call inn_setupBackgroundColumnsAnl(column_trl,column_anl)

    ! 2.3 Compute the background errors in observation space
    call compute_HBHT(column_anl,column_trl,obsSpaceData)
  end if

  ! 2.4 Write the results

  ! 2.4.1 Into the listings
  write(*,*)
  write(*,*) '> main_OminusP: printing the FIRST header and body'
  do headerIndex = 1, min(1,obs_numHeader(obsSpaceData))
    call obs_prnthdr(obsSpaceData,headerIndex)
    call obs_prntbdy(obsSpaceData,headerIndex)
  end do
  ! 2.4.2 Into burp files
  write(*,*)
  write(*,*) '> main_OminusP: writing to file'
  call burp_updateFiles(obsSpaceData)

  !
  !- 3.  Ending
  !
  write(*,*)
  write(*,*) '> main_OminusP: Ending'
  call obs_finalize(obsSpaceData) ! deallocate obsSpaceData

  call tmg_stop(1)
  call tmg_terminate(mpi_myid, 'TMG_OMINUSP' )

  call rpn_comm_finalize(ierr) 

end program main_ominusp
