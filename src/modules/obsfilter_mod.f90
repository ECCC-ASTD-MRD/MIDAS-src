!--------------------------------------- LICENCE BEGIN -----------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------

!--------------------------------------------------------------------------
!! MODULE obsFilter (prefix="filt" category='1. High-level functionality')
!!
!! *Purpose*: Various types of filters that are applied to the observations
!!            mostly to reject them so they will not be assimilated.
!!
!--------------------------------------------------------------------------
module obsFilter_mod
  use mpi_mod
  use mpivar_mod
  use EarthConstants_mod
  use MathPhysConstants_mod
  use obsSpaceData_mod
  use columnData_mod
  use bufr_mod
  use tovs_nl_mod
  use gps_mod
  use utilities_mod
  use varNameList_mod
  use chem_setup_mod
  implicit none
  save
  private

  ! public variables
  public :: filt_rlimlvhu
  ! public procedures
  public :: filt_setup, filt_topo, filt_sethind, filt_suprep
  public :: filt_surfaceWind, filt_gpsro

  integer filt_nelems, filt_nlist(30)
  integer filt_nflags, filt_nlistflg(15)

  logical ltopofilt, discardlandsfcwind

  real(8) :: filt_rlimlvhu

  ! topographic rejection criteria
  integer, parameter :: numElem = 19
  real(8)            :: altDiffMax(numElem) =   & ! default values (in metres)
       (/ 50.d0,     50.d0,     50.d0,     50.d0,     50.d0,    800.d0,    800.d0,  &
       800.d0,    800.d0,   1000.d0,    50.d0,     50.d0,     50.d0,     50.d0,  &
       50.d0,     50.d0,     50.d0,      50.d0,    50.d0 /)
  integer, parameter :: elemList(numElem) =  &
       (/ BUFR_NEDS, BUFR_NEFS, BUFR_NEUS, BUFR_NEVS, BUFR_NESS, BUFR_NETS, BUFR_NEPS, &
       BUFR_NEPN, BUFR_NEGZ, BUFR_NEZD, BUFR_NEDD, BUFR_NEFF, BUFR_NEUU, BUFR_NEVV, &
       BUFR_NEES, BUFR_NETT, BUFR_NEAL, bufr_vis, bufr_gust /)
  real(8) :: bndryCritPres         = 5000.0d0  ! in Pascals
  real(8) :: bndryCritHeightProf   =  400.0d0  ! in Metres
  real(8) :: bndryCritHeightAladin =  400.0D0  ! in Metres

  character(len=48) :: filterMode

contains

!--------------------------------------------------------------------------
! findElemIndex
!------------------------------------------------------------------------- 
 function findElemIndex(varNum) result(listIndex)
    implicit none
    integer :: varNum, listIndex, elemIndex

    listIndex = -1
    do elemIndex=1,numElem
       if(varNum.eq.elemList(elemIndex)) listIndex = elemIndex
    end do

    if (listIndex == -1) then
       write(*,*) 'filterobs_mod-findElemIndex: WARNING: varNum value not found: ',varNum
    end if

  end function findElemIndex

!--------------------------------------------------------------------------
! filt_setup
!--------------------------------------------------------------------------
  subroutine filt_setup(filterMode_in)
    implicit none

    character(len=*), intent(in) :: filterMode_in

    integer :: nulnam, ierr, elem, elem2, jflag, ibit, itotelem, ielem
    integer :: fnom, fclos
    integer :: nelems, nlist(30)
    integer :: nflags, nlistflg(15)
    integer :: nelems_altDiffMax, list_altDiffMax(numElem), elemIndex
    
    real(8) :: value_altDiffMax(numElem)
    real(8) :: rlimlvhu

    character(len=35) :: CREASON(-8:13)
    data creason/'JACOBIAN IMPORTANT ABOVE MODEL TOP', &
         'ABS OROGRAPH-PHI                  ', &
         'MASQUE TERRE-MER                  ', &
         'OROGRAPHIE                        ', &
         'REJECTED BY QCVAR                 ', &
         'REJECTED BY BACKGROUND CHECK      ', &
         'BACKGROUND CHECK  LEVEL 3         ', &
         'BACKGROUND CHECK  LEVEL 2         ', &
         'BACKGROUND GHECK  LEVEL 1         ', &
         'RESERVED                          ', &
         'REJECTED BY SELECTION PROCESS     ', &
         'GENERATED BY OI                   ', &
         'REJECTION BY  OI                  ', &
         'ELEMENT ON BLACK LIST             ', &
         'RESERVED                          ', &
         'CORRECTED ELEMENT                 ', &
         'INTERPOLATED ELEMENT              ', &
         'DOUBTFUL ELEMENT                  ', &
         'POSSIBLY ERRONEOUS ELEMENT        ', &
         'ERRONEOUS ELEMENT                 ', &
         'ELEMENT EXCEEDS CLIMATE EXTREME   ', &
         'ELEMENT MODIFIED OR GEN BY  ADE   '/

    namelist /namfilt/nelems,nlist,nflags,nlistflg,ltopofilt,rlimlvhu,discardlandsfcwind, &
         nelems_altDiffMax, list_altDiffMax, value_altDiffMax

    filterMode = filterMode_in

    ! set default values for namelist variables
    nlist(:) = 0
    nelems = 6
    nlist(1)=11003
    nlist(2)=11004
    nlist(3)=10194
    nlist(4)=12192
    nlist(5)=12062
    nlist(6)=12063

    nlistflg(:) = 0
    nflags=6
    nlistflg(1)=2
    nlistflg(2)=4
    nlistflg(3)=5
    nlistflg(4)=9
    nlistflg(5)=11
    nlistflg(6)=12

    list_altDiffMax (:) = 0
    value_altDiffMax(:) = -1.d0
    nelems_altDiffMax = 0

    ltopofilt = .true.
    rlimlvhu = 300.d0
    discardlandsfcwind = .true.

    nulnam=0
    ierr=fnom(nulnam,'./flnml','FTN+SEQ+R/O',0)
    read(nulnam,nml=namfilt,iostat=ierr)
    if(ierr.ne.0) call utl_abort('filt_setup: Error reading namelist')
    if(mpi_myid.eq.0) write(*,nml=namfilt)
    ierr=fclos(nulnam)

    ! Force nlist to be in the same sequence as NVNUMB for invariance in
    ! matrix-vector product done in matvec.
    itotelem = 0
    do elem2 = 1, filt_nelems
       elem=obs_get_obs_index_for_bufr_element(nlist(elem2))
       if (elem .ne. -1) then
          itotelem = itotelem + 1
          ielem = nlist(itotelem)
          nlist(itotelem) = nlist(elem2)
          nlist(elem2) = ielem
       else 
          if(mpi_myid.eq.0) write(*,*) 'ELEMENT NOT FOUND IN NVNUMB LIST:',nlist(elem2)
       end if
    end do

    filt_rlimlvhu    = rlimlvhu
    filt_nelems      = nelems
    filt_nlist(:)    = nlist(:)
    filt_nflags      = nflags
    filt_nlistflg(:) = nlistflg(:)

    if(mpi_myid.eq.0) then
       write(*,'(1X,"***********************************")')
       write(*,'(1X," ELEMENTS SELECTED FOR ASSIMILATION:",/)')
       write(*,'(1X,"***********************************")')
       do elem=1,filt_nelems
          write(*,'(15X,I5)') filt_nlist(elem)
       end do
       write(*,'(1X,"***********************************")')
       write(*,*) ' REJECT ELEMENTS WITH REJECT FLAG '
       write(*,*)'           BIT :  '
       do jflag=1,filt_nflags
          ibit= filt_nlistflg(jflag)
          write(*,*) ibit,' ',creason(ibit)
       end do
       write(*,'(1X,"***********************************")')
    end if

    !
    !- Set values for altDiffMax
    !
    if ( nelems_altDiffMax > 0 ) then
      if ( nelems_altDiffMax > numElem ) then
        call utl_abort('filt_setup: You have specify too many altDiffMax elements')
      end if
      do elem = 1, nelems_altDiffMax
        elemIndex = findElemIndex(list_altDiffMax(elem))
        if ( elemIndex >= 1 .and. elemIndex <= numElem ) then
          altDiffMax(elemIndex) = value_altDiffMax(elem)
          write(*,*) ' filt_setup: altDiffMax value for ', elemList(elemIndex), ' is set to ', altDiffMax(elemIndex)
        else
          call utl_abort('filt_setup: Error in value setting for altDiffMax')
        end if
      end do
    end if

  end subroutine filt_setup

  subroutine filt_sethind(obsSpaceData)
    implicit none
    type(struct_obs) :: obsSpaceData
    integer :: ij,idata,idatend,bodyIndex,headerIndex
    !
    ! Set the header index in the body of obsSpaceData
    !
    ij=0
    do headerIndex = 1, obs_numheader(obsSpaceData)
       idata   = obs_headElem_i(obsSpaceData,OBS_RLN,headerIndex)
       idatend = obs_headElem_i(obsSpaceData,OBS_NLV,headerIndex) + idata - 1
       do bodyIndex= idata, idatend
          ij   = ij+1
          call obs_bodySet_i(obsSpaceData,OBS_HIND,IJ, headerIndex)
       end do
    end do
  end subroutine filt_sethind


  subroutine filt_suprep(obsSpaceData)
    !
    !Author  : P. Koclas *CMC/AES  September 1994
    !
    !Revisions:
    !          S. Heilliette, ARMA/MRD, 2016
    !          - Updates for TO family
    !          Y. Rochon. ARQI/AQRD, June 2016
    !          - Move of tvs_Is_idburp_tovs condition within brightness
    !          temp conditional test.
    !
    !*    Purpose: Select the data in the obsSpaceData which are to be assimilated
    !
    implicit none
    type(struct_obs) :: obsSpaceData
    integer bodyIndex,headerIndex
    integer ipres,ivco,ierr,loopIndex
    integer idburp,ivnm,iflg,ibad,iknt,iknt_mpiglobal
    integer ilansea
    logical llok,llrej,llbogus

    if(mpi_myid == 0) write(*,*) 'starting subroutine filt_suprep'

    iknt = 0

    BODY: do bodyIndex = 1, obs_numbody( obsSpaceData )
      headerIndex = obs_bodyElem_i( obsSpaceData, OBS_HIND, bodyIndex   )
      ivnm        = obs_bodyElem_i( obsSpaceData, OBS_VNM , bodyIndex   )
      idburp      = obs_headElem_i( obsSpaceData, OBS_ITY , headerIndex )
      !
      ! Unwanted data types via types specified in NLIST
      !
      llok = .false.
      do loopIndex = 1, filt_nelems
        llok = ( ivnm == filt_nlist( loopIndex ) ) .or. llok
      end do
      if (.not.llok) then
        call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
        cycle BODY
      end if
      !
      ! Allow gz for bogus data only in analysis case 
      !
      llbogus = ( idburp == 150 .or. idburp == 151 .or. idburp == 152 .or. idburp == 153 )
      if  ( (filterMode == 'analysis' .or. filterMode == 'FSO') .and. llok .and. ivnm == BUFR_NEGZ .and. .not.llbogus ) then
        llok=.false.
      end if
      !
      ! Ground-based GPS (GP) data (codtyp 189)
      ! LLOK = .TRUE. DY DEFAULT IF ELEMENT IS IN NLIST
      ! If LASSMET = .FALSE. don't want to assimilate Ps (BUFR_NEPS),
      ! Ts (BUFR_NETS), or (T-Td)s (BUFR_NESS)
      !
      if ( idburp == 189 ) then
        if (.not.lassmet .and. ( ivnm == BUFR_NEPS .or.  &
                                 ivnm == BUFR_NETS .or.  &
                                 ivnm == BUFR_NESS )) then
          llok = .false.
        end if
      end if
      !
      ! Exclude T-Td above level RLIMLVHU (mbs)
      !
      ivco  = obs_bodyElem_i( obsSpaceData, OBS_VCO, bodyIndex )
      ipres = nint( obs_bodyElem_r( obsSpaceData, OBS_PPP, bodyIndex))
      if ( ( ivco == 2) .and. ( ivnm == BUFR_NEES ) .and.  &
           ( ipres < nint( filt_rlimlvhu *100.0d0 )) ) then
        llok=.false.
      end if
      !
      ! Bad data with quality control flags via bit list specified in NLISTFLG
      !
      iflg = obs_bodyElem_i( obsSpaceData, OBS_FLG, bodyIndex )
      llrej = .false.
      do loopIndex = 1, filt_nflags
        ibad = 13 - filt_nlistflg( loopIndex )
        llrej=( btest(iflg,ibad) ) .or. llrej
      end do
      !
      ! Filter TOVS data: check for invalid land/sea/sea-ice flag
      !
      if (ivnm == BUFR_NBT1 .or. ivnm == BUFR_NBT2 .or. ivnm == BUFR_NBT3) then
        if ( tvs_isIdBurpTovs(idburp) ) then
          ilansea  = obs_headElem_i( obsSpaceData, OBS_OFL, headerIndex )
          if (ilansea < 0 .or. ilansea > 2  ) llok = .false.
        end if
      end if

      if ( llok .and. .not. llrej ) then
        call obs_bodySet_i( obsSpaceData, OBS_ASS, bodyIndex, 1 )
        iknt = iknt + 1
      else
        call obs_bodySet_i( obsSpaceData, OBS_ASS, bodyIndex, 0 )
      end if

    end do body

    call rpn_comm_allreduce( iknt, iknt_mpiglobal, 1, "MPI_INTEGER", "MPI_SUM", "GRID", ierr )
    if(mpi_myid == 0) write(*,*) '  Number of data to be assimilated: ', iknt_mpiglobal

    if(mpi_myid == 0) write(*,*) 'end of filt_suprep'

    ! abort if there is no data to be assimilated
    if (iknt_mpiglobal == 0 ) then
       call utl_abort('SUPREP. NO DATA TO BE ASSIMILATED')
    end if

  end subroutine filt_suprep


  subroutine filt_topo(columnhr,obsSpaceData,beSilent)

    ! Revisions:
    !           Y.J. Rochon (ARQI/AQRD), Jan 2015
    !           - Added call to filt_topoChm

    implicit none

    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData
    logical :: beSilent

    if (ltopofilt) then
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *****************************************'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *    filt_topo:                         *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *                                       *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *    FILTER OF OBS DUE TO TOPOGRAPHY    *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *                                       *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *****************************************'
       call filt_toposfc(columnhr,obsSpaceData,beSilent)
       call filt_toporaob(columnhr,obsSpaceData,beSilent)
       call filt_topoaisw(columnhr,obsSpaceData,beSilent)
       call filt_topoprof(columnhr,obsSpaceData,beSilent)
       call filt_topoaladin(columnhr,obsSpaceData,beSilent)
       call filt_topocsbt(columnhr,obsSpaceData,beSilent)
       call filt_topoChm(columnhr,obsSpaceData,beSilent)
    else
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *****************************************'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *    filt_topo:                         *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *                                       *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' * NO FILTER OF OBS DUE TO TOPOGRAPHY    *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *                                       *'
       if(mpi_myid.eq.0 .and. .not.beSilent) write(*,*)' *****************************************'
    end if
  end subroutine filt_topo


  subroutine filt_topoSfc(columnhr,obsSpaceData,beSilent)
    !
    ! Author  : P. Koclas *CMC/AES  October  1998
    !
    ! Purpose: Refuse elements which are too far away from the surface.
    !          Replace the pressure of elements which are slightly below
    !          the model surface by the pressure of the trial field.
    !
    implicit none
    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData
    logical :: beSilent

    logical :: llok
    real(8) :: zdiff
    integer :: headerIndex, bodyIndex, familyIndex, elemIndex
    integer :: ivnm,countAssim
    integer :: countAcc(numElem),countRej(numElem)
    integer, parameter :: numFamily = 3
    character(len=2) :: list_family(numFamily)
    character(len=2) :: varLevel

    !
    ! reset dzmax for gb gps ztd to value in namelist file
    !
    altDiffMax(findElemIndex(BUFR_NEZD)) = dzmax

    if (  .not.beSilent ) then
      write(*,*) ' '
      write(*,*) ' SUBROUTINE filt_topoSFC '
      write(*,*) ' '
      write(*,*) '*****************************************************'
      write(*,222) 'ELEMENTS                  ',(elemList(elemIndex),elemIndex=1,numElem)
      write(*,223) 'REJECTION BOUNDARY(METRE) ',(altDiffMax(elemIndex),elemIndex=1,numElem)
      write(*,*) '*****************************************************'
      write(*,*) ' '
    end if

    ! Loop over the families of interest
    list_family(1) = 'SF'
    list_family(2) = 'UA'
    list_family(3) = 'GP'
    FAMILY: do familyIndex = 1, numFamily

       ! set counters to zero
       countRej(:)=0
       countAcc(:)=0

       ! loop over all header indices of each family
       call obs_set_current_header_list(obsSpaceData, &
            list_family(familyIndex))
       HEADER: do
          headerIndex = obs_getHeaderIndex(obsSpaceData)
          if (headerIndex < 0) exit HEADER

          ! loop over all body indices (still in the same family)
          call obs_set_current_body_list(obsSpaceData, headerIndex)
          BODY: do 
             bodyIndex = obs_getBodyIndex(obsSpaceData)
             if (bodyIndex < 0) exit BODY

             ! skip this obs if it is not on height levels
             if (obs_bodyElem_i(obsSpaceData,OBS_VCO,bodyIndex).ne.1) cycle BODY

             ! skip this obs if already flagged to not be assimilated
             if (obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.0) cycle BODY

             ivnm   = obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex)
             varLevel = vnl_varLevelFromVarnum(ivnm)
             zdiff = abs( obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex) -  &
                  col_getHeight(columnhr,col_getNumLev(columnhr,varLevel),headerIndex,varLevel)/RG )
             !
             ! apply filter to selected elements
             !
             elemIndex = findElemIndex(ivnm)
             if (elemIndex.eq.-1) cycle BODY

             if (zdiff.le.altDiffMax(elemIndex)) then
                ! obs passes the acceptance criteria
                countAcc(elemIndex) = countAcc(elemIndex)+1
             else
                ! obs does not pass the acceptance criteria, reject it
                call obs_bodySet_i( obsSpaceData,OBS_FLG,bodyIndex,  &
                     ibset(obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex),18) )
                call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
                countRej(elemIndex) = countRej(elemIndex)+1
             end if
          end do BODY
       end do HEADER

       if ( .not.beSilent ) then
         write(*,*) ' '
         write(*,*) '*****************************************************'
         write(*,*) 'FAMILY = ',list_family(familyIndex)
         write(*,222) 'ELEMENTS            ', (elemList(elemIndex),elemIndex=1,numElem)
         write(*,222) 'ACCEPTED  ',(countAcc(elemIndex),elemIndex=1,numElem)
         write(*,222) 'REJECTED  ',(countRej(elemIndex),elemIndex=1,numElem)
         write(*,*) '*****************************************************'
         write(*,*) ' '
       end if
222    format(2x,a29,16(2x,i5))
223    format(2x,a29,16(2x,f5.0))

    end do FAMILY

    countAssim=0
    do bodyIndex=1,obs_numbody(obsSpaceData)
       if ( obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex) .eq. 1) countAssim=countAssim+1
    end do
    if ( .not.beSilent ) write(*,'(1X," NUMBER OF DATA TO BE ASSIMILATED AFTER ADJUSTMENTS: ",i10)') countAssim
    if ( .not.beSilent ) write(*,* ) ' '

  end subroutine filt_toposfc


  subroutine filt_topoRaob(columnhr,obsSpaceData,beSilent)
    !
    ! Author  : P. Koclas *CMC/AES  October  1998
    !
    ! Purpose: Refuse elements which are too far away from the surface of the model
    !          Refuse elements which are considered in the free atmosphere of
    !          the RAOB but fall in the surface boundary layer of the model atmosphere.
    !
    implicit none
    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData
    logical :: beSilent

    integer :: headerIndex, bodyIndex, listIndex, elemIndex
    integer :: ivnm, iass, countAssim
    integer :: itotacc(numElem), itotrej(numElem), isblrej(numElem)
    integer :: igzacc(numElem), igzrej(numElem), ibndrej(numElem)
    real(8) :: zval, zlev, zdiff, zdifalt
    real(8) :: zStnAlt, zpb, zpt, zdelp
    logical :: llok

    if ( .not.beSilent ) then
      write(*,*) ' '
      write(*,*) ' SUBROUTINE filt_topoRAOB '
      write(*,*) ' '
      write(*,*) '************************************************'
      write(*,222) ' ELEMENTS                  ',(elemList(elemIndex),elemIndex=1,numElem)
      write(*,223) ' REJECTION BOUNDARY(METRE) ',(altDiffMax(elemIndex),elemIndex=1,numElem)
      write(*,223) ' REJECTION SBL (PASCAL)    ',(bndryCritPres,elemIndex=1,numElem)
      write(*,*) '************************************************'
      write(*,*) ' '
    end if

    ! set counters to zero
    itotrej(:)=0
    itotacc(:)=0
    isblrej(:)=0
    igzacc(:)=0
    igzrej(:)=0
    ibndrej(:)=0

    ! loop over all header indices of the 'UA' family
    call obs_set_current_header_list(obsSpaceData, 'UA')
    HEADER: do
       headerIndex = obs_getHeaderIndex(obsSpaceData)
       if (headerIndex < 0) exit HEADER

       ! HEIGHT GZ

       ! loop over all body indices (still in the 'UA' family)
       call obs_set_current_body_list(obsSpaceData, headerIndex)
       BODY: do 
          bodyIndex = obs_getBodyIndex(obsSpaceData)
          if (bodyIndex < 0) exit BODY

          ! skip this obs if it is not on pressure level
          if( obs_bodyElem_i(obsSpaceData,OBS_VCO,bodyIndex).ne.2 ) cycle BODY

          ! skip this obs if already flagged to not be assimilated
          if( obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.0 ) cycle BODY

          ! skip this obs if it is not GZ
          ivnm=obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex)
          listIndex = findElemIndex(ivnm)
          llok = (ivnm.eq.BUFR_NEGZ .and. listIndex.ne.-1)
          if (.not. llok ) cycle BODY

          zlev=obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex)
          zval=obs_bodyElem_r(obsSpaceData,OBS_VAR,bodyIndex)
          zdiff= ( zval - col_getGZsfc(columnhr,headerIndex) )/RG
          ! obs is above surface, so it is ok, lets jump to the next obs
          if(zdiff .ge. 0.0d0) cycle BODY

          if(zdiff .ge. -1.0d0*altDiffMax(listIndex)) then
             ! obs is an acceptably small distance below the surface
             itotacc(listIndex) = itotacc(listIndex)+1
             igzacc(listIndex) = igzacc(listIndex)+1
          else
             ! too far below surface, reject
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                  ibset( obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex), 18 ))
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             itotrej(listIndex) = itotrej(listIndex)+1
             igzrej(listIndex) = igzrej(listIndex)+1
          end if
       end do BODY
       !
       !   REJECT ELEMENTS OF U,V,T-TD,T BELOW THE MODEL SURFACE
       !   AND THOSE NON SURFACE ELEMENTS PRESENT IN THE SURFACE
       !   BOUNDARY LAYER OF THE RAOB OR OF THE MODEL.
       !   AT THIS POINT WE WANT TO KEEP OBSERVATIONS IN THE FREE
       !   ATMOSPHERE
       !
       !---Special case if station elevation is above model elevation
       !   we want to define zpt at a level above the station.
       !   To approximate that value, we will transform the difference
       !   between the 2 elevations into a difference in pressure using
       !   the rule of thumb (1Mb =8 metres)
       !---Even though TT(element=12001) is not assimmilated
       !   it is treated as if it were for the evaluation step.
       !   Otherwise we use observations of TT that are too far
       !   from the model topography in the verification.

       zStnAlt = obs_headElem_r(obsSpaceData,OBS_ALT,headerIndex)
       zdifalt = zStnAlt- col_getGZsfc(columnhr,headerIndex)/RG

       ! Set the body list & start at the beginning of the list
       call obs_set_current_body_list(obsSpaceData, headerIndex)
       BODY2: do 
          bodyIndex = obs_getBodyIndex(obsSpaceData)
          if (bodyIndex < 0) exit BODY2

          if( obs_bodyElem_i(obsSpaceData,OBS_VCO,bodyIndex).ne.2 ) cycle BODY2

          ! skip this obs if already flagged to not be assimilated
          if( obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.0 ) cycle BODY2

          ivnm=obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex)
          listIndex = findElemIndex(ivnm)
          llok = (ivnm.ne.BUFR_NEGZ .and. listIndex.ne.-1)
          if (.not. llok ) cycle BODY2 ! Proceed with the next bodyIndex

          zlev=obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex)
          zpb = col_getElem(columnhr,1,headerIndex,'P0')
          zpt = zpb - bndryCritPres
          zdelp = 999999.0d0
          if (zdifalt .gt. 0.0d0) then
             zdelp = zdifalt * 100.d0 / 8.0d0
             zpt   = zpb - (zdelp + bndryCritPres)
          end if

          if(abs(zdifalt).le.altDiffMax(listIndex)) then
             !--Model surface and station altitude are very close
             !  Accept observation if zlev is within the domain
             !  of the trial field
             zpb = col_getElem(columnhr,1,headerIndex,'P0')
             zpt = col_getPressure(columnhr,col_getNumLev(columnhr,'MM')-1,headerIndex,'MM')
          end if
          if(zlev .gt. zpb ) then
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                  ibset(obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex), 18 ))
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             itotrej(listIndex) = itotrej(listIndex) + 1
             ibndrej(listIndex) = ibndrej(listIndex) + 1
          else if(zlev.le.zpb .and. zlev.gt.zpt ) then
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                  ibset( obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex), 18 ))
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             itotrej(listIndex) = itotrej(listIndex) + 1
             isblrej(listIndex) = isblrej(listIndex) + 1
          end if
       end do BODY2
    end do HEADER

    if ( .not.beSilent ) then
      write(*,*) ' '
      write(*,*) '***************************************'
      write(*,*) 'FAMILY = UA'
      write(*,222) ' ELEMENTS          ', (elemList(elemIndex),elemIndex=1,numElem)
      write(*,222) ' ACC GZ EXT  ',(igzacc(elemIndex),elemIndex=1,numElem)
      write(*,222) ' ACC TOTAL   ',(itotacc(elemIndex),elemIndex=1,numElem)
      write(*,*) '***************'
      write(*,222) ' REJ GZ EXT  ',(igzrej(elemIndex),elemIndex=1,numElem)
      write(*,222) ' REJ OUT BND ',(ibndrej(elemIndex),elemIndex=1,numElem)
      write(*,222) ' REJ SBL     ',(isblrej(elemIndex),elemIndex=1,numElem)
      write(*,222) ' REJ TOTAL   ',(itotrej(elemIndex),elemIndex=1,numElem)
      write(*,*) '***************************************'
      write(*,*) ' '
    end if
222 format(2x,a29,16(2x,i5))
223 format(2x,a29,16(2x,f6.0))

    countAssim=0
    do bodyIndex=1,obs_numbody(obsSpaceData)
       if ( obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex) .eq. 1) countAssim=countAssim+1
    end do
    if ( .not.beSilent ) write(*,'(1X," NUMBER OF DATA TO BE ASSIMILATED AFTER ADJUSTMENTS:",i10)') countAssim
    if ( .not.beSilent ) write(*,*) ' '

  end subroutine filt_toporaob


  subroutine filt_topoAISW(columnhr,obsSpaceData,beSilent)
    !
    ! Author  : R. Sarrazin *CMC/AES  February 2000
    !
    ! Purpose:  Refuse elements which are too close to the surface.
    !
    implicit none
    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData
    logical :: beSilent

    integer :: headerIndex, bodyIndex, familyIndex, listIndex, elemIndex
    integer :: ivnm, countRej(numElem), countAssim
    real(8) :: zval, zdiff
    character(len=2) :: list_family(2)

    !    DATA    RLISTCRIT/50.d0/
    !    DATA    ILISTEL/11003,11004,12001,12192/

    if ( .not.beSilent ) then
      write(*,*) ' '
      write(*,*) ' SUBROUTINE filt_topoAISW '
      write(*,*) ' '
      write(*,*) '****************************************************'
      write(*,222) 'ELEMENTS                 ', (elemList(elemIndex),elemIndex=1,numElem)
      write(*,223) 'REJECTION BOUNDARY(HPA)  ', (bndryCritPres,elemIndex=1,numElem)
      write(*,*) '****************************************************'
      write(*,*) ' '
    end if

    ! Loop over the families of interest
    list_family(1) = 'AI'
    list_family(2) = 'SW'
    FAMILY: do familyIndex = 1,2

       ! set counters to zero
       countRej(:)=0

       ! loop over all body indices of each family
       call obs_set_current_body_list(obsSpaceData, list_family(familyIndex))
       BODY: do 
          bodyIndex = obs_getBodyIndex(obsSpaceData)
          if (bodyIndex < 0) exit BODY

          ! skip this observation if already flagged to not assimilate
          if(obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.0) cycle BODY

          !
          ! reject data too close to the model orography, put to
          ! model orography, data which is below , but close to the surface.
          !
          zval = obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex)
          headerIndex = obs_bodyElem_i(obsSpaceData,OBS_HIND,bodyIndex)
          zdiff = col_getElem(columnhr,1,headerIndex,'P0') - zval
          if ( zdiff .lt. bndryCritPres ) then
             ivnm=obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex)
             listIndex = findElemIndex(ivnm)
             if(listIndex.eq.-1) cycle BODY
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             countRej(listIndex)=countRej(listIndex)+1
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                  ibset(obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex),18 ))
          end if
       end do BODY

       if ( .not.beSilent ) then
         write(*,*) ' '
         write(*,*) '*****************************************************************'
         write(*,*) ' FAMILY = ',list_family(familyIndex)
         write(*,222) 'ELEMENTS            ',(elemList(elemIndex),elemIndex=1,numElem)
         write(*,222) 'REJECTED  ',(countRej(elemIndex),elemIndex=1,numElem)
         write(*,*) '*****************************************************************'
         write(*,*) ' '
       end if

    end do FAMILY

    countAssim=0
    do bodyIndex=1,obs_numbody(obsSpaceData)
       if ( obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.1) countAssim=countAssim+1
    end do
    if ( .not.beSilent ) write(*,'(1X," NUMBER OF DATA TO BE ASSIMILATED AFTER ADJUSTMENTS:",i10)') countAssim
    if ( .not.beSilent ) write(*,*) ' '

222 format(2x,a29,16(2x,i5))
223 format(2x,a29,16(2x,f5.0))

  end subroutine filt_topoaisw


  subroutine filt_topoProf(columnhr,obsSpaceData,beSilent)
    !
    ! Author: J. St-James October 2002
    !          - Based on the subroutine filt_toporaob.  Adapt to
    !            Profiler data
    !
    ! Purpose: Refuse elements which are too far away from the surface of the model
    !          Refuse elements which are considered in the free atmosphere of
    !          the RAOB but fall in the surface boundary layer of the model atmosphere.
    !
    implicit none
    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData
    logical :: beSilent

    integer :: headerIndex, bodyIndex, listIndex, elemIndex
    integer :: ivnm, countAssim
    integer :: itotrej(numElem), isblrej(numElem), ibndrej(numElem)
    real(8) :: zval,zlev
    real(8) :: zStnAlt,zModAlt,zpb,zpt
    logical :: llok, list_is_empty

    if ( .not.beSilent ) then
      write(*,*) ' '
      write(*,*) ' SUBROUTINE filt_topoPROF '
      write(*,*) ' '
      write(*,*) '************************************************'
      write(*,222) ' ELEMENTS                  ',(elemList(elemIndex),elemIndex=1,numElem)
      write(*,223) ' REJECTION BOUNDARY(METRE) ',(altDiffMax(elemIndex),elemIndex=1,numElem)
      write(*,223) ' REJECTION SBL (METRE) ',(bndryCritHeightProf,elemIndex=1,numElem)
      write(*,*) '************************************************'
      write(*,*) ' '
    end if

    ! set counters to zero
    itotrej(:)=0
    isblrej(:)=0
    ibndrej(:)=0

    ! loop over all header indices of the 'PR' family
    call obs_set_current_header_list(obsSpaceData, 'PR')
    HEADER: do
       headerIndex = obs_getHeaderIndex(obsSpaceData)
       if (headerIndex < 0) exit HEADER

       ! Set the body list & start at the beginning of the list
       call obs_set_current_body_list(obsSpaceData, headerIndex,list_is_empty)
       if (list_is_empty) cycle HEADER ! Proceed to the next HEADER

       !
       ! REJECT OBS BELOW THE MODEL SURFACE
       ! AND THOSE NON SURFACE ELEMENTS PRESENT IN THE SURFACE
       ! BOUNDARY LAYER OF THE RAOB OR OF THE MODEL.
       ! AT THIS POINT WE WANT TO KEEP OBSERVATIONS IN THE FREE
       ! ATMOSPHERE
       !
       zModAlt = col_getGZsfc(columnhr,headerIndex)/RG
       zStnAlt = obs_headElem_r(obsSpaceData,OBS_ALT,headerIndex)

       ! loop over all body indices (still in the 'PR' family)
       BODY: do 
          bodyIndex = obs_getBodyIndex(obsSpaceData)
          if (bodyIndex < 0) exit BODY

          ! skip this obs if already flagged to not be assimilated
          if (obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.0) cycle BODY

          ivnm = obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex)
          listIndex = findElemIndex(ivnm)
          llok = (obs_bodyElem_i(obsSpaceData,OBS_VCO,bodyIndex).eq.1  &
               .and. ivnm.ne.BUFR_NEGZ .and. listIndex.ne.-1)
          if (.not. llok ) cycle BODY ! Proceed to the next bodyIndex

          zlev = obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex)
          zpb = zModAlt
          if (zStnAlt .gt. zModAlt) then
             zpt = zStnAlt + bndryCritHeightProf
          else
             zpt = zModAlt + bndryCritHeightProf
          end if
          if(abs(zStnAlt-zModAlt).le.altDiffMax(listIndex)) then
             !----Model surface and station altitude are very close
             !    Accept observation if zlev is within the domain
             !    of the trial field
             zpb = zModAlt
             zpt = col_getHeight(columnhr,col_getNumLev(columnhr,'MM')-1,headerIndex,'MM')/RG
          end if
          if(zlev.lt.zpb ) then
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                  ibset( obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex), 18 ))
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             itotrej(listIndex)=itotrej(listIndex)+1
             ibndrej(listIndex)=ibndrej(listIndex)+1
          else if(zlev.ge.zpb .and. zlev.lt.zpt ) then
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                  ibset( obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex), 18 ))
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             itotrej(listIndex)=itotrej(listIndex)+1
             isblrej(listIndex)=isblrej(listIndex)+1
          end if
       end do BODY
    end do HEADER

    if ( .not.beSilent ) then
      write(*,*) ' '
      write(*,*) '***************************************'
      write(*,*) 'FAMILY = PR'
      write(*,222) ' ELEMENTS          ', (elemList(elemIndex),elemIndex=1,numElem)
      write(*,*) '************'
      write(*,222) ' REJ OUT BND ',(ibndrej(elemIndex),elemIndex=1,numElem)
      write(*,222) ' REJ SBL     ',(isblrej(elemIndex),elemIndex=1,numElem)
      write(*,222) ' REJ TOTAL   ',(itotrej(elemIndex),elemIndex=1,numElem)
      write(*,*) '***************************************'
      write(*,*) ' '
    end if
222 format(2x,a29,16(2x,i5))
223 format(2x,a29,16(2x,f6.0))

    countAssim=0
    do bodyIndex=1,obs_numbody(obsSpaceData)
       if (obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.1) countAssim=countAssim+1
    end do
    if ( .not.beSilent ) write(*,'(1X," NUMBER OF DATA TO BE ASSIMILATED AFTER ADJUSTMENTS:",i10)') countAssim
    if ( .not.beSilent ) write(*,*) ' '

  end subroutine filt_topoProf


!--------------------------------------------------------------------------
!!
!! *Purpose*: Refuse elements which are considered to be in the free atmosphere
!!            of the Aladin instrument but which fall in the surface boundary
!!            layer of the model atmosphere.
!!
!--------------------------------------------------------------------------
  subroutine filt_topoAladin(columnhr,obsSpaceData,beSilent)
    !
    ! Author: JW Blezius March 2018
    !          - Based on the subroutines filt_topoProf and filt_topoSfc.  Adapt
    !            to Aladin data.
    !
    implicit none
    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData

    integer :: headerIndex, bodyIndex, elemIndex
    integer :: ivnm, countAssim
    integer :: countAcc(numElem), countRej(numElem)
    real(8) :: zlev    ! altitide of the observation
    real(8) :: zModAlt ! altitude of the model's lowest layer
    real(8) :: zpt     ! top of the boundary layer
    logical :: list_is_empty, beSilent

    if(.not. beSilent )then
      write(*,*) ' '
      write(*,*) ' SUBROUTINE filt_topoALADIN '
      write(*,*) ' '
      write(*,*) '************************************************'
      write(*,222) ' ELEMENTS              ',(elemList(elemIndex),elemIndex=1,numElem)
      write(*,223) ' REJECTION SBL (METRE) ',(bndryCritHeightAladin,elemIndex=1,numElem)
      write(*,*) '************************************************'
      write(*,*) ' '
    end if

    ! set counter to zero
    countAcc(:)=0
    countRej(:)=0

    ! loop over all header indices of the 'AL' family
    call obs_set_current_header_list(obsSpaceData, 'AL')
    HEADER: do
       headerIndex = obs_getHeaderIndex(obsSpaceData)
       if (headerIndex < 0) exit HEADER

       ! Set the body list & start at the beginning of the list
       call obs_set_current_body_list(obsSpaceData, headerIndex,list_is_empty)
       if (list_is_empty) cycle HEADER ! Proceed to the next HEADER

       !
       ! REJECT OBS IN THE SURFACE BOUNDARY LAYER OF THE MODEL.
       ! AT THIS POINT WE WANT TO KEEP OBSERVATIONS THAT ARE IN THE FREE
       ! ATMOSPHERE
       !
       zModAlt = col_getHeight(columnhr,col_getNumLev(columnhr,'MM'), &
                               headerIndex,'MM')/RG 
       zpt = zModAlt + bndryCritHeightAladin

       ! Loop over all body indices (still in the 'AL' family)
       BODY: do 
          bodyIndex = obs_getBodyIndex(obsSpaceData)
          if (bodyIndex < 0) exit BODY

          ! Skip this obs if it is not on height levels
          if (obs_bodyElem_i(obsSpaceData,OBS_VCO,bodyIndex) /= 1) cycle BODY

          ! Skip this obs if already flagged not to be assimilated
          if (obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex) == 0) cycle BODY

          ! Skip this obs if it is not in the list of element types
          ivnm = obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex)
          elemIndex = findElemIndex(ivnm)
          if(elemIndex == -1) cycle BODY


          !
          ! apply filter to selected elements
          !

          ! Reject this obs if it is in the boundary layer or below
          zlev = obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex)
          if(zlev > zpt) then
             ! obs passes the acceptance criterion
             countAcc(elemIndex) = countAcc(elemIndex)+1

          else
             ! Flag rejection due to orography
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                  ibset( obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex), 18 ))

             ! Do not assimilate the observation
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             countRej(elemIndex)=countRej(elemIndex)+1
          end if
       end do BODY
    end do HEADER

    if(.not. besilent)then
      write(*,*) ' '
      write(*,*) '***************************************'
      write(*,*) 'FAMILY = AL'
      write(*,222) ' ELEMENTS  ', (elemList(elemIndex),elemIndex=1,numElem)
      write(*,222) ' ACCEPTED  ', (countAcc(elemIndex),elemIndex=1,numElem)
      write(*,222) ' REJECTED  ', (countRej(elemIndex),elemIndex=1,numElem)
      write(*,*) '***************************************'
      write(*,*) ' '
    end if
222 format(2x,a29,16(2x,i5))
223 format(2x,a29,16(2x,f6.0))

    countAssim=0
    do bodyIndex=1,obs_numbody(obsSpaceData)
       if (obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex) == 1) countAssim=countAssim+1
    end do

    if(.not. besilent)then
      write(*,'(1X," NUMBER OF DATA TO BE ASSIMILATED AFTER ADJUSTMENTS:",i10)') countAssim
      write(*,*) ' '
    end if

  end subroutine filt_topoAladin


  subroutine filt_topoCsbt(columnhr,obsSpaceData,beSilent)
    !
    ! Author  : R. Sarrazin *CMC/AES  June 2008
    ! Revision: A. Beaulne  CMDA      October 2012
    !           Set bit 9 in addition to the already setting of bit 18
    !
    ! Purpose:  Refuse data which are too close to the surface.
    !
    implicit none
    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData
    logical :: beSilent

    integer :: headerIndex, bodyIndex, elemIndex
    integer :: idatyp, countAssim, countRej
    real(8), parameter :: minSfcPressure = 80000.d0

    if ( .not.beSilent ) then
      write(*,* ) ' '
      write(*,* ) ' SUBROUTINE filt_topoCSBT '
      write(*,* ) ' '
      write(*,* ) '****************************************************'
      write(*,222) 'ELEMENTS                  ', BUFR_NBT3
      write(*,223) 'MINIMUM SFC PRESSURE (PA) ', minSfcPressure
      write(*,* ) '****************************************************'
      write(*,* ) ' '
    end if

    ! set counters to zero
    countRej=0

    ! loop over all header indices of the 'TO' family
    call obs_set_current_header_list(obsSpaceData, 'TO')
    HEADER: do
       headerIndex = obs_getHeaderIndex(obsSpaceData)
       if (headerIndex < 0) exit HEADER

       idatyp   = obs_headElem_i(obsSpaceData,OBS_ITY,headerIndex)
       if (idatyp .ne. 185) cycle HEADER ! Proceed to the next headerIndex

       ! loop over all body indices (still in the 'TO' family)
       call obs_set_current_body_list(obsSpaceData, headerIndex)
       BODY: do 
          bodyIndex = obs_getBodyIndex(obsSpaceData)
          if (bodyIndex < 0) exit BODY

          if (obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex).ne.BUFR_NBT3) cycle BODY

          ! reject obs if the model surface pressure is below the minimum specified value
          if (col_getElem(columnhr,1,headerIndex,'P0') .lt. minSfcPressure) then
             call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
             countRej=countRej+1
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex, &
                  ibset(obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex),9))
             call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex, &
                  ibset(obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex),18))
          end if

       end do BODY
    end do HEADER

    if ( .not.beSilent ) then
      write(*,*) ' '
      write(*,*) '*****************************************************************'
      write(*,*) ' FAMILY = TO'
      write(*,222) 'ELEMENTS            ', BUFR_NBT3
      write(*,222) 'REJECTED  ',countRej
      write(*,*) '*****************************************************************'
      write(*,*) ' '
    end if
222 format(2x,a29,1(4x,i5))
223 format(2x,a29,1(2x,f7.0))

    countAssim=0
    do bodyIndex=1,obs_numbody(obsSpaceData)
       if ( obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex) .EQ. 1) countAssim=countAssim+1
    end do
    if ( .not.beSilent ) write(*,'(1X," NUMBER OF DATA TO BE ASSIMILATED AFTER ADJUSTMENTS:",i10)') countAssim
    if ( .not.beSilent ) write(*,* ) ' '

  end subroutine filt_topocsbt

  SUBROUTINE filt_surfaceWind(lobsSpaceData,beSilent)
    !
    !*    Purpose:
    !      Zap sfc wind components at land stations
    IMPLICIT NONE
    type(struct_obs) :: lobsSpaceData
    logical :: beSilent

    INTEGER JPINEL,JPIDLND
    PARAMETER(JPINEL=2,JPIDLND=9)
    INTEGER J,JID,JDATA
    LOGICAL LLPRINT
    REAL(8) ZDIFF
    INTEGER ITYP,IDBURP
    INTEGER ILISTEL(JPINEL),IDLND(JPIDLND)
    INTEGER IKOUNTREJ(JPINEL),IKOUNTT
    !                     SYNOP(3)     TEMP/PILOT(6)
    character(len=2), dimension(2) :: list_family
    integer :: index_family, index_header, index_body

    DATA    IDLND / 12, 14, 146, 32, 35, 135, 136, 137, 138 /
    !
    if ( .not. discardlandsfcwind ) return
    !
    ILISTEL(1)=BUFR_NEUS
    ILISTEL(2)=BUFR_NEVS
    if ( .not.beSilent ) then
      WRITE(*,* ) ' '
      WRITE(*,* ) ' SUBROUTINE SFCWNDZAP '
      WRITE(*,* ) ' '
      WRITE(*,* ) '*****************************************************'
      WRITE(*,222)'ELEMENTS REJECTED         ',(  ILISTEL(J),J=1,jpinel)
      WRITE(*,222)'LIST OF IDTYP             ',(   idlnd(J),J=1,jpidlnd)
      WRITE(*,* ) '*****************************************************'
      WRITE(*,* ) ' '
    end if
    LLPRINT = .FALSE.
    !cc      LLPRINT = .TRUE.
    !
    !     SET COUNTERS TO ZERO
    !
    DO J=1,JPINEL
       IKOUNTREJ(J)=0
    END DO

    !
    ! Loop over the families of interest
    !
    list_family(1) = 'SF'
    list_family(2) = 'UA'
    do index_family = 1,2
       if ( .not.beSilent ) WRITE(*,'(2x,A9,2x,A2)')'FAMILY = ',list_family(index_family)

       !
       ! loop over all header indices of each family
       !
       ! Set the header list
       ! (& start at the beginning of the list)
       call obs_set_current_header_list(lobsSpaceData, &
            list_family(index_family))
       HEADER: do
          index_header = obs_getHeaderIndex(lobsSpaceData)
          if (index_header < 0) exit HEADER

          !
          ! loop over all body indices (still in the same family)
          !
          ! Set the body list
          ! (& start at the beginning of the list)
          call obs_set_current_body_list(lobsSpaceData, index_header)
          BODY: do 
             index_body = obs_getBodyIndex(lobsSpaceData)
             if (index_body < 0) exit BODY

             !             UNCONDITIONALLY REJECT SURFACE WINDS AT SYNOP/TEMP LAND STATIONS
             ITYP=obs_bodyElem_i(lobsSpaceData,OBS_VNM,INDEX_BODY)
             IDBURP = obs_headElem_i(lobsSpaceData,OBS_ITY,INDEX_HEADER)
             IF ( ITYP.EQ.BUFR_NEUS .OR. ITYP.EQ.BUFR_NEVS) THEN
                DO JID = 1, JPIDLND
                   IF(IDBURP .EQ. IDLND(JID) .AND. &
                        obs_bodyElem_i(lobsSpaceData,OBS_ASS,INDEX_BODY) .EQ. 1) THEN
                      call obs_bodySet_i(lobsSpaceData,OBS_FLG,INDEX_BODY, &
                           ibset( obs_bodyElem_i(lobsSpaceData,OBS_FLG,INDEX_BODY), 19))
                      call obs_bodySet_i(lobsSpaceData,OBS_ASS,INDEX_BODY,0)
                      DO J = 1, JPINEL
                         IF(ITYP .EQ.ILISTEL(J)) THEN
                            IKOUNTREJ(J)=IKOUNTREJ(J)+1
                         END IF
                      END DO
                      IF(LLPRINT .and. .not.beSilent ) THEN
                         WRITE(*,225) 'Rej sfc wind lnd',INDEX_HEADER,ITYP &
                              ,obs_elem_c(lobsSpaceData,'STID',INDEX_HEADER),IDBURP &
                              ,obs_headElem_r(lobsSpaceData,OBS_LAT,INDEX_HEADER) &
                              ,obs_headElem_r(lobsSpaceData,OBS_LON,INDEX_HEADER) &
                              ,obs_bodyElem_r(lobsSpaceData,OBS_PPP,INDEX_BODY),ZDIFF
                      END IF
                   END IF
                END DO
             END IF ! BUFR_NEUS or BUFR_NEVS
          END DO BODY
       END DO HEADER
       !
       if ( .not.beSilent ) then
         WRITE(*,* ) ' '
         WRITE(*,* ) '*****************************************************'
         WRITE(*,222 )'ELEMENTS            ', (  ILISTEL(J),J=1,JPINEL)
         WRITE(*,222)'REJECTED             ',(IKOUNTREJ(J),J=1,JPINEL)
         WRITE(*,* ) '*****************************************************'
         WRITE(*,* ) ' '
       END IF
222    FORMAT(2x,a29,10(2x,i5))
223    FORMAT(2x,a29,10(2x,f5.0))
224    FORMAT(2x,a17,2x,I6,2X,I5,1x,a9,1x,2(2x,f9.2))
225    FORMAT(2x,a13,2x,I6,2X,I5,1x,a9,1x,I6,1x,4(2x,f9.2))
       !
    END DO ! family
    !
    IKOUNTT=0
    DO JDATA=1,obs_numbody(lobsSpaceData)
       IF ( obs_bodyElem_i(lobsSpaceData,OBS_ASS,JDATA) .EQ. 1) IKOUNTT=IKOUNTT+1
    END DO
    if ( .not.beSilent ) WRITE(*, &
         '(1X," NUMBER OF DATA ASSIMILATED BY 3D","-VAR AFTER ADJUSTMENTS: ",i10)') &
         IKOUNTT
    if ( .not.beSilent ) WRITE(*,* ) ' '

  END SUBROUTINE filt_surfaceWind

  SUBROUTINE FILT_GPSRO(lcolumnhr,lobsSpaceData)

    !**s/r FILTERGPSRO - Filter GPSRO observations
    !                    Guarantee that altitude and observation values are
    !                    within bounds for further processing
    !
    !                    For noncompliant GPSRO observations:
    !                    -Set assimilable flag to 0
    !                    -Set bit of cma flag 11 ON
    !
    use gps_mod
    IMPLICIT NONE
    !
    type(struct_columnData) :: lcolumnhr
    type(struct_obs)        :: lobsSpaceData
    !
    INTEGER INDEX_HEADER, IDATYP, INDEX_BODY
    INTEGER JL, IAZM, ISAT, ICLF, iProfile, NH, I
    REAL*8 ZMT, Rad, Geo, zLat, zLon, Lat, Lon
    REAL*8 HNH1, HSF, HTP, HGP, HMIN, HMAX, ZOBS, ZREF
    LOGICAL LLEV, LOBS, LNOM, LSAT
    !
    WRITE(*,*)'ENTER FILTERGPSRO'
    !
    !     Loop over all header indices of the 'RO' family:
    !
    call obs_set_current_header_list(lobsSpaceData,'RO')
    gps_numROProfiles=0
    HEADER: do
       index_header = obs_getHeaderIndex(lobsSpaceData)
       if (index_header < 0) exit HEADER
       !
       !     *  Process only refractivity data (codtyp 169)
       !
       IDATYP = obs_headElem_i(lobsSpaceData,OBS_ITY,INDEX_HEADER)
       IF ( IDATYP .EQ. 169 ) THEN
          gps_numROProfiles=gps_numROProfiles+1
          !
          !     *     Basic geometric variables of the profile:
          !
          IAZM = obs_headElem_i(lobsSpaceData,OBS_AZA,INDEX_HEADER)
          ISAT = obs_headElem_i(lobsSpaceData,OBS_SAT,INDEX_HEADER)
          ICLF = obs_headElem_i(lobsSpaceData,OBS_ROQF,INDEX_HEADER)
          Rad  = obs_headElem_r(lobsSpaceData,OBS_TRAD,INDEX_HEADER)
          Geo  = obs_headElem_r(lobsSpaceData,OBS_GEOI,INDEX_HEADER)
          LNOM = .NOT.BTEST(ICLF,16-1)
          !
          !     *     Check if the satellite is within the accepted set:
          !
          IF ( NUMGPSSATS .GE. 1 ) THEN
             LSAT = .FALSE.
             DO I=1,NUMGPSSATS
                LSAT=( LSAT .OR. (ISAT.EQ.IGPSSAT(I)) )
             END DO
          ELSE
             LSAT = .TRUE.
          END IF
          !
          ZMT = col_getGZsfc(lcolumnhr,index_header)/RG
          !
          !     *     Acceptable height limits:
          !
          JL = 1
          HTP = col_getHeight(lcolumnhr,JL,INDEX_HEADER,'TH')/RG
          HSF = ZMT+SURFMIN
          !
          !     *     Discard low data for METOP/GRAS:
          !
          IF ( NUMGPSSATS .GE. 1 ) THEN
             IF ( ISAT.EQ.3 .OR. ISAT.EQ.4 .OR. ISAT.EQ.5 ) THEN
                IF (HSF .LT. 10000.d0) HSF=10000.d0
             END IF
          END IF
          !
          !     *     Min/max altitudes:
          !
          IF (HSF .LT. HSFMIN) HSF=HSFMIN
          IF (HTP .GT. HTPMAX) HTP=HTPMAX
          HMIN=Geo+HSF
          HMAX=Geo+HTP
          !
          !     *     Loop over all body indices for this index_header:
          !     *     (start at the beginning of the list)
          !
          call obs_set_current_body_list(lobsSpaceData, INDEX_HEADER)
          BODY: do 
             index_body = obs_getBodyIndex(lobsSpaceData)
             if (index_body < 0) exit BODY
             !
             !     *        Altitude:
             !
             HNH1= obs_bodyElem_r(lobsSpaceData,OBS_PPP,INDEX_BODY)
             IF (LEVELGPSRO.EQ.1) HNH1=HNH1-Rad
             !
             !     *        Observation:
             !
             ZOBS= obs_bodyElem_r(lobsSpaceData,OBS_VAR,INDEX_BODY)
             !
             !     *        Reference order of magnitude value:
             !
             IF (LEVELGPSRO.EQ.1) THEN
                ZREF = 0.025d0*exp(-HNH1/6500.d0)
             ELSE
                ZREF = 300.d0*exp(-HNH1/6500.d0)
             END IF
             !
             !     *        Positively verify that the altitude is within bounds:
             !
             LLEV= (HNH1.GT.HMIN) .AND. (HNH1.LT.HMAX)
             !
             !     *        Positively verify that the observable is within bounds:
             !
             LOBS= (ZOBS.GT.(0.3d0*ZREF)) .AND. (ZOBS.LT.(3.d0*ZREF))
             !
             !     *        Mark as not assimilable unless all conditions are satisfied:
             !
             IF ( .NOT.LLEV .OR. .NOT.LOBS .OR. IAZM.LT.0 .OR. .NOT.LNOM .OR. .NOT.LSAT) THEN
                call obs_bodySet_i(lobsSpaceData,OBS_ASS,INDEX_BODY, 0)
                call obs_bodySet_i(lobsSpaceData,OBS_FLG,INDEX_BODY, IBSET(obs_bodyElem_i(lobsSpaceData,OBS_FLG,INDEX_BODY),11))
             END IF
          END DO BODY

       END IF

    END DO HEADER

    IF (gps_numROProfiles.GT.0) THEN
       if(.not.allocated(gps_vRO_IndexPrf)) allocate(gps_vRO_IndexPrf(gps_numROProfiles))

       iProfile=0
       !
       !     *  Loop over all header indices of the 'RO' family:
       !
       call obs_set_current_header_list(lobsSpaceData,'RO')
       HEADER2: do
          index_header = obs_getHeaderIndex(lobsSpaceData)
          if (index_header < 0) exit HEADER2
          !     
          !     *  Process only refractivity data (codtyp 169)
          !
          IDATYP = obs_headElem_i(lobsSpaceData,OBS_ITY,INDEX_HEADER)
          IF ( IDATYP .EQ. 169 ) THEN
             iProfile=iProfile+1
             gps_vRO_IndexPrf(iProfile)=INDEX_HEADER
             zLat = obs_headElem_r(lobsSpaceData,OBS_LAT,INDEX_HEADER)
             zLon = obs_headElem_r(lobsSpaceData,OBS_LON,INDEX_HEADER)
             Lat  = zLat * MPC_DEGREES_PER_RADIAN_R8
             Lon  = zLon * MPC_DEGREES_PER_RADIAN_R8
          END IF
       END DO HEADER2

    END IF

    WRITE(*,*)'EXIT FILTERGPSRO'

  END SUBROUTINE FILT_GPSRO

  SUBROUTINE filt_topoChm(columnhr,obsSpaceData,beSilent)
    !
    ! Author: Y.J. Rochon Feb 2015
    !          - Based on the subroutines filt_topo* in obsfilter_mod.ftn90.
    !            Adapted to chemical constituent data.
    !
    ! Revisions:  M. Sitwell Nov 2015
    !             - Removed call to subroutine chm_adj_model_bndry for consistency
    !               in the flagging of obs above/below the model done in vobslyrs
    !             M. Sitwell Sept 2016
    !             - Added flagging of bit 4 in OBS_FLG set in set_scale_chm indicating
    !               mismatch of number of levels of mantissa and exponent.
    !
    ! Purpose: Rejects elements which are too far below the model surface
    !          or above the model top.
    !
    ! Comments:
    !   - Flagging of bit 4 in OBS_FLG done in filt_topoChm instead of set_scale_chm
    !     since this subroutine is called after chm_setup, allowing use of utl_open_asciifile
    !

    implicit none
    type(struct_columnData) :: columnhr
    type(struct_obs) :: obsSpaceData
    logical :: beSilent

    integer :: headerIndex, bodyIndex, listIndex, elemIndex, listIndex_stnid
    integer :: ivnm, countAssim, jl, icount,unit,ier
    real(8) :: zlev,zPtop,zP0
    real(8) :: zStnAlt,zModAlt,zpb,zpt
    logical :: list_is_empty,warn_suspicious

    integer, parameter :: Nmax=100
    integer :: Num_stnid_chm,nobslev,Num_chm,iconstituentid
    character(len=13) :: CstnidList_chm(Nmax)
    integer :: countAcc_stnid(Nmax),countRej_stnid(Nmax)
    integer :: countRejflg_stnid(Nmax),countRejflg(Nmax)
    integer :: countAcc(Nmax),countRej(Nmax),iConstituentList(Nmax)
    integer, external :: fclos

    if (.not.obs_famExist(obsSpaceData,'CH')) return

    ! Set counters to zero
    countAcc_stnid(:)=0
    countRej_stnid(:)=0
    countRejflg_stnid(:)=0
    Num_stnid_chm=0

    countAcc(:)=0
    countRej(:)=0
    countRejflg(:)=0
    Num_chm=0

    ! Loop over all header indices of the 'CH' family
    call obs_set_current_header_list(obsSpaceData, 'CH')
    HEADER: do
      headerIndex = obs_getHeaderIndex(obsSpaceData)
      if (headerIndex < 0) exit HEADER

      ! Set the body list & start at the beginning of the list
      call obs_set_current_body_list(obsSpaceData, headerIndex,list_is_empty)

      if (list_is_empty) cycle HEADER ! Proceed to next HEADER

      ! Set geopotential height and pressure boundaries.

      zModAlt = col_getGZsfc(columnhr,headerIndex)/RG
      zpb=zModAlt
      zStnAlt = obs_headElem_r(obsSpaceData,OBS_ALT,headerIndex)
      zpt = col_getHeight(columnhr,1,headerIndex,'MM')/RG
      zP0 = col_getElem(columnhr,1,headerIndex,'P0')
      zPtop = col_getPressure(columnhr,1,headerIndex,'MM')

      ! Identify max number of profile points in the profile (exclude BUFR_SCALE_EXPONENT elements)

      nobslev = obs_headElem_i(obsSpaceData,OBS_NLV,headerIndex)
      bodyIndex =obs_headElem_i(obsSpaceData,OBS_RLN,headerIndex)
      do jl=0,obs_headElem_i(obsSpaceData,OBS_NLV,headerIndex)-1
          if (obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex+jl).eq.BUFR_SCALE_EXPONENT) &
             nobslev = nobslev-1
      end do

      ! Identify element index of stnid list for the CH family.

      call utl_get_stringId(obs_elem_c(obsSpaceData,'STID',headerIndex),&
               nobslev,CstnidList_chm,Num_stnid_chm,Nmax,listIndex_stnid)

      warn_suspicious = .false.

      ! Loop over all body indices (still in the 'CH' family)
      icount=0
      BODY: do
        bodyIndex = obs_getBodyIndex(obsSpaceData)
        if (bodyIndex < 0) exit BODY

        ivnm = obs_bodyElem_i(obsSpaceData,OBS_VNM,bodyIndex)
        if (ivnm.eq.BUFR_SCALE_EXPONENT) cycle BODY

        !  Identify element index of constituent ID list for the CH family.
        if (icount.eq.0) call utl_get_Id(obs_headElem_i(obsSpaceData,OBS_CHM,headerIndex), &
                              iConstituentList,Num_chm,Nmax,listIndex)
        icount=icount+1

        ! Check for bit 4 of OBS_FLG, indicating a 'Suspicious element'
        if (btest(obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex),4)) then
           call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
           warn_suspicious = .true.
        end if

        if (obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.0) then

            ! Already rejected from input marker/flag.

            countRej(listIndex)=countRej(listIndex)+1
            countRej_stnid(listIndex_stnid)=countRej_stnid(listIndex_stnid)+1
            countRejflg(listIndex)=countRejflg(listIndex)+1
            countRejflg_stnid(listIndex_stnid)=countRejflg_stnid(listIndex_stnid)+1

        else if (obs_bodyElem_i(obsSpaceData,OBS_VCO,bodyIndex).eq.1) then

           ! Check as a function of altitude.

           zlev = obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex)
           !write(*,*) 'rejected zzz ',obs_elem_c(obsSpaceData,'STID',headerIndex),zlev,zP0,zPtop
           if( zlev.lt.zpb .or. zlev.gt.zpt ) then
               call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                   ibset( obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex), 18 ))
               call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
               countRej(listIndex)=countRej(listIndex)+1
               countRej_stnid(listIndex_stnid)=countRej_stnid(listIndex_stnid)+1
           else
               countAcc(listIndex)=countAcc(listIndex)+1
               countAcc_stnid(listIndex_stnid)=countAcc_stnid(listIndex_stnid)+1
           end if 

        else if (obs_bodyElem_i(obsSpaceData,OBS_VCO,bodyIndex).eq.2) then

           ! Check as a function of pressure.

           zlev = obs_bodyElem_r(obsSpaceData,OBS_PPP,bodyIndex)

           if ( zlev.gt.zP0 .or. zlev.lt.zPtop) then
               call obs_bodySet_i(obsSpaceData,OBS_ASS,bodyIndex,0)
               call obs_bodySet_i(obsSpaceData,OBS_FLG,bodyIndex,  &
                 ibset(obs_bodyElem_i(obsSpaceData,OBS_FLG,bodyIndex),18 ))
               countRej(listIndex)=countRej(listIndex)+1
               countRej_stnid(listIndex_stnid)=countRej_stnid(listIndex_stnid)+1
           else
               countAcc(listIndex)=countAcc(listIndex)+1
               countAcc_stnid(listIndex_stnid)=countAcc_stnid(listIndex_stnid)+1
           end if
        else
           countAcc(listIndex)=countAcc(listIndex)+1
           countAcc_stnid(listIndex_stnid)=countAcc_stnid(listIndex_stnid)+1
        end if

      end do BODY

      if (warn_suspicious) then
         call utl_open_asciifile(chm_setup_get_str('message'),unit)
         write(unit,'(A)') "filt_topoChm: Number of levels mismatch between mantissa and exponent for observation"
         write(unit,'(14X,A,4X,I8,2X,I4)') obs_elem_c(obsSpaceData,'STID',headerIndex),obs_headElem_i(obsSpaceData,OBS_DAT,headerIndex),&
              obs_headElem_i(obsSpaceData,OBS_ETM,headerIndex)
         write(unit,'(14X,A,F9.2,A,F9.2)') "lon = ",obs_headElem_r(obsSpaceData,OBS_LON,headerIndex)*MPC_DEGREES_PER_RADIAN_R8, &
              " lat = ",obs_headElem_r(obsSpaceData,OBS_LAT,headerIndex)*MPC_DEGREES_PER_RADIAN_R8
         write(unit,*)
         ier = fclos(unit)
      end if

    end do HEADER

    if (Num_stnid_chm.gt.0 .and. .not.beSilent) then
       write(*,*) ' '
       write(*,*) '*****************************************************************'
       write(*,*) ' SUBROUTINE filt_topoChm '
       write(*,*) ' FAMILY = CH'
       write(*,222) 'ELEMENTS for CH stnids',(CstnidList_chm(elemIndex),elemIndex=1,Num_stnid_chm)
       write(*,223) 'ACCEPTED for CH stnids',(countAcc_stnid(elemIndex),elemIndex=1,Num_stnid_chm)
       write(*,223) 'REJECTED for CH stnids',(countRej_stnid(elemIndex),elemIndex=1,Num_stnid_chm)
       write(*,223) 'REJECTED due to marker',(countRejflg_stnid(elemIndex),elemIndex=1,Num_stnid_chm)
       write(*,*) ' '
       write(*,224) 'ELEMENTS for CH       ',(iConstituentList(elemIndex),elemIndex=1,Num_chm)
       write(*,224) 'ACCEPTED for CH       ',(countAcc(elemIndex),elemIndex=1,Num_chm)
       write(*,224) 'REJECTED for CH       ',(countRej(elemIndex),elemIndex=1,Num_chm)
       write(*,223) 'REJECTED due to marker',(countRejflg(elemIndex),elemIndex=1,Num_stnid_chm)
       write(*,*) '*****************************************************************'
       write(*,*) ' '

       countAssim=0
       do bodyIndex=1,obs_numbody(obsSpaceData)
          if (obs_bodyElem_i(obsSpaceData,OBS_ASS,bodyIndex).eq.1) countAssim=countAssim+1
       end do
       write(*,'(1X," NUMBER OF DATA TO BE ASSIMILATED AFTER ADJUSTMENTS (after filter_topoChm):",i10)') countAssim
       write(*,*) ' '
    end if
222 format(2x,a29,100(2x,a10))
223 format(2x,a29,100(2x,i8,2x))
224 format(2x,a29,100(2x,i6))

  END SUBROUTINE filt_topoChm

end module obsFilter_mod
