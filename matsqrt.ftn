!--------------------------------------- LICENCE BEGIN -----------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------

      SUBROUTINE MATSQRT(PA,KN,ZSIGN,print_information)
#if defined (DOC) 
*
***s/r MATSQRT     - Calculate square root of an error covariance
*     .              matrix
*
*Author  : M. Buehner *ARMA/AES  February, 2000
*Revision:
*
*Arguments
*     .  PA(KN,KN)     :  on entry, the original matrix
*     .                   on exit,  the sqrt     matrix
*     .  KN            : order of the matrix
#endif
      IMPLICIT NONE
*
* Arguments
*
      INTEGER KN
      REAL*8 PA(KN,KN),ZSIGN
*
* Local variables
*
      INTEGER JI, J, INFO, IER, IWORK
      REAL*8 size_zwork
      REAL*8, allocatable :: ZWORK(:), ZRESULT(:,:), ZEIGENV2(:,:), ZEIGEN(:,:), ZEIGENV(:)
      LOGICAL, intent(in) :: print_information

      if (print_information) then
         WRITE(*,*)' MATSQRT-Sqrt matrix of a symmetric matrix'
         WRITE(*,*)' zsign= ',zsign
         WRITE(*,*)'  -----------------------------------------------'
      end if
*
*     1. Computation of eigenvalues and eigenvectors
*
      allocate(ZRESULT(KN,KN))
      allocate(ZEIGEN(KN,KN))
      allocate(ZEIGENV2(KN,KN))
      allocate(ZEIGENV(KN))
      
      DO JI=1,KN
         DO J=1,KN
            ZEIGEN(JI,J)=PA(JI,J)
         END DO
      END DO

      !! query the size of the 'zwork' vector by calling 'DSYEV' with 'iwork=-1'
      iwork=-1
      info = -1
      CALL DSYEV('V','U',KN, ZEIGEN,KN, ZEIGENV,size_ZWORK, IWORK, INFO )

      iwork=int(size_zwork)
      allocate(zwork(iwork))
      !! compute the eigenvalues
      CALL DSYEV('V','U',KN, ZEIGEN,KN, ZEIGENV,ZWORK, IWORK, INFO )
      deallocate(zwork)
*
      if (print_information) then
         WRITE(*,'(1x,"ORIGINAL EIGEN VALUES: ")')
         WRITE(*,'(1x,10f7.3)') (ZEIGENV(JI),JI=1,KN)
      end if
*
*     2.  Take SQRT of eigenvalues
*
      DO JI=1,KN
        DO J=1,KN
          ZEIGENV2(JI,J)= 0.0d0
        END DO
      END DO
      DO JI=1,KN
        ZEIGENV2(JI,JI)= ZEIGENV(JI)**(0.5d0*ZSIGN)
      END DO
*
      if (print_information) then
         WRITE(*,'(1x,"SQRT OF ORIGINAL EIGEN VALUES: ")')
         WRITE(*,'(1x,10f7.3)') (ZEIGENV2(JI,JI),JI=1,KN)
      end if
*
      CALL DGEMM('N','N',KN,KN,KN,1.0d0,ZEIGEN,KN,ZEIGENV2,KN,
     +           0.0D0 ,ZRESULT,KN)

      CALL DGEMM('N','T',KN,KN,KN,1.0D0,ZRESULT,KN,ZEIGEN,KN,
     +           0.0d0,PA,KN)


*
*     4. Deallocate local arrays
*
      deallocate(ZRESULT,ZEIGEN,ZEIGENV2,ZEIGENV)

      if (print_information) then
         WRITE(*,*)'MATSQRT-----------Done--------------- '
         WRITE(*,*)' '
      end if

      RETURN
      END
