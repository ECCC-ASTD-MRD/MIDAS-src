!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!
      PROGRAM CNT0
*
***   s/r CNT0  - Routine controlling the job at its highest level
*
      use topLevelControl_mod
      use mpi
      use obsSpaceData_mod
      use columnData_mod  
      implicit none
      integer istamp,exdb,exfin
      character(len=9) :: clmsg
      integer  :: root,ierr,numAnalyses,indexAnalysis_loop,indexAnalysis,getNumAnalyses
      logical :: lastAnalysis
      type(struct_obs),target :: obsSpaceData
      type(struct_columnData),target :: columng,columnhr

      ISTAMP=EXDB('3DVAR','DEBUT','NON')

      WRITE(*,FMT=9000) top_crevision
 9000 FORMAT(/,3(" *****************"),/
     S     ,14x,"-- START OF MAIN PROGRAM CNT0: --",/
     S     ,14x,"-- 3D VARIATIONAL ASSIMILATION --",/
     S     ,14x,"-- VARGLB Revision number ",a," --",/
     S     ,3(" *****************"))
!
!     MPI initilization .  
!
      CALL mpi_init  
      root = 0  

      call tmg_init (mpi_myid, 'TMG_3D-VAR' )

      IF(mpi_myid == root) THEN
         clmsg = 'VAR3D_BEG'
         call wrstatus(clmsg)
      ENDIF 
C
C     *       1.    Initialize level 0 commons.
C
      call top_setup
C
C     *       2.    Decide on configuration of job.
C
      if(top_BgckMode()) then
C BGCHECK AIRS IASI + donnees conventionnelles
         IF(mpi_myid == root) WRITE(*,*) 'CNT0: BGCHECK MODE'

         call tmg_start(1,'PREMIN')
         numAnalyses = 1
         indexAnalysis = 0
         call preproc(columng,columnhr,obsSpaceData,
     &                "ALL",          ! obsColumnMode
     &                "ROUNDROBIN",   ! obsMpiStrategy
     &                numAnalyses)
         !
         !- Reading, horizontal interpolation and unit conversions of the 3D trial fields
         !
         call tmg_start(10,'SUGOMOBS')
         call sugomobs(columng,columnhr,obsSpaceData,indexAnalysis)
         call tmg_stop(10)
         !
         !- Compute observation innovations and prepare obsSpaceData for minimization
         !
         call suinnov(columng,columnhr,obsSpaceData)
         call tmg_stop (1)

         call tmg_start(2,'BGCHECK')
         CALL BGCHECK(columng,columnhr,obsSpaceData)
         call tmg_stop(2)

      elseif(top_OmpMode()) then
C calcul des O-P pour AMSU, GEORAD et SSMIS
         WRITE(*,*) 'CNT0: RESIDUAL MODE (i.e. O-P)'
         call tmg_start(1,'PREMIN')
         numAnalyses = 1
         indexAnalysis = 0
         call preproc(columng,columnhr,obsSpaceData,  
     &                "VAR",          ! obsColumnMode
     &                "ROUNDROBIN",   ! obsMpiStrategy
     &                numAnalyses)
         !
         !- Reading, horizontal interpolation and unit conversions of the 3D trial fields
         !
         call tmg_start(10,'SUGOMOBS')
         call sugomobs(columng,columnhr,obsSpaceData,indexAnalysis)
         call tmg_stop(10)
         !
         !- Compute observation innovations and prepare obsSpaceData for minimization
         !
         call suinnov(columng,columnhr,obsSpaceData)
         call tmg_stop (1)

         call tmg_start(2,'RESIDUALS')
         CALL RESIDUALS(columng,columnhr,obsSpaceData)
         call tmg_stop (2)

      elseif(top_AnalysisMode()) then
C ANALYSIS MODE
         WRITE(*,*) 'CNT0: ANALYSIS MODE'
         !
         !- Determine the number of analyses to perform
         !
         numAnalyses = getNumAnalyses()

         call tmg_start(1,'PREMIN')
         call preproc(columng,columnhr,obsSpaceData,
     &                "VAR",          ! obsColumnMode
     &                "LATBANDS",     ! obsMpiStrategy
     &                numAnalyses)
         call tmg_stop (1)

         do indexAnalysis_loop=1,numAnalyses

           write(*,*) '============================='
           write(*,*) 'Starting analysis number ', indexAnalysis_loop
           write(*,*) '============================='

           lastAnalysis= (indexAnalysis_loop.eq.numAnalyses)
           if(numAnalyses.eq.1) then
             indexAnalysis=0
           else
             indexAnalysis=indexAnalysis_loop
           endif
           !
           !- Reading, horizontal interpolation and unit conversions of the 3D trial fields
           !
           call tmg_start(1,'PREMIN')
           call tmg_start(10,'SUGOMOBS')
           call sugomobs(columng,columnhr,obsSpaceData,indexAnalysis)
           call tmg_stop(10)

           !
           !- Compute observation innovations and prepare obsSpaceData for minimization
           !
           call suinnov(columng,columnhr,obsSpaceData)
           call tmg_stop (1)

           !
           !- Add random unbiased perturbations to the OMP values
           !
           if(numAnalyses.gt.1) call perturbObs(obsSpaceData,numAnalyses,indexAnalysis)

           call tmg_start(2,'MIN')
           call minimize(columng,obsSpaceData,lastAnalysis)
           call tmg_stop (2)

           call tmg_start(3,'POSMIN')
           call postmin(columng,columnhr,indexAnalysis)
           call tmg_stop (3)

         enddo

         !
         !- Now write out the observation data
         !
         call tmg_start(93,'POST_UPDATEBRP')
         CALL LISTREJ(obsSpaceData)
         CALL VINT3DFD(OBS_OMA,obsSpaceData)
         CALL VINT3DFD(OBS_OMP,obsSpaceData)
         CALL SETASSFLG(obsSpaceData)
         CALL UPDATE_BURPFILES(obsSpaceData)
         call tmg_stop(93)

         call obs_finalize(obsSpaceData)
c
      else

         WRITE(*,*) ' CNT0: ERROR, UNKNOWN NCONF SPECIFIED'

      endif
C
C     *    3. Job termination
C
      ISTAMP=EXFIN('3DVAR','FIN','NON')
C
      IF(mpi_myid == root) THEN
         clmsg = 'VAR3D_END'
         call wrstatus(clmsg)
      ENDIF

      call tmg_terminate (mpi_myid, 'TMG_3D-VAR' )

      CALL RPN_COMM_FINALIZE(ierr) 

      RETURN
      END
