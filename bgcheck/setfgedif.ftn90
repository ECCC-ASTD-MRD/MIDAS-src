      SUBROUTINE SETFGEDIF(CDFAM,lcolumng,lobsSpaceData)
!*
!***s/r SETFGEDIF   - Interpolation of THE FIRST GUESS ERROR VARIANCES
!*                    for data derived through the diff self-differencing variables
!*
!*
!*Author  : J.M. Aparicio *MSC/ARMA November 2004
!*
!**    Purpose:  -Construct the FIRST GUESS ERROR VARIANCES from the
!*                diff-calculated dependencies and the primary errors.
!*
      use EarthConstants_mod
      use MathPhysConstants_mod
      use modgps04profile
      use modgps06gravity
      use modgps07geostruct
      use modgps08refop
      use columnData_mod
      use obsSpaceData_mod
      use verticalCoord_mod
      IMPLICIT NONE
      type(struct_columnData) :: lcolumng
      type(struct_obs) :: lobsSpaceData
      type(struct_vco), pointer :: vco_anl
      CHARACTER*2 CDFAM
      INTEGER JF
      INTEGER INDEX_HEADER, first_header
      INTEGER IDATYP
      INTEGER IDATA   , IDATEND, JDATA
      INTEGER NH, NH1
      INTEGER JL, JV, NGPSLEV
      integer, parameter :: jpnflev=200
      LOGICAL  ASSIM

      REAL*8 ZLAT, Lat
      REAL*8 ZLON, Lon
      REAL*8 ZETA(JPNFLEV)
      REAL*8 ZTT (JPNFLEV)
      REAL*8 ZHU (JPNFLEV)
      REAL*8 ZGZ (JPNFLEV)
      REAL*8 ZP0
      REAL*8 ZPT
      REAL*8 ZMT
      REAL*8 ZMHX
      REAL*8 HNH1

      REAL*8 JAC(ngpscvmx)
      REAL*8 DV (ngpscvmx)
      TYPE(GPSPROFILE)           :: PRF
      REAL(DP)     , ALLOCATABLE :: H   (:)
      TYPE(GPSDIFF), ALLOCATABLE :: RSTV(:)
!C
!C     * 1.  Initializations
!C     *     ---------------
!C
!C     *     Eta vector:
!C
      vco_anl => col_getVco(lcolumng)
      NGPSLEV=col_getNumLev(lcolumng)
      DO JL = 1, col_getNumLev(lcolumng)
         ZETA(JL) = vco_anl%DHYB_M(JL)
      ENDDO  
      first_header=-1

      ! loop over all header indices of the CDFAM family
      call obs_set_current_header_list(lobsSpaceData,CDFAM)
      HEADER: do
         index_header = obs_getHeaderIndex(lobsSpaceData)
         if (index_header < 0) exit HEADER
         if(first_header.eq.-1) first_header=index_header
!C
!C     *    .   Process only refractivity data (codtyp 169)
!C
               IDATYP = obs_elem_i(lobsSpaceData,'ITY ',INDEX_HEADER)
               IF ( IDATYP .EQ. 169 ) THEN
!C
!C                 Loops over data in the observation
!C
                  IDATA   = obs_elem_i(lobsSpaceData,'RLN ',INDEX_HEADER)
                  IDATEND = obs_elem_i(lobsSpaceData,'NLV ',INDEX_HEADER) + IDATA - 1
                  ASSIM = .FALSE.
!C
!C                 Scan for requested assimilations, and count them
!C
                  NH = 0
                  DO JDATA= IDATA, IDATEND
                     IF ( obs_elem_i(lobsSpaceData,'ASS ',JDATA).EQ.1 ) THEN
                        ASSIM = .TRUE.
                        NH = NH + 1
                     ENDIF
                  ENDDO
!C
!C     *           If assimilations are requested, apply the observation operator
!C
                  IF (ASSIM) THEN
!C     
!C     *              Profile at the observation location:
!C
                     Lat  = obs_elem_r(lobsSpaceData,'LAT ',index_header)
                     Lon  = obs_elem_r(lobsSpaceData,'LON ',index_header)
                     ZLAT = Lat * MPC_DEGREES_PER_RADIAN_R8
                     ZLON = Lon * MPC_DEGREES_PER_RADIAN_R8
                     DO JL = 1, col_getNumLev(lcolumng)
!C
!C     *                 Profile x
!C
                        ZTT(JL) = col_getElem(lcolumng,JL,INDEX_HEADER,'TT')-273.15
                        ZHU(JL) = col_getElem(lcolumng,JL,INDEX_HEADER,'HU')
                        ZGZ(JL) = col_getElem(lcolumng,JL,INDEX_HEADER,'GZ')
                     ENDDO
                     ZP0 = col_getElem(lcolumng,1,INDEX_HEADER,'P0')
                     ZMT = ZGZ(col_getNumLev(lcolumng))/RG
                     ZMT = gpsgeopotential(Lat, ZMT)/RG
                     ZPT = vco_anl%dpt_M
!C     
!C     *              GPS profile structure:
!C
                     CALL GPSSTRUCT1(NGPSLEV,ZLAT,ZLON,ZETA,ZTT,ZHU,ZP0,ZMT,ZPT,PRF)
!C
!C     *              Local error
!C
                     DO JL = 1, col_getNumLev(lcolumng)
                        DV (        JL) = 1.
                        DV (col_getNumLev(lcolumng)+JL) = 1.
                     ENDDO
                     DV (2*col_getNumLev(lcolumng)+1)   = 2.

                     IF (INDEX_HEADER.EQ.first_header) THEN
                        DO JL = 1, 2*col_getNumLev(LCOLUMNG)+1
                           WRITE(*,*)'SETFGEDIF', JL, DV(JL)
                        ENDDO
                     ENDIF
!C
!C     *              PREPARE THE VECTOR OF ALL THE OBSERVATIONS
!C
                     ALLOCATE( H    (NH) )
                     ALLOCATE( RSTV (NH) )
                     NH1 = 0
                     DO JDATA= IDATA, IDATEND
                        IF ( obs_elem_i(lobsSpaceData,'ASS ',JDATA).EQ.1 ) THEN
                           NH1   = NH1 + 1
                           HNH1  = obs_elem_r(lobsSpaceData,'PPP ',jdata)
                           H(NH1)= gpsgeopotential(Lat,HNH1)/9.80616
                        ENDIF
                     ENDDO
!C
!C     *              Apply the observation operator
!C  
                     CALL GPSREFOPV(H, PRF, RSTV)
!C
!C     *              Perform the H(xb)DV operation
!C
                     NH1 = 0
                     DO JDATA= IDATA, IDATEND
                        IF ( obs_elem_i(lobsSpaceData,'ASS ',jdata).EQ.1 ) THEN
                           NH1 = NH1 + 1
!C
!C     *                    Observation jacobian
!C
                           JAC = RSTV(NH1)%DVAR
!C
!C     *                    Evaluate sqrt( H(xb)DV **2 )
!C
                           ZMHX = 0._dp
                           DO JV = 1, 2*PRF%NGPSLEV+1
                              ZMHX = ZMHX + (JAC(JV) * DV(JV))**2
                           ENDDO
                           ZMHX = SQRT(ZMHX)
!C     
!C     *                    FIRST GUESS ERROR VARIANCE
!C
                           call obs_set_r(lobsSpaceData,'HPHT',jdata,ZMHX)
                           IF (INDEX_HEADER.EQ.first_header) THEN
 11                           FORMAT(A12,2I5,2F12.2,2F12.4)
                              WRITE(*,11)'SETFGEDIFFGE', NH1, NH, H(NH1),RSTV(NH1)%VAR,  &
                                 ZMHX,obs_elem_r(lobsSpaceData,'OER ',JDATA)
                           ENDIF
                        ENDIF
                     ENDDO
                     DEALLOCATE( RSTV )
                     DEALLOCATE( H    )
                  ENDIF
               ENDIF
      ENDDO HEADER
      write(*,*) 'end of setfgedif'
      call flush(6)

      RETURN
      END SUBROUTINE SETFGEDIF
