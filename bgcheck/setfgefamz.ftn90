      SUBROUTINE SETFGEFAMZ(CDFAM,lcolumn,lcolumng,lobsSpaceData)
!*
!***s/r SETFGEFAMZ   - Interpolation of THE FIRST GUESS ERROR VARIANCES
!*
!*
!*Author  :  J. St-James, CMDA/SMC November 2002
!*
!**    Purpose:  -Interpolate vertically the contents of commvo to
!*                the levels of the observations (in meters). Then
!*                compute THE FIRST GUESS ERROR VARIANCES
!*                A linear interpolation in z is performed.
!*
      use EarthConstants_mod
      use MathPhysConstants_mod
      use columnData_mod
      use obsSpaceData_mod
      IMPLICIT NONE
      type(struct_columnData) :: lcolumn,lcolumng
      type(struct_obs) :: lobsSpaceData
      CHARACTER*2 CDFAM
      INTEGER IPB,IPT
      INTEGER INDEX_HEADER,ITYP,IK,IBEGIN,ILAST
      INTEGER J,INDEX_BODY
      REAL*8 ZWB,ZWT
      REAL*8 ZLEV,ZPB,ZPT

      ! loop over all header indices of the CDFAM family
      call obs_set_current_header_list(lobsSpaceData,CDFAM)
      HEADER: do
         index_header = obs_getHeaderIndex(lobsSpaceData)
         if (index_header < 0) exit HEADER

         ! loop over all body indices for this index_header
         call obs_set_current_body_list(lobsSpaceData, index_header)
         BODY: do 
            index_body = obs_getBodyIndex(lobsSpaceData)
            if (index_body < 0) exit BODY
!C
!C*    1. Computation of sigmap
!C     .  -----------------------------
!C
               IF ( obs_bodyElem_i(lobsSpaceData,NCM_ASS,index_body) .EQ. 1 .AND.   &
                    obs_bodyElem_i(lobsSpaceData,NCM_VCO,index_body) .EQ. 1 ) then
                  IF  (obs_bodyElem_i(lobsSpaceData,NCM_XTR,index_body) .NE. 0) THEN
                     ITYP = obs_bodyElem_i(lobsSpaceData,NCM_VNM,index_body)
                     IK=col_getNumLev(LCOLUMNG)-1
                     IPT  = IK + col_getOffsetFromVarno(lcolumng,ityp)
                     IPB  = IPT +1
                     call obs_bodySet_r(lobsSpaceData,NCM_HPHT,index_body,col_getElem(lcolumn,IPB,INDEX_HEADER))
                  ELSE
                     ITYP = obs_bodyElem_i(lobsSpaceData,NCM_VNM,index_body)
                     ZLEV = obs_bodyElem_r(lobsSpaceData,NCM_PPP,index_body)
                     IK   = obs_bodyElem_i(lobsSpaceData,NCM_LYR,index_body)
                     IPT  = IK + col_getOffsetFromVarno(lcolumng,ityp)
                     IPB  = IPT+1
                     ZPT  = col_getHeight(lcolumng,IK  ,INDEX_HEADER,'NA')/RG
                     ZPB  = col_getHeight(lcolumng,IK+1,INDEX_HEADER,'NA')/RG
                     ZWB  = (ZPT-ZLEV)/(ZPT-ZPB)
                     ZWT  = 1. - ZWB
!C
!C     FIRST GUESS ERROR VARIANCE
!C
                     call obs_bodySet_r(lobsSpaceData,NCM_HPHT,index_body,   &
                          (ZWB*col_getElem(lcolumn,IPB,INDEX_HEADER) + ZWT*col_getElem(lcolumn,IPT,INDEX_HEADER)))

                  ENDIF
               ENDIF

         END DO BODY

      END DO HEADER

      RETURN
      END SUBROUTINE SETFGEFAMZ
