      SUBROUTINE SETFGEFAMZ(CDFAM,lcolumn,lcolumng,lobsSpaceData)
!*
!***s/r SETFGEFAMZ   - Interpolation of THE FIRST GUESS ERROR VARIANCES
!*
!*
!*Author  :  J. St-James, CMDA/SMC November 2002
!*
!**    Purpose:  -Interpolate vertically the contents of commvo to
!*                the levels of the observations (in meters). Then
!*                compute THE FIRST GUESS ERROR VARIANCES
!*                A linear interpolation in z is performed.
!*
      use EarthConstants_mod
      use MathPhysConstants_mod
      use columnData_mod
      use obsSpaceData_mod
      IMPLICIT NONE
#include "cvcord.cdk"
      type(struct_columnData) :: lcolumn,lcolumng
      type(struct_obs) :: lobsSpaceData
      CHARACTER*2 CDFAM
      INTEGER IPB,IPT
      INTEGER IOBS,ITYP,IK,IBEGIN,ILAST
      INTEGER J,JDATA
      REAL*8 ZWB,ZWT
      REAL*8 ZLEV,ZPB,ZPT

      DO J = 1,NFILES
         IF ((CFAMTYP(J) .EQ. CDFAM).AND.(NBEGINTYP(J).GT.0)) THEN
            IBEGIN=NBEGINTYP(J)
            ILAST=NENDTYP(J)
!C
!C*    1. Computation of sigmap
!C     .  -----------------------------
!C
            DO JDATA=IBEGIN,ILAST
!ccc   IF ( obs_elem_i(lobsSpaceData,'ASS ',jdata) .EQ. 1) then
               IF ( obs_elem_i(lobsSpaceData,'ASS ',jdata) .EQ. 1 .AND.   &
                    obs_elem_i(lobsSpaceData,'VCO ',jdata) .EQ. 1 ) then
                  IF  (obs_elem_i(lobsSpaceData,'XTR ',jdata) .NE. 0) THEN
                     ITYP = obs_elem_i(lobsSpaceData,'VNM ',jdata)
                     IK=col_getNumLev(LCOLUMNG)-1
                     IOBS = obs_elem_i(lobsSpaceData,'OBS ',jdata)
                     IPT  = IK + col_getOffsetFromVarno(lcolumng,ityp)
                     IPB  = IPT +1
                     call obs_set_r(lobsSpaceData,'FGE ',jdata,col_getElem(lcolumn,IPB,IOBS))
                  ELSE
                     ITYP = obs_elem_i(lobsSpaceData,'VNM ',jdata)
                     IOBS = obs_elem_i(lobsSpaceData,'OBS ',jdata)
                     ZLEV = obs_elem_r(lobsSpaceData,'PPP ',jdata)
                     IK   = obs_elem_i(lobsSpaceData,'LYR ',jdata)
                     IPT  = IK + col_getOffsetFromVarno(lcolumng,ityp)
                     IPB  = IPT+1
                     ZPT  = col_getHeight(lcolumng,IK  ,IOBS,'NA')/RG
                     ZPB  = col_getHeight(lcolumng,IK+1,IOBS,'NA')/RG
                     ZWB  = (ZPT-ZLEV)/(ZPT-ZPB)
                     ZWT  = 1. - ZWB
!C
!C     FIRST GUESS ERROR VARIANCE
!C
                     call obs_set_r(lobsSpaceData,'FGE ',jdata,   &
                          (ZWB*col_getElem(lcolumn,IPB,IOBS) + ZWT*col_getElem(lcolumn,IPT,IOBS)))

                  ENDIF
               ENDIF

            END DO

         ENDIF
      END DO

      RETURN
      END SUBROUTINE SETFGEFAMZ
