SUBROUTINE ESTIM_TS_AVHRR(TS, tg,emi,rcal,radobs,sfctau,cldflag, &
     ichref,nchn,nprf)
!
!**ID ESTIM_TS -- GET AN ESTIMATED SKIN TEMPERATURE
!
!       AUTHOR:   L. GARAND                   May 2004
!                 A. BEAULNE (CMDA/SMC)     March 2006  (ADAPT TO 3DVAR)                 
!
!       REVISION:
!
!       OBJECT:   GET AN ESTIMATED SKIN TEMPERATURE BY INVERSION OF
!          RADIATIVE TRANSFER EQUATION ASSUMING GUESS T AND Q PROFILES
!          ARE PERFECT. DESIGNED FOR A SINGLE CHANNEL ICHREF AND NPRF
!          PROFILES. ASSUMES A REAL TG (GUESS) OVER OCEANS AND A TG 
!          WITH HYPOTHESIS OF UNITY EMISSIVITY OVER LAND.
!      
!          USES:  RCAL = B(TG)*EMI*SFCTAU + ATMOS_PART
!             TS = B(TS)*EMI*SFCTAU + ATMOS_PART
!          SOLVES FOR TS
!
!       ARGUMENTS:
!          INPUT:
!            -TG(NPRF)          : GUESS SKIN TEMPERATURE (DEG K)
!            -EMI(NCHN,NPRF)    : SURFACE EMISSIVITIES FROM WINDOW CHANNEL (0.-1.)
!            -RCAL(NCHN,NPRF)   : COMPUTED CLEAR RADIANCES (MW/M2/SR/CM-1)
!            -RADOBS(NCHN,NPRF) : OBSERVED RADIANCES (")
!            -SFCTAU(NCHN,NPRF) : SURFACE TO SPACE TRANSMITTANCES (0.-1.)
!            -CLDFLAG(NPRF)     : CLEAR(0), CLOUDY(1) OR UNDEFINED(-1) PROFILES
!            -ICHREF(NPRF)      : REFERENCE SURFACE CHANNEL (SUBSET VALUES)
!            -NCHN              : NUMBER OF CHANNELS
!            -NPRF              : NUMBER OF PROFILES
!
!          OUTPUT:
!            -TS(NPRF)          : RETRIEVED SKIN TEMPERATURE (-1. FOR MISSING)
!
!

  use multi_ir_bgck_mod,only : coefs_avhrr

  IMPLICIT NONE
  integer ,intent(in) :: nchn,nprf
  integer ,intent(in) :: ichref(nprf),CLDFLAG(NPRF)
  REAL(8) ,intent(in) :: TG(NPRF),EMI(NCHN,NPRF),RCAL(NCHN,NPRF),RADOBS(NCHN,NPRF)
  REAL(8) ,intent(in) :: SFCTAU(NCHN,NPRF)
  REAL(8) ,intent(out):: TS(NPRF)
!***************************************
  INTEGER    :: JN,JC
  INTEGER    :: ICHN
  REAL(8)    :: RTG,RADTG(NCHN,NPRF)
  REAL(8)    :: RADTS,TS1(NCHN,NPRF),tstore,t_effective

  TS1(:,:) = -1.D0
  TS(:) = -1.D0
  RADTG(:,:) = -1.D0

!*    transform guess skin temperature to plank radiances 

  DO JN = 1, NPRF
     IF ( CLDFLAG(JN) == -1 ) CYCLE

     DO JC = 1, NCHN
        t_effective =  coefs_avhrr%coef%ff_bco(jc) + coefs_avhrr%coef%ff_bcs(jc) * TG(jn)
        RADTG(JC,jn) =  coefs_avhrr%coef%planck1(jc) / &
             ( Exp( coefs_avhrr%coef%planck2(jc)/t_effective ) - 1.0D0 )
     END DO
  END DO

  DO JN = 1, NPRF

     IF ( CLDFLAG(JN) /= 0 ) CYCLE

!*   compute TOA planck radiances due to guess skin planck radiances

     RTG =   RADTG(ICHREF(JN),JN)*EMI(ICHREF(JN),JN)*SFCTAU(ICHREF(JN),JN)
       
!*   compute true skin planck radiances due to TOA true planck radiances

     RADTS = ( RADOBS(ICHREF(JN),JN) + RTG - RCAL(ICHREF(JN),JN) ) / &
          ( EMI(ICHREF(JN),JN) * SFCTAU(ICHREF(JN),JN) )

!*   transform true skin planck radiances to true skin temperatures

     DO JC = 1, NCHN
          
        tstore = coefs_avhrr%coef%planck2(jc) / Log( 1+coefs_avhrr%coef%planck1(jc)/RADTS )
        TS1(JC,jn) = ( tstore-coefs_avhrr%coef%ff_bco(jc) ) / coefs_avhrr%coef%ff_bcs(jc)
     END DO
     TS(JN) = TS1(ichref(jn),jn)

  END DO


END SUBROUTINE ESTIM_TS_AVHRR
