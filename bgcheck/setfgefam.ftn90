      SUBROUTINE SETFGEFAM(CDFAM,lcolumn,lcolumng,lobsSpaceData)
!*
!***s/r SETFGEFAM   - Interpolation of THE FIRST GUESS ERROR VARIANCES
!*
!*
!*Author  : P. Koclas *CMC/CMSV November 1998
!*
!**    Purpose:  -Interpolate vertically the contents of commvo to
!*                the pressure levels of the observations. Then
!*                compute THE FIRST GUESS ERROR VARIANCES
!*                A linear interpolation in ln(p) is performed.
!*
      use columnData_mod
      use obsSpaceData_mod
      IMPLICIT NONE
#include "comlun.cdk"
#include "cvcord.cdk"
      type(struct_columnData) :: lcolumn,lcolumng
      type(struct_obs) :: lobsSpaceData
      CHARACTER*2 CDFAM
      INTEGER IPB,IPT
      INTEGER IOBS,IPOS,IK,IBEGIN,ILAST
      INTEGER J,JDATA
      REAL*8 ZWB,ZWT
      REAL*8 ZLEV,ZPB,ZPT

      DO J = 1,NFILES
      IF ( (CFAMTYP(J) .EQ. CDFAM ) .AND.( NBEGINTYP(J) .GT. 0) ) THEN
         IBEGIN=NBEGINTYP(J)
          ILAST=NENDTYP(J)
!C
!C*    1. Computation of sigmap
!C     .  -----------------------------
!C
        write(nulout,*) 'setfgefam: CDFAM=',CDFAM
        write(nulout,*) 'setfgefam: IBEGIN,ILAST=',IBEGIN,ILAST
         DO JDATA=IBEGIN,ILAST
!ccc            IF ( obs_elem_i(lobsSpaceData,'ASS ',jdata) .EQ. 1) then
            IF ( obs_elem_i(lobsSpaceData,'ASS ',jdata) .EQ. 1 .AND.   &
                 obs_elem_i(lobsSpaceData,'VCO ',jdata) .EQ. 2      ) then
            IF  (obs_elem_i(lobsSpaceData,'XTR ',jdata) .NE. 0) THEN
               IK=col_getNumLev(LCOLUMNG)-1
               IOBS=obs_elem_i(lobsSpaceData,'OBS ',jdata)
               IPOS=obs_elem_i(lobsSpaceData,'POS ',jdata)
               IPT  = IK + IPOS*col_getNumLev(LCOLUMNG)
               IPB  = IPT +1
               call obs_set_r4(lobsSpaceData,'FGE ',jdata,real(col_getElem(lcolumn,IPB,IOBS)))
            ELSE
               IOBS = obs_elem_i(lobsSpaceData,'OBS ',jdata)
               IPOS = obs_elem_i(lobsSpaceData,'POS ',jdata)
               ZLEV = obs_elem_r8(lobsSpaceData,'PPP ',jdata)
               IK   = obs_elem_i(lobsSpaceData,'LYR ',jdata)
               IPT  = IK + IPOS*col_getNumLev(LCOLUMNG)
               IPB  = IPT+1
               ZPT  = col_getPressure(lcolumng,IK,IOBS,'NA')
               ZPB  = col_getPressure(lcolumng,IK+1,IOBS,'NA')
               ZWB  = LOG(ZLEV/ZPT)/LOG(ZPB/ZPT)
               ZWT  = 1.0D0 - ZWB
!C
!C              FIRST GUESS ERROR VARIANCE
!C
               call obs_set_r4(lobsSpaceData,'FGE ',jdata,   &
                    real((ZWB*col_getElem(lcolumn,IPB,IOBS) + ZWT*col_getElem(lcolumn,IPT,IOBS))) )
               if(obs_elem_r4(lobsSpaceData,'FGE ',jdata).le.0.) then
                 write(nulout,*) 'SETFGEFAM: CDFAM = ',CDFAM
                 write(nulout,*) 'SETFGEFAM: lcolumn_all(IPB,IOBS)=',col_getElem(lcolumn,IPB,IOBS)
                 write(nulout,*) 'SETFGEFAM: lcolumn_all(IPT,IOBS)=',col_getElem(lcolumn,IPT,IOBS)
                 CALL ABORT3D(NULOUT,'SETFGEFAM: First-guess stdev bad value')
               endif
            ENDIF
            ENDIF

         END DO

      ENDIF
      END DO

      RETURN
      END SUBROUTINE SETFGEFAM
