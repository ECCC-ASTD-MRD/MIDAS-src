SUBROUTINE ESTIM_TS(TS, tg,emi,rcal,radobs,sfctau,cldflag, &
     ichref,nchn,nchnkept,nprf,satid,CINST)

!
!**ID ESTIM_TS -- GET AN ESTIMATED SKIN TEMPERATURE
!
!       AUTHOR:   L. GARAND                   May 2004
!                 A. BEAULNE (CMDA/SMC)     March 2006  (ADAPT TO 3DVAR)                 
!
!       REVISION:
!
!       OBJECT:   GET AN ESTIMATED SKIN TEMPERATURE BY INVERSION OF
!          RADIATIVE TRANSFER EQUATION ASSUMING GUESS T AND Q PROFILES
!          ARE PERFECT. DESIGNED FOR A SINGLE CHANNEL ICHREF AND NPRF
!          PROFILES. ASSUMES A REAL TG (GUESS) OVER OCEANS AND A TG 
!          WITH HYPOTHESIS OF UNITY EMISSIVITY OVER LAND.
!      
!          USES:  RCAL = B(TG)*EMI*SFCTAU + ATMOS_PART
!             TS = B(TS)*EMI*SFCTAU + ATMOS_PART
!          SOLVES FOR TS
!
!       ARGUMENTS:
!          INPUT:
!            -TG(NPRF)          : GUESS SKIN TEMPERATURE (DEG K)
!            -EMI(NCHN,NPRF)    : SURFACE EMISSIVITIES FROM WINDOW CHANNEL (0.-1.)
!            -RCAL(NCHN,NPRF)   : COMPUTED CLEAR RADIANCES (MW/M2/SR/CM-1)
!            -RADOBS(NCHN,NPRF) : OBSERVED RADIANCES (")
!            -SFCTAU(NCHN,NPRF) : SURFACE TO SPACE TRANSMITTANCES (0.-1.)
!            -CLDFLAG(NPRF)     : CLEAR(0), CLOUDY(1) OR UNDEFINED(-1) PROFILES
!            -ICHREF(NPRF)      : REFERENCE SURFACE CHANNEL (SUBSET VALUES)
!            -NCHN              : NUMBER OF CHANNELS
!            -NCHNKEPT          : NUMBER OF CHANNELS KEPT IN CMA
!            -NPRF              : NUMBER OF PROFILES
!            -SATID             : SATELLITE IDENTYFIER
!            -CINST             : INTRUMENT CrIS, IASI or AIRS
!
!          OUTPUT:
!            -TS(NPRF)          : RETRIEVED SKIN TEMPERATURE (-1. FOR MISSING)
!
  use tovs_nl_mod
  use hir_chans

  IMPLICIT NONE
  integer ,intent(in) :: NPRF,NCHN,NCHNKEPT,satid
  integer ,intent(in) :: ICHREF(NPRF),CLDFLAG(NPRF)
  REAL(8) ,intent(in) :: TG(NPRF),EMI(NCHN,NPRF),RCAL(NCHN,NPRF),RADOBS(NCHN,NPRF)
  REAL(8) ,intent(in) :: SFCTAU(NCHN,NPRF)
  CHARACTER (LEN=*)  ,intent(in) :: CINST
  REAL(8) ,intent(out):: TS(NPRF)
!************************************************************************************
  INTEGER    :: JN,JC
  INTEGER    :: ICHN,INDX
  REAL(8)    :: RTG,RADTG(NCHN,NPRF)
  REAL(8)    :: RADTS,TS1(NCHN,NPRF),tstore,t_effective
  integer    :: ptc(NCHNKEPT)


  TS1(:,:) = -1.D0
  TS(:) = -1.D0
  RADTG(:,:) = -1.D0

  if (NCHNKEPT/=hir_get_nchan_selected(CINST)) then
     Write(*,*) "Invalid NCHNKEPT: ",NCHNKEPT,hir_get_nchan_selected(CINST),CINST
     call abort3d("estim_ts")
  endif


  DO JC = 1, NCHNKEPT
     ptc(JC)=hir_get_chindx_fr_chn(CINST,ichan(JC,SATID))
  END DO


!*    transform guess skin temperature to plank radiances 

  DO JN = 1, NPRF
     IF ( CLDFLAG(JN) == -1 ) CYCLE
     DO JC = 1, NCHNKEPT
        t_effective =  coefs(satid)%coef%ff_bco(jc) + coefs(satid)%coef%ff_bcs(jc) * TG(jn)

        indx=ptc(jc)

        RADTG(indx,jn) =  coefs(satid)%coef%planck1(jc) / &
             ( Exp( coefs(satid)%coef%planck2(jc)/t_effective ) - 1.0D0 )
     END DO
  END DO
      
  DO JN = 1, NPRF

     IF ( CLDFLAG(JN) /= 0 ) CYCLE

!*   compute TOA planck radiances due to guess skin planck radiances

     RTG =   RADTG(ICHREF(JN),JN)*EMI(ICHREF(JN),JN)*SFCTAU(ICHREF(JN),JN)

!*   compute true skin planck radiances due to TOA true planck radiances

     RADTS = ( RADOBS(ICHREF(JN),JN) + RTG - RCAL(ICHREF(JN),JN) ) / &
          ( EMI(ICHREF(JN),JN) * SFCTAU(ICHREF(JN),JN) )
    
!*   transform true skin planck radiances to true skin temperatures

     DO JC = 1, NCHNKEPT
        tstore = coefs(satid)%coef%planck2(jc) / Log( 1+coefs(satid)%coef%planck1(jc)/RADTS )
        indx=ptc(jc)
        TS1(indx,jn) = ( tstore-coefs(satid)%coef%ff_bco(jc) ) / coefs(satid)%coef%ff_bcs(jc)
     END DO

     TS(JN) = TS1(ichref(jn),jn)

  END DO


END SUBROUTINE ESTIM_TS
