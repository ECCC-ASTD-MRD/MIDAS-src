      SUBROUTINE BGCDATA(PJO,CDFAM,lobsSpaceData)
!*
!***s/r BGCDATA  - Computation OF BACKGROUND CHECK FLAGS
!*
!*Author  : P. Koclas *CMC/CMDA  January 1999
!*
!**    Purpose:  -Calculate a background check for a data family
!*                AND SET the appropriate quality control flags in
!*                the CMA
!*
      use MathPhysConstants_mod
      use bufr
      use obsSpaceData_mod
      IMPLICIT NONE
#include "comgpsgb.cdk"
!*
!C NOTE: YSFERRWGT IN COMGPSGB.CDK (FROM NML FILE) IS USED HERE FOR ERROR WEIGHTING
!C       OF TIME SERIES (FGAT) GPS MET OBSERVATIONS PS, TS, DPDS. IT IS APPLIED
!C       (ALONG WITH YZDERRWGT FOR ZTD) IN S/R SETERR AS A MULT. FACTOR TO ERRORS.
!C       YZDERRWGT AND YSFERRWGT = 1 FOR NORMAL 3D-VAR (3D-THINNING).
!C
      type(struct_obs) :: lobsSpaceData
      REAL*8 PJO,zsum,zsumo
      CHARACTER*2 CDFAM
      INTEGER IFLAG,INAM,ISETFLAG,INDEX_HEADER,IDBURP
      INTEGER IBEGIN,ILAST,ierr,fclos,fnom,ITYP
      INTEGER J,J2,JJ,JD,INDEX_BODY,ibgc,icoun
      INTEGER INOBS, INREJ, INZOBS, INZREJ
      INTEGER INPOBS, INTOBS, INDOBS, INPREJ, INTREJ, INDREJ
      REAL*8 ZOER,ZOMP,ZFGE,ZBGCHK,ZVAR,ZLEV,ZLAT,ZLON,ZSOP
      LOGICAL LLOK, LLZD, LMODIF1020
      lmodif1020=.false.

      WRITE(*,*)' '
      WRITE(*,*)' ------------------------------'
      WRITE(*,*)'  BACKGROUND CHECK FOR'
      WRITE(*,*)'       ',CDFAM, ' DATA'
      WRITE(*,*)' ------------------------------'
      WRITE(*,*)' '
      WRITE(*,'(a55,a74)')'  STNID     LATITU LONGITU  ID Elem        Level        ',  &
      ' Value        Sigmao       Sigmap         O-P       SigmaOP         qcflag  '
      WRITE(*,'(a55,a74)')'  -----     ------ -------  -- ----        -----        ',  &
      ' -----        ------       ------         ---       -------         ------  '
      icoun=0
      zsum=0.
      zsumo=0.
      PJO=-99.99

      INOBS=0
      INREJ=0
!C
!C*    Initialize counters for GP family observations
!C
      IF (CDFAM .EQ. 'GP') THEN
        INZOBS=0
        INPOBS=0
        INTOBS=0
        INDOBS=0
        INZREJ=0
        INPREJ=0
        INTREJ=0
        INDREJ=0
      ENDIF

      ! loop over all header indices of the CDFAM family
      call obs_set_current_header_list(lobsSpaceData,CDFAM)
      HEADER: do
         index_header = obs_getHeaderIndex(lobsSpaceData)
         if (index_header < 0) exit HEADER
!C
!C*    1. Computation of (HX - Z)**2/(SIGMAo**2 +SIGMAp**2)
!C     .  ----------------------------------------------------
!C
         ! loop over all body indices for this index_header
         call obs_set_current_body_list(lobsSpaceData, index_header)
         BODY: do 
            index_body = obs_getBodyIndex(lobsSpaceData)
            if (index_body < 0) exit BODY

               ITYP = obs_bodyElem_i(lobsSpaceData,NCM_VNM,index_body)
               LLOK=( obs_bodyElem_i(lobsSpaceData,NCM_ASS,index_body) .EQ. 1)
               IF ( LLOK ) THEN
                  INOBS = INOBS + 1
                  IF (CDFAM .EQ. 'GP') THEN
                    IF (ITYP .EQ. BUFR_NEZD) INZOBS = INZOBS+1
                    IF (ITYP .EQ. BUFR_NEPS) INPOBS = INPOBS+1
                    IF (ITYP .EQ. BUFR_NETS) INTOBS = INTOBS+1
                    IF (ITYP .EQ. BUFR_NESS) INDOBS = INDOBS+1
                  ENDIF
                  ZVAR = obs_bodyElem_r(lobsSpaceData,NCM_VAR,index_body)
                  ZLEV = obs_bodyElem_r(lobsSpaceData,NCM_PPP,index_body)
                  ZOER = obs_bodyElem_r(lobsSpaceData,NCM_OER,index_body)
                  ZLAT  = obs_headElem_r(lobsSpaceData,NCM_LAT,index_header) &
                                                      * MPC_DEGREES_PER_RADIAN_R8
                  ZLON  = obs_headElem_r(lobsSpaceData,NCM_LON,index_header) &
                                                      * MPC_DEGREES_PER_RADIAN_R8
!C
!C              BACK GROUND CHECK
!C
                  ZOMP  =-obs_bodyElem_r(lobsSpaceData,NCM_OMF,index_body)*obs_bodyElem_r(lobsSpaceData,NCM_OER,index_body)
                  ZFGE  = obs_bodyElem_r(lobsSpaceData,NCM_HPHT,index_body)
                  IF ( CDFAM .EQ. 'GP' ) THEN
                    IF (ITYP .EQ. BUFR_NEZD) THEN
                      ZOER = ZOER/YZDERRWGT
                    ELSE
                      ZOER = ZOER/YSFERRWGT
                    ENDIF
                    IF (ZFGE .LT. 1.E-3 .OR. ZOER .LT. 1.E-3 ) THEN
                      WRITE(*,*)' Problem for STNID FGE ZOER=',  &
                         obs_elem_c(lobsSpaceData,'STID',index_header),ZFGE,ZOER
                      CALL ABORT3D('BGCDATA: PROBLEM WITH FGE, OER.')
                    ENDIF
                  ELSE
                    IF ( ZFGE**2 + ZOER**2 .LT. 1.e-5)THEN
                        WRITE(*,*)' Problem for STNID FGE ZOER=',  &
                           obs_elem_c(lobsSpaceData,'STID',index_header),ZFGE,ZOER
                        ZFGE=1.E-5
                        ZOER=1.E-5
                    ENDIF
                  ENDIF
                  ZBGCHK=(ZOMP)**2/(ZFGE**2 + ZOER**2)
                  ibgc=ZBGCHK/1.
                  ibgc=min(ibgc,99)
                  ZSOP = SQRT(ZFGE**2 + ZOER**2)
!C
!C              UPDATE QUALITY CONTROL FLAGS
!C              ( ELEMENT FLAGS + GLOBAL HEADER FLAGS)
!C
                  INAM =obs_bodyElem_i(lobsSpaceData,NCM_VNM,index_body)
                if( inam.eq.12192 .and. obs_bodyElem_i(lobsSpaceData,NCM_XTR,index_body).eq.0)then
                   zsum=zsum+zfge*zfge
                   zsumo=zsumo+zoer*zoer
                   icoun=icoun+1
                endif
                  IDBURP=obs_headElem_i(lobsSpaceData,NCM_ITY,INDEX_HEADER)
                  IFLAG=ISETFLAG(CDFAM,IDBURP,INAM,ZLEV,ZBGCHK,LMODIF1020)
!C
!C              CONVERT ZTD VALUES FROM M TO MM FOR PRINTOUT
!C
                  LLZD = .FALSE.
                  IF ( CDFAM .EQ. 'GP' .AND. ITYP .EQ. BUFR_NEZD) THEN
                    ZVAR = ZVAR * 1000.0
                    ZOER = ZOER * 1000.0
                    ZFGE = ZFGE * 1000.0
                    ZOMP = ZOMP * 1000.0
                    ZSOP = ZSOP * 1000.0
                    LLZD = .TRUE.
                  ENDIF

                  IF (IFLAG .GE. 2 ) THEN
                    WRITE(*,122)  &
                       obs_elem_c(lobsSpaceData,'STID',index_header),  &
                       zlat,zlon,IDBURP,INAM,ZLEV,ZVAR,ZOER  &
                       ,ZFGE,ZOMP,ZSOP,ZBGCHK,IFLAG
                    INREJ = INREJ + 1
                    IF (CDFAM .EQ. 'GP') THEN
                      IF (ITYP .EQ. BUFR_NEZD) INZREJ = INZREJ+1
                      IF (ITYP .EQ. BUFR_NEPS) INPREJ = INPREJ+1
                      IF (ITYP .EQ. BUFR_NETS) INTREJ = INTREJ+1
                      IF (ITYP .EQ. BUFR_NESS) INDREJ = INDREJ+1
                    ENDIF
                  ENDIF

                  IF ( IFLAG .EQ. 1 ) THEN
                     call obs_bodySet_i(lobsSpaceData,NCM_FLG,index_body,IBSET(obs_bodyElem_i(lobsSpaceData,NCM_FLG,index_body),13))
                  ELSEIF ( IFLAG .EQ. 2 ) THEN
                     call obs_bodySet_i(lobsSpaceData,NCM_FLG,index_body,IBSET(obs_bodyElem_i(lobsSpaceData,NCM_FLG,index_body),14))
                     call obs_bodySet_i(lobsSpaceData,NCM_FLG,index_body,IBSET(obs_bodyElem_i(lobsSpaceData,NCM_FLG,index_body),16))
                     call obs_bodySet_i(lobsSpaceData,NCM_FLG,index_body,IBSET(obs_bodyElem_i(lobsSpaceData,NCM_FLG,index_body),09))
                     call obs_headSet_i(lobsSpaceData,NCM_ST1,index_header,ibset(obs_headElem_i(lobsSpaceData,NCM_ST1,index_header),06))

                  ELSEIF ( IFLAG .EQ. 3 ) THEN
                     call obs_bodySet_i(lobsSpaceData,NCM_FLG,index_body,IBSET(obs_bodyElem_i(lobsSpaceData,NCM_FLG,index_body),15))
                     call obs_bodySet_i(lobsSpaceData,NCM_FLG,index_body,IBSET(obs_bodyElem_i(lobsSpaceData,NCM_FLG,index_body),16))
                     call obs_bodySet_i(lobsSpaceData,NCM_FLG,index_body,IBSET(obs_bodyElem_i(lobsSpaceData,NCM_FLG,index_body),09))
                     call obs_headSet_i(lobsSpaceData,NCM_ST1,index_header,ibset(obs_headElem_i(lobsSpaceData,NCM_ST1,index_header),06))
                  ENDIF
               ENDIF
         ENDDO BODY
122   FORMAT(2x,a9,1x,f6.2,1x,f7.2,1x,I3,1x,I5,7(2x,F11.2),I3 )

      ENDDO HEADER

      IF ( INOBS .GT. 0 ) THEN
        WRITE(*,*)' '
        WRITE(*,*) '  BGCDATA: FINISHED BGCHECK OF ',CDFAM, ' DATA'
        WRITE(*,123) 'BGCDATA:   ',INREJ, ' OBSERVATIONS REJECTED OUT OF ', INOBS
        WRITE(*,*)' '
      ENDIF

      IF ( (INOBS .GT. 0) .AND. (CDFAM .EQ. 'GP') ) THEN
        WRITE(*,*)' '
        WRITE(*,*) '  BGCDATA:    REPORT FOR GP FAMILY OF OBSERVATIONS'
        WRITE(*,123) 'BGCDATA:   ',INZREJ, ' ZTD  OBSERVATIONS REJECTED OUT OF ', INZOBS
        WRITE(*,123) 'BGCDATA:   ',INPREJ, ' PSFC OBSERVATIONS REJECTED OUT OF ', INPOBS
        WRITE(*,123) 'BGCDATA:   ',INTREJ, ' TSFC OBSERVATIONS REJECTED OUT OF ', INTOBS
        WRITE(*,123) 'BGCDATA:   ',INDREJ, ' DPDS OBSERVATIONS REJECTED OUT OF ', INDOBS
        WRITE(*,*)' '
      ENDIF

123   FORMAT(2X,A,I0,A,I0)

      WRITE(*,*)' '
      WRITE(*,*)' ---------------------------'
      WRITE(*,*)'           DONE BGCDATA     '
      WRITE(*,*)' ---------------------------'
      WRITE(*,*)' '
      if ( icoun .gt. 0) then
         write(*,*) ' icoun meanfge=',icoun,zsum/icoun
         write(*,*) ' icoun meanzoer',icoun,zsumo/icoun
      endif
      RETURN
      END SUBROUTINE BGCDATA
