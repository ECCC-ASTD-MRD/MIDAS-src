      SUBROUTINE SETFGEGPS(lcolumn,lcolumng,lobsSpaceData)
!*
!***s/r  -SETFGEGPS Sets first-guess error for all GB-GPS ZTD observations
!*
!*Author  : S. Macpherson *ARMA/MSC  December 2004
!*
!*    -------------------
!**    Purpose: Set FGE for all GPS ZTD observations using
!*              adjoint of gpsztdop to get Jacobians
!*
      use EarthConstants_mod
      use MathPhysConstants_mod
      use bufr
      use columnData_mod
      use obsSpaceData_mod
      use verticalCoord_mod
      IMPLICIT NONE
#include "pardim.cdk"
#include "comgpsgb.cdk"
      type(struct_columnData) :: lcolumn,lcolumng
      type(struct_obs) :: lobsSpaceData
      type(struct_vco), pointer :: vco_anl
      REAL*8 ZLAT
      REAL*8 ZLON
      REAL*8, allocatable :: ZETA(:)
      REAL*8, allocatable :: ZTT (:)
      REAL*8, allocatable :: ZHU (:)
      REAL*8, allocatable :: ZGZ (:)
      REAL*8, allocatable :: ZTTB (:)
      REAL*8, allocatable :: ZHUB (:)
      REAL*8 ZP0
      REAL*8 ZP0B
      REAL*8 ZPT
      REAL*8 ZMT
      REAL*8 ZP0S
      REAL*8, allocatable :: ZTTS(:), ZHUS(:), ZGZS(:)

      REAL*8 ZOER, ZINC, ZHX, ZLEV
      REAL*8 ZDZ
      REAL*8 ZHXB, ZVAR

      REAL*8, allocatable :: ZJTT (:)
      REAL*8, allocatable :: ZJHU (:)
      REAL*8 ZJP0, ZLSUM

      INTEGER INDEX_HEADER,FIRST_HEADER
      INTEGER IDATYP,ITYP
      INTEGER IDATA,IDATEND,INDEX_BODY
      INTEGER JL, JK

      LOGICAL  ASSIM, LLSHIFT, LLOK

      INTEGER NH, NH1
!C
!C     * 1.  Initializations
!C     *     ---------------
!C
      allocate(ZETA(col_getNumLev(lcolumng)))
      allocate(ZTT(col_getNumLev(lcolumng)))
      allocate(ZHU(col_getNumLev(lcolumng)))
      allocate(ZGZ(col_getNumLev(lcolumng)))
      allocate(ZTTB(col_getNumLev(lcolumng)))
      allocate(ZHUB(col_getNumLev(lcolumng)))
      allocate(ZTTS(col_getNumLev(lcolumng)))
      allocate(ZHUS(col_getNumLev(lcolumng)))
      allocate(ZGZS(col_getNumLev(lcolumng)))
      allocate(ZJTT(col_getNumLev(lcolumng)))
      allocate(ZJHU(col_getNumLev(lcolumng)))
!C
!C     *    .   1.1  Eta vector
!C     *    .        ----------
!C      
      vco_anl => col_getVco(lcolumng)
      DO JL = 1, col_getNumLev(LCOLUMNG)
         ZETA(JL) = vco_anl%DHYB_M(JL)
      ENDDO
!C
      first_header=-1
!C
      ! loop over all header indices of the 'GP' family
      call obs_set_current_header_list(lobsSpaceData,'GP')
      HEADER: do
         index_header = obs_getHeaderIndex(lobsSpaceData)
         if (index_header < 0) exit HEADER
         if(first_header.eq.-1) first_header=index_header
!C     
!C     *    .   Process only zenith delay data (codtyp 189 and BUFR_NEZD)
!C
               IDATYP = obs_elem_i(lobsSpaceData,'ITY ',INDEX_HEADER)
               IF ( IDATYP .EQ. 189 ) THEN
!C
!C                 Loop over data in the observations
!C
                  IDATA   = obs_elem_i(lobsSpaceData,'RLN ',INDEX_HEADER)
                  IDATEND = obs_elem_i(lobsSpaceData,'NLV ',INDEX_HEADER) + IDATA - 1
                  ASSIM = .FALSE.
!C
!C                 Scan for requested assimilations, and count them.
!C
                  NH = 0
                  DO INDEX_BODY= IDATA, IDATEND
                     ITYP = obs_elem_i(lobsSpaceData,'VNM ',INDEX_BODY)
                     LLOK = ( (ITYP .EQ. BUFR_NEZD) .AND. (obs_elem_i(lobsSpaceData,'ASS ',INDEX_BODY) .EQ. 1) )
                     IF ( LLOK ) THEN
                        ASSIM = .TRUE.
                        NH = NH + 1
                     ENDIF
                  ENDDO
!C
!C     *           If assimilations are requested, apply the AD observation operator
!C
                  IF (ASSIM) THEN
!C     
!C     *        LR background profile and background errors at the observation location x :
!C
                     ZLAT = obs_elem_r(lobsSpaceData,'LAT ',INDEX_HEADER) &
                                                      * MPC_DEGREES_PER_RADIAN_R8
                     ZLON = obs_elem_r(lobsSpaceData,'LON ',INDEX_HEADER) &
                                                      * MPC_DEGREES_PER_RADIAN_R8
                     DO JL = 1, col_getNumLev(LCOLUMNG)
                       ZTTB(JL) = col_getElem(lcolumng,JL,INDEX_HEADER,'TT')
                       ZTT(JL)  = col_getElem(lcolumn,JL,INDEX_HEADER,'TT')
                       ZHUB(JL) = EXP(col_getElem(lcolumng,JL,INDEX_HEADER,'HU'))
                       ZHU(JL)  = col_getElem(lcolumn,JL,INDEX_HEADER,'HU')
                       ZGZ(JL)  = col_getElem(lcolumng,JL,INDEX_HEADER,'GZ')
                     ENDDO
                     ZP0B = col_getElem(lcolumng,1,INDEX_HEADER,'P0')
                     ZP0  = col_getElem(lcolumn,1,INDEX_HEADER,'P0')
                     ZPT  = col_getPressure(lcolumng,1,INDEX_HEADER,'NA')
                     ZMT  = ZGZ(col_getNumLev(LCOLUMNG))/GRAV
!C
!C     *        Call AD of ZTD observation operator to get Jacobians dH/dX
!C 
                     NH1 = 0
                     DO INDEX_BODY= IDATA, IDATEND
                        ITYP = obs_elem_i(lobsSpaceData,'VNM ',INDEX_BODY)
                        IF ( obs_elem_i(lobsSpaceData,'ASS ',INDEX_BODY).EQ.1 .AND. ITYP.EQ.BUFR_NEZD ) THEN
                           NH1 = NH1 + 1
!C
!C     *                    Observation error    SDERR
!c                           ZOER = obs_elem_r(lobsSpaceData,'PPP ',INDEX_BODY)
!C
                           ZHX = 1.0
!C
!C     *                    Observation height (m)
                           ZLEV = obs_elem_r(lobsSpaceData,'PPP ',INDEX_BODY)

                      CALL GPSZTDOPAD(ZLAT,ZLON,ZLEV,ZETA,ZTTB,ZHUB,ZP0B,ZPT,ZMT,ZGZ,ZHXB,ZJTT,ZJHU,ZJP0,ZHX)

!C                          dH/dQ ---> dH/d(lnQ)
                           ZJHU(:) = ZHUB(:)*ZJHU(:)
!C
!C     *        Compute the background ZTD error (HBHt)
                           ZLSUM = 0.0
                           DO JL = 1, col_getNumLev(LCOLUMNG)
                             ZLSUM = ZLSUM +  (ZJTT(JL)*ZTT(JL))**2 + (ZJHU(JL)*ZHU(JL))**2 
                           ENDDO
                           ZLSUM = ZLSUM + (ZJP0*ZP0)**2
                           call obs_set_r(lobsSpaceData,'HPHT',index_body,SQRT(ZLSUM))

                           IF (INDEX_HEADER.EQ.first_header .AND. NH1.LE.3) THEN
                            WRITE(*,'(A11,A9,3(1x,f7.2))')   &
                               'SETFGEGPS: ',obs_elem_c(lobsSpaceData,'STID',INDEX_HEADER),ZLAT,ZLON,ZLEV
                            WRITE(*,*) 'JL JACT JACQ FGE_T FGE_LQ'
                            DO JL = 1, col_getNumLev(LCOLUMNG)
                              WRITE(*,'(1X,I2,4(1x,E13.6))') JL,ZJTT(JL),ZJHU(JL)/ZHUB(JL),ZTT(JL), ZHU(JL)
                            ENDDO                         
                            WRITE(*,*) 'JACPS FGE_PS'
                            WRITE(*,'(2(1x,E13.6))') ZJP0, ZP0
                           ENDIF

                        ENDIF
                     ENDDO
                  ENDIF
               ENDIF

      ENDDO HEADER

      deallocate(ZETA)
      deallocate(ZTT)
      deallocate(ZHU)
      deallocate(ZGZ)
      deallocate(ZTTB)
      deallocate(ZHUB)
      deallocate(ZTTS)
      deallocate(ZHUS)
      deallocate(ZGZS)
      deallocate(ZJTT)
      deallocate(ZJHU)

      RETURN
      END SUBROUTINE SETFGEGPS
