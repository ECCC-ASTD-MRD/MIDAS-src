subroutine stat_avhrr(avhrr_in,avhrr_out)
! calcul de statistiques
! sur l'information sous-pixel AVHRR
use mod_tovs ,only : avhrr_var, avhrr_bgck_iasi, NIR, NVIS,NCLASSAVHRR
implicit none
type (avhrr_var) ,intent(in) :: avhrr_in
type (avhrr_bgck_iasi) ,intent(inout) :: avhrr_out
integer :: ICL,ICH
Real (8) :: SUMFRAC(NVIS+NIR),TBMIN(NVIS+1:NVIS+NIR),TBMAX(NVIS+1:NVIS+NIR),SUMTB(NVIS+1:NVIS+NIR),SUMTB2(NVIS+1:NVIS+NIR)
Real (8) :: SUMALB(1:NVIS),SUMALB2(1:NVIS)
!******************************************

SUMFRAC(:)=0.d0
SUMTB(:)=0.d0
SUMTB2(:)=0.d0
SUMALB(:)=0.d0
SUMALB2(:)=0.d0

DO ICL=1,NCLASSAVHRR
   IF (avhrr_in%CFRAC(ICL) > 0.d0 ) THEN

      DO ICH=1,NVIS
         IF (avhrr_out%ALBEDMOY(ICL,ICH) >=0.d0 ) THEN
            SUMFRAC(ICH)=SUMFRAC(ICH)+avhrr_in%CFRAC(ICL)
            SUMALB(ICH) = SUMALB(ICH) + avhrr_in%CFRAC(ICL) * avhrr_out%ALBEDMOY(ICL,ICH)
            SUMALB2(ICH) = SUMALB2(ICH) + avhrr_in%CFRAC(ICL) * ( avhrr_out%ALBEDMOY(ICL,ICH)**2 + avhrr_out%ALBEDSTD(ICL,ICH)**2)
         ENDIF
      ENDDO
      DO ICH=1+NVIS,NVIS+NIR
         IF (avhrr_out%TBMOY(ICL,ICH) > 0.d0 ) THEN
            SUMFRAC(ICH) = SUMFRAC(ICH) + avhrr_in%CFRAC(ICL)
            SUMTB(ICH) = SUMTB(ICH) + avhrr_in%CFRAC(ICL) * avhrr_out%TBMOY(ICL,ICH)
            SUMTB2(ICH) = SUMTB2(ICH) + avhrr_in%CFRAC(ICL) * (avhrr_out%TBMOY(ICL,ICH)**2 + avhrr_out%TBSTD(ICL,ICH)**2 )
         ENDIF
      ENDDO
   ENDIF
ENDDO

DO ICH=1,NVIS
   IF (SUMFRAC(ICH) >0.d0 ) THEN
      SUMALB(ICH) = SUMALB(ICH) / SUMFRAC(ICH)
      SUMALB2(ICH) = SUMALB2(ICH)/SUMFRAC(ICH) - SUMALB(ICH)**2
      IF (SUMALB2(ICH)>0.d0) THEN
         SUMALB2(ICH)=SQRT( SUMALB2(ICH) )
      ELSE
         SUMALB2(ICH)=0.d0
      ENDIF
   ENDIF
ENDDO

DO ICH=NVIS+1,NVIS+NIR
   IF (SUMFRAC(ICH) >0.d0 ) THEN
      SUMTB(ICH) = SUMTB(ICH) / SUMFRAC(ICH)
      SUMTB2(ICH) = SUMTB2(ICH)/SUMFRAC(ICH) - SUMTB(ICH)**2
      IF (SUMTB2(ICH)>0.d0) THEN
         SUMTB2(ICH)=SQRT ( SUMTB2(ICH) )
      ELSE
         SUMTB2(ICH)=0.d0
      ENDIF
   ENDIF
ENDDO

avhrr_out%TBSTD_PIXELIASI=SUMTB2
avhrr_out%ALBSTD_PIXELIASI=SUMALB2

end subroutine stat_avhrr
