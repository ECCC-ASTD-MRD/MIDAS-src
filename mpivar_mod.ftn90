module mpivar_mod
  use mpi_mod
  implicit none
  save
  private

  ! public procedures
  public :: mpivar_setup_latbands,mpivar_setup_m,mpivar_setup_levels

  ! public variables through inheritance
  public :: mpi_myid,mpi_nprocs,mpi_myidx,mpi_myidy
  ! public procedures through inheritance
  public :: mpi_initialize,mpi_getptopo

  contains


  subroutine mpivar_setup_latbands(nj,latPerPE,myLatBeg,myLatEnd,myLatHalfBeg,myLatHalfEnd)
    implicit none
    integer          :: nj,latPerPE,myLatBeg,myLatEnd,njlath
    integer,optional :: myLatHalfBeg,myLatHalfEnd

    latPerPE=ceiling(dble(nj)/dble(mpi_nprocs))
    myLatBeg=1+mpi_myid*latPerPE
    if(myLatBeg.gt.nj) then
      call abort3d('mpivar_setup_latbands: latitudes not separable on mpi processes!')
    endif
    myLatEnd=(1+mpi_myid)*latPerPE
    if(myLatEnd>nj) then
      call abort3d('mpivar_setup_latbands: latitudes not divisible by MPI numprocs!')
    endif

    if(present(myLatHalfBeg).and.present(myLatHalfEnd)) then
      njlath = (nj + 1)/2
      if(myLatBeg<=njlath .and. myLatEnd<=njlath) then
        myLatHalfBeg=myLatBeg
        myLatHalfEnd=myLatEnd
      elseif(myLatBeg>=njlath .and. myLatEnd>=njlath) then
        myLatHalfBeg=1+nj-myLatEnd
        myLatHalfEnd=1+nj-myLatBeg
      else
        myLatHalfBeg=min(myLatBeg,1+nj-myLatEnd)
        myLatHalfEnd=njlath
      endif
    endif

  end subroutine mpivar_setup_latbands


  subroutine mpivar_setup_m(ntrunc,mymBeg,mymEnd,mymSkip,mymCount)
    implicit none
    integer :: ntrunc,mymBeg,mymEnd,mymSkip,mymCount,jm

    mymBeg=mpi_myid
    mymEnd=ntrunc
    mymSkip=mpi_nprocs
    mymCount=0
    do jm=mymBeg,mymEnd,mymSkip
      mymCount=mymCount+1
    enddo

  end subroutine mpivar_setup_m

 
  subroutine mpivar_setup_levels(numlevels,myLevBeg,myLevEnd,myLevCount)
    implicit none
    integer :: numlevels,myLevBeg,myLevEnd,myLevCount
    integer :: jlev,jproc
    integer :: myLevCounts(mpi_nprocs)

    myLevCounts(:)=0
    do jproc=1,mpi_nprocs
      do jlev=jproc,numlevels,mpi_nprocs
        myLevCounts(jproc)=myLevCounts(jproc)+1
      enddo
    enddo
    myLevCount=myLevCounts(mpi_myid+1)

    myLevBeg=1
    do jproc=1,mpi_myid
      myLevBeg=myLevBeg+myLevCounts(jproc)
    enddo
    myLevEnd=myLevBeg+myLevCount-1

  end subroutine mpivar_setup_levels

 
end module mpivar_mod
