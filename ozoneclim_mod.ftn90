
Module ozoneclim

!#if defined (DOC)
!************************************************************************
!*
!*       MODULE OZONECLIM
!*       -----------------
!*
!*       PURPOSE: CLIMATOLOGICAL OZONE (1998)
!*
!*       AUTHOR:   A. BEAULNE (CMDA/SMC) May 2006
!*
!*       REVISION:
!*
!************************************************************************
!#endif

PUBLIC OZO_GET_PROFILE, OZO_READ_CLIMATOLOGY

PRIVATE


! Number of latitudes and vertical levels in climatology file

  INTEGER, PARAMETER    :: NLATO3=19, NLEVO3=28

! Climatological ozone field (ppmv) and total ozone 

  REAL                  :: FOZO_r4(NLATO3,NLEVO3), TOTOZO_r4(NLATO3,12)

! Pressure height of climatology file vertical levels (mb)

  REAL(8)               :: PO3(NLEVO3)

  DATA PO3 /    0.010D0, 0.015D0, 0.022D0, 0.032D0, 0.046D0, 0.068D0, 0.100D0,   &
       0.150D0, 0.200D0, 0.300D0, 0.500D0, 1.000D0, 2.000D0, 3.000D0,   &
       5.000D0, 7.000D0, 10.00D0, 20.00D0, 30.00D0, 50.00D0, 70.00D0,   &
       100.0D0, 150.0D0, 200.0D0, 300.0D0, 500.0D0, 700.0D0, 1000.D0 / 


contains

  SUBROUTINE OZO_GET_PROFILE(O3P,toto3obs,zlat,  &
       plev,nlev,nprf)

#if defined (DOC)
!***********************************************************************
!
!**ID OZO_GET_PROFILE -- GET OZONE PROFILE FROM CLIMATOLOGY INTERPOLATED TO DESIRED P LEVELS
!
!       AUTHOR:   A. BEAULNE (CMDA/SMC) March 2006
!
!       REVISION:
!
!       OBJECT:   GET OZONE PROFILE FROM CLIMATOLOGY INTERPOLATED TO DESIRED P LEVELS
!
!       ARGUMENTS:
!          INPUT:
!            -TOTO3OBS(NPRF)      : TOTAL OZONE IF KNOWN FROM OUTSIDE SOURCE SUCH AS TOMS
!            -ZLAT(NPRF)          : ARRAY OF LATITUDE (-90S TO 90N)
!            -PLEV(NLEV)          : PRESSURE LEVELS (HPA)
!            -NLEV                : NUMBER OF VERTICAL LEVELS
!            -NPRF                : NUMBER OF PROFILES
!
!          OUTPUT:
!            -O3P(NLEV,NPRF)      : OZONE PROFILES (PPMV)
!
!
***********************************************************************
#endif

use obsSpaceData_mod
use timeCoord_mod, only: tim_getDatestamp
IMPLICIT NONE       

integer ,intent(in) :: nlev,nprf
REAL(8),intent(in)  :: ZLAT(NPRF), TOTO3OBS(NPRF),PLEV(NLEV)
REAL(8),intent(out)  :: O3P(NLEV,NPRF)
!***************************************************************
INTEGER   :: JN, K, NUMLAT(NPRF)
INTEGER   :: IJOUR,ITIME,IJ,IMONTH,IER
REAL(8)   :: QO3B(NLEVO3,NPRF)
REAL(8)   :: PRO3(NLEVO3,NPRF)
INTEGER   :: NEWDATE


!* assign default qgas values if need be

DO JN = 1, NPRF
   NUMLAT(JN) = NINT( (ZLAT(JN)+90.D0) / (180.D0/(REAL(NLATO3-1,8))) ) + 1
   DO K = 1, NLEVO3
      QO3B(K,JN) = FOZO_r4(NUMLAT(JN),K)
   END DO
END DO

!* interpolation of field QO3B at NLEVO3 levels of height PO3mbb
!* into field O3P at NLEV levels of height PLEV

FORALL(K=1:NLEVO3) PRO3(K,:) = PO3(K)

CALL LINTV2(pro3,qo3b,nlevo3,nlevo3,nprf,nlev,plev,O3P)


!* if total climatological ozone is known from outside source
!* then set the climatological profile so that it matches that total

DO JN = 1, NPRF
   IF ( NINT(TOTO3OBS(JN)) /= 0 ) THEN
!      IJOUR = obs_headElem_i(lobsSpaceData,OBS_DAT,1)
      ier = newdate(tim_getDatestamp(),ijour,itime,-3)
      IJ = IJOUR/100
      IMONTH = IJ - (IJ/100)*100
      O3P(:,JN) = O3P(:,JN) * TOTO3OBS(JN) / TOTOZO_r4(NUMLAT(JN),IMONTH)
   END IF
ENDDO


END SUBROUTINE OZO_GET_PROFILE


SUBROUTINE OZO_READ_CLIMATOLOGY()

#if defined (DOC)
!***********************************************************************
!
!**ID OZONECLIM -- READ OZONE CLIMATOLOGICAL FIELDS
!
!       AUTHOR:   A. BEAULNE (CMDA/SMC) March 2006
!
!       REVISION:
!
!       OBJECT:   READ OZONE CLIMATOLOGICAL FIELDS AND PUT IN COMMON DECK
!
!       ARGUMENTS:
!          INPUT:  NONE
!          OUTPUT: NONE
!
!
!***********************************************************************
#endif

  use obsSpaceData_mod
  use timeCoord_mod, only: tim_getDatestamp

  IMPLICIT NONE

  INTEGER            :: IJOUR,ITIME,IMONTH,IJ,IV,K,IER
  CHARACTER(len=100) :: CFILE
  INTEGER            :: NIOZO,NJOZO,NKOZO
  INTEGER, EXTERNAL  :: FNOM,FSTOUV,FSTLIR,FSTFRM,FCLOS,NEWDATE

  integer            :: IOZTEST
  integer            :: iv1,iv2,iv3,iv4,iv5,iv6

!  IJOUR = obs_headElem_i(lobsSpaceData,OBS_DAT,1)
   ier = newdate(tim_getDatestamp(),ijour,itime,-3)

  IJ= IJOUR/100
  IMONTH = IJ - (IJ/100)*100

  ioztest=0

  CFILE='ozoneclim98'
  IV1=FNOM(IOZTEST,CFILE,'RND+R/O',0)
  IV2=FSTOUV(IOZTEST,'RND')
  IV3=FSTLIR(FOZO_r4,IOZTEST,NIOZO,NJOZO,NKOZO,-1,' ',-1,-1,IMONTH,' ','O3')
  IV4=FSTLIR(TOTOZO_r4,IOZTEST,NIOZO,NJOZO,NKOZO,-1,' ',-1,-1,-1,' ','TO')
  IV5=FSTFRM(IOZTEST)
  IV6=FCLOS(IOZTEST)

  if(iv1.lt.0.or.iv2.lt.0.or.iv3.lt.0.or.iv4.lt.0.or.iv5.lt.0.or.iv6.lt.0) then
     write(*,*) 'LES IV DE OZO_READ_CLIMATOLOGY ',iv1,iv2,iv3,iv4,iv5,iv6
     write(*,*) 'THESE NUMBERS SHOULD NOT BE NEGATIVE'
     call abort3d('Problem with file in ozo_read_climatology (ozoneclim_mod)')
  endif
    
END SUBROUTINE OZO_READ_CLIMATOLOGY



End module ozoneclim
