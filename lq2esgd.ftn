!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!
*
      subroutine lq2esgd(pesinc,statevector,statevectorg)
*
***S/R lq2esgd  -  Computes increments of ES=T-TD FROM T AND lnq
*                  increments in Grid-Point Space.
*
#if defined (DOC)
*
*Author: L. Fillion - ARMA/AES - 19 Nov 98 - Grid-Point version of
C LMHUAESV
*
*Revision:
*          Y.J. Rochon and Cecilien Charette - SMC - Sept 2004
*            - Conversion to hybrid vertical coordinate
*              Follow WMO convention on T-Td vs HU relationship)
*
*Arguments:
*
* Out:
*      pesinc : Dew-point depression increment on the analysis grid
* IN:
*      statevector    : increment
*      statevectorg   : Background state on analysis grid
*
*Object:   For Postprocessing analysis increment (called by DIAG3DVAR):
*          calculate the TLM OF dew point depression from TLM specific
*          humidity, temperature and pressure.  No ice phase is
*          CONSIDERED.
*
#endif
      use MathPhysConstants_mod
      use physicsFunctions_mod
      use gridStateVector_mod
      IMPLICIT NONE
      type(struct_gsv) :: statevector,statevectorg
      real*8 pesinc(statevectorg%ni,statevectorg%nlev,statevectorg%nj)
      integer ji,jj,jk
      REAL*8 ZE, ZEL, ZTD, ZTDL, ZGAMMA, zpres
      real*8, pointer :: ps_trl_ptr(:,:,:,:),ps_inc_ptr(:,:,:,:),hu_trl_ptr(:,:,:,:),hu_inc_ptr(:,:,:,:),tt_inc_ptr(:,:,:,:)
*
      WRITE(*,FMT='(/,4X,"Starting LQ2ESGD",//)')
      ps_trl_ptr => gsv_getField(statevectorg,'P0')
      ps_inc_ptr => gsv_getField(statevector ,'P0')
      hu_trl_ptr => gsv_getField(statevectorg,'HU')
      hu_inc_ptr => gsv_getField(statevector ,'HU')
      tt_inc_ptr => gsv_getField(statevector ,'TT')
*
!$OMP PARALLEL DO PRIVATE(jk,jj,ji,zpres,ze,zel,ztd,zgamma,ztdl)
      do jj=1,statevectorg%nj
        do jk=1,statevectorg%nlev
          do ji=1,statevectorg%ni
            zpres  = statevectorg%vco%da_M(jk) + statevectorg%vco%db_M(jk)*ps_trl_ptr(ji,1,jj,1)
*
*           First do the forward branch to get saturated vapour pressure
*           from q
*
            ZE = FOEFQ8(max(hu_trl_ptr(ji,jk,jj,1),1.d-12), zpres)
*
*           TLM of the saturated vapor pressure from q (specific humidity)
*
            ZEL = FOEFQL(hu_inc_ptr(ji,jk,jj,1),ps_inc_ptr(ji,1,jj,1)
     &           ,hu_trl_ptr(ji,jk,jj,1),zpres,statevectorg%vco%dhyb_m(jk))
*
*           TLM of the dewpoint temperature calculation
C           From Teten's relation
C
            ZTD=FOTW8(ZE)
            ZGAMMA=FODTW8(ZTD,ZE)
            ZTDL = ZGAMMA*ZEL

            pesinc(ji,jk,jj) = tt_inc_ptr(ji,jk,jj,1) - ZTDL

          enddo
        enddo
      enddo
!$OMP END PARALLEL DO

      RETURN
      END
