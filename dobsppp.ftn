!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer,
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer
!version 3 or (at your option) any later version that should be found at:
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software;
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec),
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!
      SUBROUTINE DOBSPPP(PJO,CDFAM,lcolumnhr,lobsSpaceData)
#if defined (DOC)
!
!**s/r DOBSPPP  - Computation of Jo and the residuals to the observations
!                 for pressure-level observations
!
!*    Purpose:  -Interpolate vertically lcolumnhr to
!                the pressure levels of the observations. Then compute Jo.
!                A linear interpolation in ln(p) is performed.
!
!Arguments
!     PJO:  CONTRIBUTION to Jo
!     CDFAM: FAMILY OF OBSSERVATION
!
#endif
      use MathPhysConstants_mod
      use obsSpaceData_mod
      use columnData_mod 
      use bufr
      IMPLICIT NONE
      REAL*8 PJO
      CHARACTER *2 CDFAM
      type(struct_columnData) :: lcolumnhr
      type(struct_obs) :: lobsSpaceData
      INTEGER IPB,IPT,ITY,IDBURP
      INTEGER IOBS,IPOS,IK,IBEGIN,ILAST
      INTEGER J,JDATA
      INTEGER IASS,IXTR,IVCO,IITY,IVNM
      REAL*8 ZVAR,ZOER,ZCON,ZINC,ZPHI
      REAL*8 ZWB,ZWT,ZEXP,ZGAMMA,ZTVG
      REAL*8 ZLEV,ZPT,ZPB,ZTORAD,ZOMA
      REAL*8 columnVarB,columnVarT,lqtoes
      REAL*4 ZLAT,ZLON
      LOGICAL LLPRINT,LLNOXTR
      CHARACTER*2 CVAR
!
!     Temperature lapse rate for extrapolation of gz below model surface
!
      LLPRINT   = .FALSE.
      LLNOXTR   = .FALSE.
      zgamma = 0.0065 / GRAV
      zexp = RGASD*zgamma
!
!     1. Computation of (HX - Z)/SIGMA
!
      PJO=0.
      do jdata=1,obs_numbody(lobsSpaceData)
         if(obs_getFamily(lobsSpaceData,bodyIndex=jdata).eq.CDFAM) then
!
! Process all data within the domain of the model
!
            IPOS=obs_elem_i (lobsSpaceData,'POS ',jdata)
            ZVAR=obs_elem_r8(lobsSpaceData,'VAR ',jdata)
            IVNM=obs_elem_i (lobsSpaceData,'VNM ',jdata)
            ZOER=obs_elem_r8(lobsSpaceData,'OER ',jdata)
            ZLEV=obs_elem_r8(lobsSpaceData,'PPP ',jdata)
            IK  =obs_elem_i (lobsSpaceData,'LYR ',jdata)
            iass=obs_elem_i (lobsSpaceData,'ASS ',jdata)
            ixtr=obs_elem_i (lobsSpaceData,'XTR ',jdata)
            ivco=obs_elem_i (lobsSpaceData,'VCO ',jdata)
            IOBS=obs_elem_i (lobsSpaceData,'OBS ',jdata)
            IF ( (iass.EQ.1).AND.(ixtr.EQ.0).AND.(ivco.EQ.2) ) THEN
               ITY =obs_elem_i (lobsSpaceData,'VNM ',jdata)
               IPT = IK + IPOS*LCOLUMNHR%NLEV
               IPB = IPT+1
               CALL GETVARTYPE(ITY,CVAR)
               IF (CVAR .EQ. 'NA') THEN
                 ZPT= LCOLUMNHR%RPPOBS(IK,IOBS)
                 ZPB= LCOLUMNHR%RPPOBS(IK+1,IOBS)
               ELSEIF (CVAR .EQ. 'TH') THEN
                 ZPT  = LCOLUMNHR%RPPOBS_T(IK,IOBS)
                 ZPB  = LCOLUMNHR%RPPOBS_T(IK+1,IOBS)
               ELSEIF (CVAR .EQ. 'MM' ) THEN
                 ZPT  = LCOLUMNHR%RPPOBS_M(IK,IOBS)
                 ZPB  = LCOLUMNHR%RPPOBS_M(IK+1,IOBS)
               ENDIF
               ZWB  = LOG(ZLEV/ZPT)/LOG(ZPB/ZPT)
               ZWT  = 1. - ZWB
               if(ivnm.eq.bufr_nees) then
                 columnVarB=lqtoes(lcolumnhr%hu(IK+1,IOBS),
     +              lcolumnhr%tt(IK+1,IOBS),lcolumnhr%rppobs_T(IK+1,IOBS))
                 columnVarT=lqtoes(lcolumnhr%hu(IK  ,IOBS),
     +              lcolumnhr%tt(IK  ,IOBS),lcolumnhr%rppobs_T(IK  ,IOBS))
               else
                 columnVarB=lcolumnhr%all(IPB,IOBS)
                 columnVarT=lcolumnhr%all(IPT,IOBS)
               endif
               zoma = (ZWB*columnVarB+ZWT*columnVarT-ZVAR)/ZOER
               PJO = PJO + zoma*zoma
               call obs_set_r8(lobsSpaceData,'OMA ',jdata,zoma)
               call obs_set_r8(lobsSpaceData,'OMI ',jdata,zoma)
!
! Process all upper air data data below model's orography
!
            ELSEIF ( (iass.EQ.1).AND.(ixtr.EQ.2).AND.(ivco.EQ.2) ) THEN
               IPT = LCOLUMNHR%NLEV-1 + IPOS*LCOLUMNHR%NLEV
               IPB = IPT+1
               IF(IVNM .EQ. BUFR_NEGZ ) THEN
!
! Forward nonlinear model for geopotential data below model's orography
!
                  ZTVG = (1.0 + DELTA * EXP(lcolumnhr%hu(LCOLUMNHR%NLEV,IOBS)))
     &                     *lcolumnhr%tt(LCOLUMNHR%NLEV,IOBS)
                  zoma = ( lcolumnhr%RMTMOBS(iobs) + ZTVG/zgamma
     &                     *(1.-(zlev/lcolumnhr%ps(1,iobs))**zexp) - zvar )/zoer
                  PJO =PJO+zoma*zoma
                  call obs_set_r8(lobsSpaceData,'OMA ',jdata,zoma)
                  call obs_set_r8(lobsSpaceData,'OMI ',jdata,zoma)
               ENDIF
            ENDIF
         ENDIF
      ENDDO

      IF(LLNOXTR) THEN
        WRITE(*,*)'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'
        WRITE(*,*)' Warning 3DV:dobsppp: NO EXTRAPOLATION'
     &       ,' ALLOWED SEE LISTING FOR MORE DETAILS'
        WRITE(*,*)'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'
      ENDIF

      RETURN
      END

